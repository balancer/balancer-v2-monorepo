{
  "language": "Solidity",
  "sources": {
    "contracts/vault/Vault.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"./interfaces/IAuthorizer.sol\";\nimport \"./interfaces/IWETH.sol\";\n\nimport \"./VaultAuthorization.sol\";\nimport \"./FlashLoanProvider.sol\";\nimport \"./Swaps.sol\";\n\n/**\n * @dev The `Vault` is Balancer V2's core contract. A single instance of it exists in the entire network, and it is the\n * entity used to interact with Pools by joining, exiting, or swapping with them.\n *\n * The `Vault`'s source code is split among a number of sub-contracts with the goal of improving readability and making\n * understanding the system easier. All sub-contracts have been marked as `abstract` to make explicit the fact that only\n * the full `Vault` is meant to be deployed.\n *\n * Roughly speaking, these are the contents of each sub-contract:\n *\n *  - `InternalBalance`: deposit, withdrawal and transfer of Internal Balance.\n *  - `Fees`: set and compute protocol fees.\n *  - `FlashLoanProvider`: flash loans.\n *  - `PoolRegistry`: Pool registration, joining, exiting, and Asset Manager interactions.\n *  - `Swaps`: Pool swaps.\n *\n * Additionally, the different Pool specializations are handled by the `GeneralPoolsBalance`,\n * `MinimalSwapInfoPoolsBalance` and `TwoTokenPoolsBalance` sub-contracts, which in turn make use of the\n * `BalanceAllocation` library.\n *\n * The most important goal of the `Vault` is to make token swaps use as little gas as possible. This is reflected in a\n * multitude of design decisions, from minor things like the format used to store Pool IDs, to major features such as\n * the different Pool specialization settings.\n *\n * Finally, the large number of tasks carried out by the Vault mean its bytecode is very large, which is at odds with\n * the contract size limit imposed by EIP 170 (https://eips.ethereum.org/EIPS/eip-170). Manual tuning of the source code\n * was required to improve code generation and bring the bytecode size below this limit. This includes extensive\n * utilization of `internal` functions (particularly inside modifiers), usage of named return arguments, and dedicated\n * storage access methods, to name a few.\n */\ncontract Vault is VaultAuthorization, FlashLoanProvider, Swaps {\n    constructor(\n        IAuthorizer authorizer,\n        IWETH weth,\n        uint256 emergencyPeriod,\n        uint256 emergencyPeriodCheckExtension\n    )\n        VaultAuthorization(authorizer)\n        AssetTransfersHandler(weth)\n        EmergencyPeriod(emergencyPeriod, emergencyPeriodCheckExtension)\n    {\n        // solhint-disable-previous-line no-empty-blocks\n    }\n\n    function setEmergencyPeriod(bool active) external authenticate {\n        _setEmergencyPeriod(active);\n    }\n}\n"
    },
    "contracts/vault/interfaces/IAuthorizer.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\ninterface IAuthorizer {\n    function hasRole(bytes32 role, address account) external view returns (bool);\n}\n"
    },
    "contracts/vault/interfaces/IWETH.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\ninterface IWETH is IERC20 {\n    function deposit() external payable;\n\n    function withdraw(uint256 amount) external;\n}\n"
    },
    "contracts/vault/VaultAuthorization.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"../lib/helpers/Authentication.sol\";\nimport \"../lib/helpers/EmergencyPeriod.sol\";\nimport \"../lib/helpers/ReentrancyGuard.sol\";\n\nimport \"./interfaces/IVault.sol\";\nimport \"./interfaces/IAuthorizer.sol\";\n\nabstract contract VaultAuthorization is IVault, Authentication, EmergencyPeriod, ReentrancyGuard {\n    IAuthorizer private _authorizer;\n    mapping(address => mapping(address => bool)) private _allowedRelayers;\n\n    /**\n     * @dev Reverts unless `user` has allowed the caller as a relayer, and the caller is allowed by the Authorizer to\n     * call this function. Should only be applied to external functions.\n     */\n    modifier authenticateFor(address user) {\n        _authenticateCallerFor(user);\n        _;\n    }\n\n    constructor(IAuthorizer authorizer) {\n        _authorizer = authorizer;\n    }\n\n    function changeAuthorizer(IAuthorizer newAuthorizer) external override nonReentrant authenticate {\n        _authorizer = newAuthorizer;\n    }\n\n    function getAuthorizer() external view override returns (IAuthorizer) {\n        return _authorizer;\n    }\n\n    function changeRelayerAllowance(address relayer, bool allowed) external override nonReentrant noEmergencyPeriod {\n        _allowedRelayers[msg.sender][relayer] = allowed;\n    }\n\n    function hasAllowedRelayer(address user, address relayer) external view override returns (bool) {\n        return _hasAllowedRelayer(user, relayer);\n    }\n\n    /**\n     * @dev Reverts unless  `user` has allowed the caller as a relayer, and the caller is allowed by the Authorizer to\n     * call the entry point function.\n     */\n    function _authenticateCallerFor(address user) internal view {\n        if (msg.sender != user) {\n            _authenticateCaller();\n            require(_hasAllowedRelayer(user, msg.sender), \"USER_DOESNT_ALLOW_RELAYER\");\n        }\n    }\n\n    function _hasAllowedRelayer(address user, address relayer) internal view returns (bool) {\n        return _allowedRelayers[user][relayer];\n    }\n\n    function _canPerform(bytes32 roleId, address user) internal view override returns (bool) {\n        return _authorizer.hasRole(roleId, user);\n    }\n}\n"
    },
    "contracts/vault/FlashLoanProvider.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\n// This flash loan provider was based on the Aave protocol's open source\n// implementation and terminology and interfaces are intentionally kept\n// similar\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/SafeERC20.sol\";\n\nimport \"../lib/helpers/ReentrancyGuard.sol\";\n\nimport \"./Fees.sol\";\nimport \"./interfaces/IFlashLoanReceiver.sol\";\n\nabstract contract FlashLoanProvider is ReentrancyGuard, Fees {\n    using SafeMath for uint256;\n    using SafeERC20 for IERC20;\n\n    function flashLoan(\n        IFlashLoanReceiver receiver,\n        IERC20[] memory tokens,\n        uint256[] memory amounts,\n        bytes calldata receiverData\n    ) external override nonReentrant noEmergencyPeriod {\n        /*\n        InputHelpers.ensureInputLengthMatch(tokens.length, amounts.length);\n\n        uint256[] memory feeAmounts = new uint256[](tokens.length);\n        uint256[] memory preLoanBalances = new uint256[](tokens.length);\n\n        IERC20 previousToken = IERC20(0);\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            uint256 amount = amounts[i];\n\n            require(token > previousToken, \"UNSORTED_TOKENS\"); // Prevents duplicate tokens\n            previousToken = token;\n\n            // Not checking amount against current balance, transfer will revert if it is exceeded\n            preLoanBalances[i] = token.balanceOf(address(this));\n            feeAmounts[i] = _calculateProtocolFlashLoanFeeAmount(amount);\n\n            token.safeTransfer(address(receiver), amount);\n        }\n\n        receiver.receiveFlashLoan(tokens, amounts, feeAmounts, receiverData);\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            uint256 preLoanBalance = preLoanBalances[i];\n\n            uint256 postLoanBalance = token.balanceOf(address(this));\n            require(postLoanBalance >= preLoanBalance, \"INVALID_POST_LOAN_BALANCE\");\n\n            uint256 receivedFees = postLoanBalance - preLoanBalance;\n            require(receivedFees >= feeAmounts[i], \"INSUFFICIENT_COLLECTED_FEES\");\n\n            _increaseCollectedFees(token, receivedFees);\n        }\n        */\n    }\n}\n"
    },
    "contracts/vault/Swaps.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/SafeERC20.sol\";\nimport \"@openzeppelin/contracts/utils/EnumerableSet.sol\";\n\nimport \"../lib/math/Math.sol\";\nimport \"../lib/helpers/EnumerableMap.sol\";\nimport \"../lib/helpers/InputHelpers.sol\";\nimport \"../lib/helpers/ReentrancyGuard.sol\";\n\nimport \"./PoolRegistry.sol\";\nimport \"./interfaces/IPoolSwapStructs.sol\";\nimport \"./interfaces/IGeneralPool.sol\";\nimport \"./interfaces/IMinimalSwapInfoPool.sol\";\nimport \"./balances/BalanceAllocation.sol\";\n\nabstract contract Swaps is ReentrancyGuard, PoolRegistry {\n    using SafeERC20 for IERC20;\n    using EnumerableSet for EnumerableSet.AddressSet;\n    using EnumerableMap for EnumerableMap.IERC20ToBytes32Map;\n\n    using Math for int256;\n    using Math for uint256;\n    using SafeCast for uint256;\n    using BalanceAllocation for bytes32;\n\n    // Despite the external API having two separate functions for given in and given out, internally their are handled\n    // together to avoid unnecessary code duplication. This enum indicates which kind of swap we're processing.\n\n    // We use inline assembly to convert arrays of different struct types that have the same underlying data\n    // representation. This doesn't trigger any actual conversions or runtime analysis: it is just coercing the type\n    // system to reinterpret the data as another type.\n\n    /**\n     * @dev Converts an array of `SwapIn` into an array of `SwapRequest`, with no runtime cost.\n     */\n    function _toInternalSwap(SwapIn[] memory swapsIn) private pure returns (SwapRequest[] memory internalSwapRequests) {\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            internalSwapRequests := swapsIn\n        }\n    }\n\n    /**\n     * @dev Converts an array of `SwapOut` into an array of `InternalSwap`, with no runtime cost.\n     */\n    function _toInternalSwap(SwapOut[] memory swapsOut)\n        private\n        pure\n        returns (SwapRequest[] memory internalSwapRequests)\n    {\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            internalSwapRequests := swapsOut\n        }\n    }\n\n    // This struct is identical in layout to SwapRequestGivenIn and SwapRequestGivenOut from IPoolSwapStructs, except\n    // the 'amountIn/Out' is named 'amount'.\n    struct InternalSwapRequest {\n        IERC20 tokenIn;\n        IERC20 tokenOut;\n        uint256 amount;\n        bytes32 poolId;\n        uint256 latestBlockNumberUsed;\n        address from;\n        address to;\n        bytes userData;\n    }\n\n    /**\n     * @dev Converts an InternalSwapRequest into a SwapRequestGivenIn, with no runtime cost.\n     */\n    function _toSwapRequestGivenIn(InternalSwapRequest memory internalSwapRequest)\n        private\n        pure\n        returns (IPoolSwapStructs.SwapRequestGivenIn memory swapRequestGivenIn)\n    {\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            swapRequestGivenIn := internalSwapRequest\n        }\n    }\n\n    /**\n     * @dev Converts an InternalSwapRequest into a SwapRequestGivenOut, with no runtime cost.\n     */\n    function _toSwapRequestGivenOut(InternalSwapRequest memory internalSwapRequest)\n        private\n        pure\n        returns (IPoolSwapStructs.SwapRequestGivenOut memory swapRequestGivenOut)\n    {\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            swapRequestGivenOut := internalSwapRequest\n        }\n    }\n\n    function batchSwapGivenIn(\n        SwapIn[] memory swaps,\n        IAsset[] memory assets,\n        FundManagement memory funds,\n        int256[] memory limits,\n        uint256 deadline\n    ) external payable override nonReentrant noEmergencyPeriod authenticateFor(funds.sender) returns (int256[] memory) {\n        return _batchSwap(_toInternalSwap(swaps), assets, funds, limits, deadline, SwapKind.GIVEN_IN);\n    }\n\n    function batchSwapGivenOut(\n        SwapOut[] memory swaps,\n        IAsset[] memory assets,\n        FundManagement memory funds,\n        int256[] memory limits,\n        uint256 deadline\n    ) external payable override nonReentrant noEmergencyPeriod authenticateFor(funds.sender) returns (int256[] memory) {\n        return _batchSwap(_toInternalSwap(swaps), assets, funds, limits, deadline, SwapKind.GIVEN_OUT);\n    }\n\n    /**\n     * @dev Implements both `batchSwapGivenIn` and `batchSwapGivenIn`, depending on the `kind` value.\n     */\n    function _batchSwap(\n        SwapRequest[] memory swaps,\n        IAsset[] memory assets,\n        FundManagement memory funds,\n        int256[] memory limits,\n        uint256 deadline,\n        SwapKind kind\n    ) private returns (int256[] memory assetDeltas) {\n        // The deadline is timestamp-based: it should not be relied on having sub-minute accuracy.\n        // solhint-disable-next-line not-rely-on-time\n        require(block.timestamp <= deadline, \"SWAP_DEADLINE\");\n\n        InputHelpers.ensureInputLengthMatch(assets.length, limits.length);\n\n        // Perform the swaps, updating the Pool token balances and computing the net Vault asset deltas.\n        assetDeltas = _swapWithPools(swaps, assets, funds, kind);\n\n        // Process asset deltas, by either transferring tokens from the sender (for positive deltas) or to the recipient\n        // (for negative deltas).\n\n        bool ethAssetSeen = false;\n        uint256 wrappedETH = 0;\n\n        for (uint256 i = 0; i < assets.length; ++i) {\n            IAsset asset = assets[i];\n            int256 delta = assetDeltas[i];\n\n            require(delta <= limits[i], \"SWAP_LIMIT\");\n\n            bool isETH = _isETH(asset);\n            if (isETH) {\n                ethAssetSeen = true;\n            }\n\n            if (delta > 0) {\n                uint256 toReceive = uint256(delta);\n                _receiveAsset(asset, toReceive, funds.sender, funds.fromInternalBalance);\n\n                if (isETH) {\n                    wrappedETH = wrappedETH.add(toReceive);\n                }\n            } else if (delta < 0) {\n                uint256 toSend = uint256(-delta);\n\n                // Withdraw fees are not charged when sending funds as part of a swap.\n                // Deposits to Internal Balance are also exempt of this fee during the current block.\n                _sendAsset(asset, toSend, funds.recipient, funds.toInternalBalance, false);\n            }\n        }\n\n        // We prevent user error by reverting if ETH was sent but not referenced by any asset.\n        _ensureNoUnallocatedETH(ethAssetSeen);\n\n        // By returning the excess ETH, we also check that at least wrappedETH has been received.\n        _returnExcessEthToCaller(wrappedETH);\n    }\n\n    // For `_swapWithPools` to handle both given in and given out swaps, it internally tracks the 'given' amount\n    // (supplied by the caller), and the 'calculated' one (returned by the Pool in response to the swap request).\n\n    /**\n     * @dev Given the two swap tokens and the swap kind, returns which one is the 'given' token (the one for which the\n     * amount is supplied by the caller).\n     */\n    function _tokenGiven(\n        SwapKind kind,\n        IERC20 tokenIn,\n        IERC20 tokenOut\n    ) private pure returns (IERC20) {\n        return kind == SwapKind.GIVEN_IN ? tokenIn : tokenOut;\n    }\n\n    /**\n     * @dev Given the two swap tokens and the swap kind, returns which one is the 'calculated' token (the one for\n     * which the amount is calculated by the Pool).\n     */\n    function _tokenCalculated(\n        SwapKind kind,\n        IERC20 tokenIn,\n        IERC20 tokenOut\n    ) private pure returns (IERC20) {\n        return kind == SwapKind.GIVEN_IN ? tokenOut : tokenIn;\n    }\n\n    /**\n     * @dev Returns an ordered pair (amountIn, amountOut) given the amounts given and calculated and the swap kind.\n     */\n    function _getAmounts(\n        SwapKind kind,\n        uint256 amountGiven,\n        uint256 amountCalculated\n    ) private pure returns (uint256 amountIn, uint256 amountOut) {\n        if (kind == SwapKind.GIVEN_IN) {\n            (amountIn, amountOut) = (amountGiven, amountCalculated);\n        } else {\n            (amountIn, amountOut) = (amountCalculated, amountGiven);\n        }\n    }\n\n    // This struct helps implement the multihop logic: if the amount given is not provided for a swap, then the given\n    // token must match the previous calculated token, and the previous calculated amount becomes the new given amount.\n    // For swaps of kind given in, amount in and token in are given, while amount out and token out are calculated.\n    // For swaps of kind given out, amount out and token out are given, while amount in and token in are calculated.\n    struct LastSwapData {\n        IERC20 tokenCalculated;\n        uint256 amountCalculated;\n    }\n\n    /**\n     * @dev Performs all `swaps`, calling swap hooks on the Pool contracts and updating their balances. Does not cause\n     * any transfer of tokens - it instead returns the net Vault token deltas: positive if the Vault should receive\n     * tokens, and negative if it should send them.\n     */\n    function _swapWithPools(\n        SwapRequest[] memory swaps,\n        IAsset[] memory assets,\n        FundManagement memory funds,\n        SwapKind kind\n    ) private returns (int256[] memory assetDeltas) {\n        assetDeltas = new int256[](assets.length);\n\n        // Passed to _swapWithPool, which stores data about the previous swap here to implement multihop logic across\n        // swaps.\n        LastSwapData memory previous;\n\n        // This variable could be declared inside the loop, but that causes the compiler to allocate memory on each loop\n        // iteration, increasing gas costs.\n        SwapRequest memory swap;\n        for (uint256 i = 0; i < swaps.length; ++i) {\n            swap = swaps[i];\n            _ensureRegisteredPool(swap.poolId);\n            require(swap.tokenInIndex < assets.length && swap.tokenOutIndex < assets.length, \"OUT_OF_BOUNDS\");\n\n            IERC20 tokenIn = _translateToIERC20(assets[swap.tokenInIndex]);\n            IERC20 tokenOut = _translateToIERC20(assets[swap.tokenOutIndex]);\n            require(tokenIn != tokenOut, \"CANNOT_SWAP_SAME_TOKEN\");\n\n            // Sentinel value for multihop logic\n            if (swap.amount == 0) {\n                // When the amount given is zero, we use the calculated amount for the previous swap, as long as the\n                // current swap's given token is the previous' calculated token. This makes it possible to e.g. swap a\n                // given amount of token A for token B, and then use the resulting token B amount to swap for token C.\n                if (swaps.length > 1) {\n                    bool usingPreviousToken = previous.tokenCalculated == _tokenGiven(kind, tokenIn, tokenOut);\n                    require(usingPreviousToken, \"MALCONSTRUCTED_MULTIHOP_SWAP\");\n                    swap.amount = previous.amountCalculated;\n                } else {\n                    revert(\"UNKNOWN_AMOUNT_IN_FIRST_SWAP\");\n                }\n            }\n\n            (uint256 amountIn, uint256 amountOut) = _swapWithPool(\n                tokenIn,\n                tokenOut,\n                swap,\n                funds.sender,\n                funds.recipient,\n                previous,\n                kind\n            );\n\n            // Accumulate Vault deltas across swaps\n            assetDeltas[swap.tokenInIndex] = assetDeltas[swap.tokenInIndex].add(amountIn.toInt256());\n            assetDeltas[swap.tokenOutIndex] = assetDeltas[swap.tokenOutIndex].sub(amountOut.toInt256());\n\n            emit Swap(swap.poolId, tokenIn, tokenOut, amountIn, amountOut);\n        }\n    }\n\n    /**\n     * @dev Performs `swap`, calling the Pool's contract hook and updating the Pool balance. Returns a pair with the\n     * amount of tokens going into and out of the Vault as a result of this swap.\n     *\n     * This function expects to be called with the `previous` swap struct, which will be updated internally to\n     * implement multihop logic.\n     */\n    function _swapWithPool(\n        IERC20 tokenIn,\n        IERC20 tokenOut,\n        SwapRequest memory swap,\n        address from,\n        address to,\n        LastSwapData memory previous,\n        SwapKind kind\n    ) private returns (uint256 amountIn, uint256 amountOut) {\n        InternalSwapRequest memory request = InternalSwapRequest({\n            tokenIn: tokenIn,\n            tokenOut: tokenOut,\n            amount: swap.amount,\n            poolId: swap.poolId,\n            latestBlockNumberUsed: 0, // will be updated later on based on the pool specialization\n            from: from,\n            to: to,\n            userData: swap.userData\n        });\n\n        // Get the calculated amount from the Pool and update its balances\n        uint256 amountCalculated = _processSwapRequest(request, kind);\n\n        // Store swap information for next swap\n        previous.tokenCalculated = _tokenCalculated(kind, tokenIn, tokenOut);\n        previous.amountCalculated = amountCalculated;\n\n        (amountIn, amountOut) = _getAmounts(kind, swap.amount, amountCalculated);\n    }\n\n    /**\n     * @dev Calls the swap hook on the Pool and updates its balances as a result of the swap being executed. The\n     * interface used for the call will depend on the Pool's specialization setting.\n     *\n     * Returns the token amount calculated by the Pool.\n     */\n    function _processSwapRequest(InternalSwapRequest memory request, SwapKind kind) private returns (uint256) {\n        address pool = _getPoolAddress(request.poolId);\n        PoolSpecialization specialization = _getPoolSpecialization(request.poolId);\n\n        if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            return _processMinimalSwapInfoPoolSwapRequest(request, IMinimalSwapInfoPool(pool), kind);\n        } else if (specialization == PoolSpecialization.TWO_TOKEN) {\n            return _processTwoTokenPoolSwapRequest(request, IMinimalSwapInfoPool(pool), kind);\n        } else {\n            return _processGeneralPoolSwapRequest(request, IGeneralPool(pool), kind);\n        }\n    }\n\n    function _processTwoTokenPoolSwapRequest(\n        InternalSwapRequest memory request,\n        IMinimalSwapInfoPool pool,\n        SwapKind kind\n    ) private returns (uint256 amountCalculated) {\n        // Due to gas efficiency reasons, this function uses low-level knowledge of how Two Token Pool balances are\n        // stored internally, instead of using getters and setters for all operations.\n\n        (\n            bytes32 tokenABalance,\n            bytes32 tokenBBalance,\n            TwoTokenPoolBalances storage poolBalances\n        ) = _getTwoTokenPoolSharedBalances(request.poolId, request.tokenIn, request.tokenOut);\n\n        // We have the two Pool balances, but we don't know which one is the token in and which one is the token out.\n        bytes32 tokenInBalance;\n        bytes32 tokenOutBalance;\n\n        // In Two Token Pools, token A has a smaller address than token B\n        if (request.tokenIn < request.tokenOut) {\n            // in is A, out is B\n            tokenInBalance = tokenABalance;\n            tokenOutBalance = tokenBBalance;\n        } else {\n            // in is B, out is A\n            tokenOutBalance = tokenABalance;\n            tokenInBalance = tokenBBalance;\n        }\n\n        // Perform the swap request and compute the new balances for token in and token out after the swap\n        (tokenInBalance, tokenOutBalance, amountCalculated) = _callMinimalSwapInfoPoolOnSwapHook(\n            request,\n            pool,\n            kind,\n            tokenInBalance,\n            tokenOutBalance\n        );\n\n        // We check the token ordering again to create the new shared cash packed struct\n        poolBalances.sharedCash = request.tokenIn < request.tokenOut\n            ? BalanceAllocation.toSharedCash(tokenInBalance, tokenOutBalance) // in is A, out is B\n            : BalanceAllocation.toSharedCash(tokenOutBalance, tokenInBalance); // in is B, out is A\n    }\n\n    function _processMinimalSwapInfoPoolSwapRequest(\n        InternalSwapRequest memory request,\n        IMinimalSwapInfoPool pool,\n        SwapKind kind\n    ) private returns (uint256 amountCalculated) {\n        bytes32 tokenInBalance = _getMinimalSwapInfoPoolBalance(request.poolId, request.tokenIn);\n        bytes32 tokenOutBalance = _getMinimalSwapInfoPoolBalance(request.poolId, request.tokenOut);\n\n        // Perform the swap request and compute the new balances for token in and token out after the swap\n        (tokenInBalance, tokenOutBalance, amountCalculated) = _callMinimalSwapInfoPoolOnSwapHook(\n            request,\n            pool,\n            kind,\n            tokenInBalance,\n            tokenOutBalance\n        );\n\n        _minimalSwapInfoPoolsBalances[request.poolId][request.tokenIn] = tokenInBalance;\n        _minimalSwapInfoPoolsBalances[request.poolId][request.tokenOut] = tokenOutBalance;\n    }\n\n    /**\n     * @dev Calls the onSwap hook for a Pool that implements IMinimalSwapInfoPool, which are both minimal swap info\n     * pools and two token pools.\n     */\n    function _callMinimalSwapInfoPoolOnSwapHook(\n        InternalSwapRequest memory request,\n        IMinimalSwapInfoPool pool,\n        SwapKind kind,\n        bytes32 tokenInBalance,\n        bytes32 tokenOutBalance\n    )\n        internal\n        returns (\n            bytes32 newTokenInBalance,\n            bytes32 newTokenOutBalance,\n            uint256 amountCalculated\n        )\n    {\n        uint256 tokenInTotal = tokenInBalance.total();\n        uint256 tokenOutTotal = tokenOutBalance.total();\n        request.latestBlockNumberUsed = Math.max(tokenInBalance.blockNumber(), tokenOutBalance.blockNumber());\n\n        // Perform the swap request callback and compute the new balances for token in and token out after the swap\n        if (kind == SwapKind.GIVEN_IN) {\n            IPoolSwapStructs.SwapRequestGivenIn memory swapIn = _toSwapRequestGivenIn(request);\n            uint256 amountOut = pool.onSwapGivenIn(swapIn, tokenInTotal, tokenOutTotal);\n\n            newTokenInBalance = tokenInBalance.increaseCash(request.amount);\n            newTokenOutBalance = tokenOutBalance.decreaseCash(amountOut);\n            amountCalculated = amountOut;\n        } else {\n            IPoolSwapStructs.SwapRequestGivenOut memory swapOut = _toSwapRequestGivenOut(request);\n            uint256 amountIn = pool.onSwapGivenOut(swapOut, tokenInTotal, tokenOutTotal);\n\n            newTokenInBalance = tokenInBalance.increaseCash(amountIn);\n            newTokenOutBalance = tokenOutBalance.decreaseCash(request.amount);\n            amountCalculated = amountIn;\n        }\n    }\n\n    function _processGeneralPoolSwapRequest(\n        InternalSwapRequest memory request,\n        IGeneralPool pool,\n        SwapKind kind\n    ) private returns (uint256 amountCalculated) {\n        bytes32 tokenInBalance;\n        bytes32 tokenOutBalance;\n\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[request.poolId];\n        uint256 indexIn = poolBalances.indexOf(request.tokenIn, \"TOKEN_NOT_REGISTERED\");\n        uint256 indexOut = poolBalances.indexOf(request.tokenOut, \"TOKEN_NOT_REGISTERED\");\n\n        uint256 tokenAmount = poolBalances.length();\n        uint256[] memory currentBalances = new uint256[](tokenAmount);\n\n        for (uint256 i = 0; i < tokenAmount; i++) {\n            // Because the iteration is bounded by `tokenAmount` and no tokens are registered or deregistered here, we\n            // can use `unchecked_valueAt` as we know `i` is a valid token index, saving storage reads.\n            bytes32 balance = poolBalances.unchecked_valueAt(i);\n\n            currentBalances[i] = balance.total();\n            request.latestBlockNumberUsed = Math.max(request.latestBlockNumberUsed, balance.blockNumber());\n\n            if (i == indexIn) {\n                tokenInBalance = balance;\n            } else if (i == indexOut) {\n                tokenOutBalance = balance;\n            }\n        }\n\n        // Perform the swap request callback and compute the new balances for token in and token out after the swap\n        if (kind == SwapKind.GIVEN_IN) {\n            IPoolSwapStructs.SwapRequestGivenIn memory swapRequestIn = _toSwapRequestGivenIn(request);\n            uint256 amountOut = pool.onSwapGivenIn(swapRequestIn, currentBalances, indexIn, indexOut);\n\n            amountCalculated = amountOut;\n            tokenInBalance = tokenInBalance.increaseCash(request.amount);\n            tokenOutBalance = tokenOutBalance.decreaseCash(amountOut);\n        } else {\n            IPoolSwapStructs.SwapRequestGivenOut memory swapRequestOut = _toSwapRequestGivenOut(request);\n            uint256 amountIn = pool.onSwapGivenOut(swapRequestOut, currentBalances, indexIn, indexOut);\n\n            amountCalculated = amountIn;\n            tokenInBalance = tokenInBalance.increaseCash(amountIn);\n            tokenOutBalance = tokenOutBalance.decreaseCash(request.amount);\n        }\n\n        // Because no token registrations or unregistrations happened between now and when we retrieved the indexes for\n        // token in and token out, we can use `unchecked_setAt`, saving storage reads.\n        poolBalances.unchecked_setAt(indexIn, tokenInBalance);\n        poolBalances.unchecked_setAt(indexOut, tokenOutBalance);\n    }\n\n    // This function is not marked as `nonReentrant` because the underlying mechanism relies on reentrancy\n    function queryBatchSwap(\n        SwapKind kind,\n        SwapRequest[] memory swaps,\n        IAsset[] memory assets,\n        FundManagement memory funds\n    ) external override returns (int256[] memory) {\n        // In order to accurately 'simulate' swaps, this function actually does perform the swaps, including calling the\n        // Pool hooks and updating  balances in storage. However, once it computes the final Vault Deltas it then\n        // reverts unconditionally, returning this array as the revert data.\n        // By wrapping this reverting call, we can decode the deltas 'returned' and return them as a normal Solidity\n        // function would. The only caveat is the function becomes non-view, but off-chain clients can still call it\n        // via eth_call to get the expected result.\n        //\n        // This technique was inspired by the work from the Gnosis team in the Gnosis Safe contract:\n        // https://github.com/gnosis/safe-contracts/blob/v1.2.0/contracts/GnosisSafe.sol#L265\n\n        // Most of this function is implemented using inline assembly, as the actual work it needs to do is not\n        // significant, and Solidity is not particularly well-suited to generate this behavior, resulting in a large\n        // amount of generated bytecode.\n\n        if (msg.sender != address(this)) {\n            // We perform an external call to ourselves, forwarding the same calldata. In this call, the else clause of\n            // the preceding if statement will be executed instead.\n\n            // solhint-disable-next-line avoid-low-level-calls\n            (bool success, ) = address(this).call(msg.data);\n\n            // solhint-disable-next-line no-inline-assembly\n            assembly {\n                // This call should always revert to decode the actual token deltas from the revert reason\n                switch success\n                    case 0 {\n                        // Note we are manually writing the memory slot 0. We can safely overwrite whatever is\n                        // stored there as we take full control of the execution and then immediately return.\n\n                        // We copy the first 4 bytes to check if it matches with the expected signature, otherwise\n                        // there was another revert reason and we should forward it.\n                        returndatacopy(0, 0, 0x04)\n                        let error := and(mload(0), 0xffffffff00000000000000000000000000000000000000000000000000000000)\n\n                        // If the first 4 bytes don't match with the expected signature, we forward the revert reason.\n                        if eq(eq(error, 0xfa61cc1200000000000000000000000000000000000000000000000000000000), 0) {\n                            returndatacopy(0, 0, returndatasize())\n                            revert(0, returndatasize())\n                        }\n\n                        // The returndata contains the signature, followed by the raw memory representation of an array:\n                        // length + data. We need to return an ABI-encoded representation of this array.\n                        // An ABI-encoded array contains an additional field when compared to its raw memory\n                        // representation: an offset to the location of the length. The offset itself is 32 bytes long,\n                        // so the smallest value we  can use is 32 for the data to be located immediately after it.\n                        mstore(0, 32)\n\n                        // We now copy the raw memory array from returndata into memory. Since the offset takes up 32\n                        // bytes, we start copying at address 0x20. We also get rid of the error signature, which takes\n                        // the first four bytes of returndata.\n                        let size := sub(returndatasize(), 0x04)\n                        returndatacopy(0x20, 0x04, size)\n\n                        // We finally return the ABI-encoded array, which has a total length equal to that of the array\n                        // (returndata), plus the 32 bytes for the offset.\n                        return(0, add(size, 32))\n                    }\n                    default {\n                        // This call should always revert, but we fail nonetheless if that didn't happen\n                        invalid()\n                    }\n            }\n        } else {\n            int256[] memory deltas = _swapWithPools(swaps, assets, funds, kind);\n\n            // solhint-disable-next-line no-inline-assembly\n            assembly {\n                // We will return a raw representation of the array in memory, which is composed of a 32 byte length,\n                // followed by the 32 byte int256 values. Because revert expects a size in bytes, we multiply the array\n                // length (stored at `deltas`) by 32.\n                let size := mul(mload(deltas), 32)\n\n                // We send one extra value for the error signature \"QueryError(int256[])\" which is 0xfa61cc12.\n                // We store it in the previous slot to the `deltas` array. We know there will be at least one available\n                // slot due to how the memory scratch space works.\n                // We can safely overwrite whatever is stored in this slot as we will revert immediately after that.\n                mstore(sub(deltas, 0x20), 0x00000000000000000000000000000000000000000000000000000000fa61cc12)\n                let start := sub(deltas, 0x04)\n\n                // When copying from `deltas` into returndata, we copy an additional 36 bytes to also return the array's\n                // length and the error signature.\n                revert(start, add(size, 36))\n            }\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/IERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP.\n */\ninterface IERC20 {\n    /**\n     * @dev Returns the amount of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the amount of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves `amount` tokens from the caller's account to `recipient`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transfer(address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through {transferFrom}. This is\n     * zero by default.\n     *\n     * This value changes when {approve} or {transferFrom} are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * IMPORTANT: Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address spender, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Moves `amount` tokens from `sender` to `recipient` using the\n     * allowance mechanism. `amount` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to {approve}. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n}\n"
    },
    "contracts/lib/helpers/Authentication.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nabstract contract Authentication {\n    /**\n     * @dev Reverts unless the caller is allowed to call this function. Should only be applied to external functions.\n     */\n    modifier authenticate() {\n        _authenticateCaller();\n        _;\n    }\n\n    /**\n     * @dev Reverts unless the caller is allowed to call the entry point function.\n     */\n    function _authenticateCaller() internal view {\n        // Each external function is dynamically assigned a role ID as the hash of the contract address\n        // and the function selector.\n        bytes32 roleId = keccak256(abi.encodePacked(address(this), msg.sig));\n        require(_canPerform(roleId, msg.sender), \"SENDER_NOT_ALLOWED\");\n    }\n\n    function _canPerform(bytes32 roleId, address user) internal view virtual returns (bool);\n}\n"
    },
    "contracts/lib/helpers/EmergencyPeriod.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\n// solhint-disable not-rely-on-time\ncontract EmergencyPeriod {\n    uint256 private constant _MAX_EMERGENCY_PERIOD = 90 days;\n    uint256 private constant _MAX_EMERGENCY_PERIOD_CHECK_EXT = 30 days;\n\n    bool private _emergencyPeriodActive;\n    uint256 internal immutable _emergencyPeriodEndDate;\n    uint256 internal immutable _emergencyPeriodCheckEndDate;\n\n    event EmergencyPeriodChanged(bool active);\n\n    modifier noEmergencyPeriod() {\n        _ensureInactiveEmergencyPeriod();\n        _;\n    }\n\n    constructor(uint256 emergencyPeriod, uint256 emergencyPeriodCheckExtension) {\n        require(emergencyPeriod <= _MAX_EMERGENCY_PERIOD, \"MAX_EMERGENCY_PERIOD\");\n        require(emergencyPeriodCheckExtension <= _MAX_EMERGENCY_PERIOD_CHECK_EXT, \"MAX_EMERGENCY_PERIOD_CHECK_EXT\");\n\n        _emergencyPeriodEndDate = block.timestamp + emergencyPeriod;\n        _emergencyPeriodCheckEndDate = block.timestamp + emergencyPeriod + emergencyPeriodCheckExtension;\n    }\n\n    function getEmergencyPeriod()\n        external\n        view\n        returns (\n            bool active,\n            uint256 endDate,\n            uint256 checkEndDate\n        )\n    {\n        return (!_isEmergencyPeriodInactive(), _emergencyPeriodEndDate, _emergencyPeriodCheckEndDate);\n    }\n\n    function _setEmergencyPeriod(bool active) internal {\n        require(block.timestamp < _emergencyPeriodEndDate, \"EMERGENCY_PERIOD_FINISHED\");\n        _emergencyPeriodActive = active;\n        emit EmergencyPeriodChanged(active);\n    }\n\n    function _ensureInactiveEmergencyPeriod() internal view {\n        require(_isEmergencyPeriodInactive(), \"EMERGENCY_PERIOD_ON\");\n    }\n\n    function _isEmergencyPeriodInactive() internal view returns (bool) {\n        return (block.timestamp >= _emergencyPeriodCheckEndDate) || !_emergencyPeriodActive;\n    }\n}\n"
    },
    "contracts/lib/helpers/ReentrancyGuard.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n// Based on the ReentrancyGuard library from OpenZeppelin contracts, altered to reduce bytecode size.\n// Modifier code is inlined by the compiler, which causes its code to appear multiple times in the codebase. By using\n// private functions, we achieve the same end result with slightly higher runtime gas costs but reduced bytecode size.\n\n/**\n * @dev Contract module that helps prevent reentrant calls to a function.\n *\n * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier\n * available, which can be applied to functions to make sure there are no nested\n * (reentrant) calls to them.\n *\n * Note that because there is a single `nonReentrant` guard, functions marked as\n * `nonReentrant` may not call one another. This can be worked around by making\n * those functions `private`, and then adding `external` `nonReentrant` entry\n * points to them.\n *\n * TIP: If you would like to learn more about reentrancy and alternative ways\n * to protect against it, check out our blog post\n * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].\n */\nabstract contract ReentrancyGuard {\n    // Booleans are more expensive than uint256 or any type that takes up a full\n    // word because each write operation emits an extra SLOAD to first read the\n    // slot's contents, replace the bits taken up by the boolean, and then write\n    // back. This is the compiler's defense against contract upgrades and\n    // pointer aliasing, and it cannot be disabled.\n\n    // The values being non-zero value makes deployment a bit more expensive,\n    // but in exchange the refund on every call to nonReentrant will be lower in\n    // amount. Since refunds are capped to a percentage of the total\n    // transaction's gas, it is best to keep them low in cases like this one, to\n    // increase the likelihood of the full refund coming into effect.\n    uint256 private constant _NOT_ENTERED = 1;\n    uint256 private constant _ENTERED = 2;\n\n    uint256 private _status;\n\n    constructor() {\n        _status = _NOT_ENTERED;\n    }\n\n    /**\n     * @dev Prevents a contract from calling itself, directly or indirectly.\n     * Calling a `nonReentrant` function from another `nonReentrant`\n     * function is not supported. It is possible to prevent this from happening\n     * by making the `nonReentrant` function external, and make it call a\n     * `private` function that does the actual work.\n     */\n    modifier nonReentrant() {\n        _enterNonReentrant();\n        _;\n        _exitNonReentrant();\n    }\n\n    function _enterNonReentrant() private {\n        // On the first call to nonReentrant, _notEntered will be true\n        require(_status != _ENTERED, \"REENTRANCY\");\n\n        // Any calls to nonReentrant after this point will fail\n        _status = _ENTERED;\n    }\n\n    function _exitNonReentrant() private {\n        // By storing the original value once again, a refund is triggered (see\n        // https://eips.ethereum.org/EIPS/eip-2200)\n        _status = _NOT_ENTERED;\n    }\n}\n"
    },
    "contracts/vault/interfaces/IVault.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\nimport \"./IAuthorizer.sol\";\nimport \"./IFlashLoanReceiver.sol\";\nimport \"./IAsset.sol\";\n\npragma solidity ^0.7.0;\n\n/**\n * @dev Full external interface for the Vault core contract - no external or public methods exist in the contract that\n * don't override one of these declarations.\n */\ninterface IVault {\n    // Generalities about the Vault:\n    //\n    // - Whenever documentation refers to 'tokens', it strictly refers to ERC20-compliant token contracts. Tokens are\n    // transferred out of the Vault by calling the `IERC20.transfer` function, and transferred in by calling\n    // `IERC20.transferFrom`. In these cases, the sender must have previously allowed the Vault to use their tokens by\n    // calling `IERC20.approve`. The only deviation from the ERC20 standard that is supported is functions not returning\n    // a boolean value: in these scenarios, a non-reverting call is assumed to be successful.\n    //\n    // - All non-view functions in the Vault are non-reentrant: calling them while another one is mid-execution (e.g.\n    // while execution control is transferred to a token contract during a swap) will result in a revert. View\n    // functions can be called in a re-reentrant way, but doing so might cause them to return inconsistent results.\n    // Contracts calling view functions in the Vault must make sure the Vault has not already been entered.\n    //\n    // - View functions revert if referring to either unregistered Pools, or unregistered tokens for registered Pools.\n\n    // Authorizer\n    //\n    // Some system actions are permissioned, like setting and collecting protocol fees. This permissioning system exists\n    // outside of the Vault in the Authorizer contract: the Vault simply calls the Authorizer to check if the caller\n    // can perform a given action.\n    // The only exception to this are relayers. A relayer is an account (typically a contract) that can use the Internal\n    // Balance and Vault allowance of other accounts. For an account to be able to wield this power, two things must\n    // happen:\n    //  - it must be allowed by the Authorizer to call the functions where it intends to use this permission\n    //  - it must be allowed by each individual user to act in their stead\n    // This combined requirements means users cannot be tricked into allowing malicious relayers (because they will not\n    // have been allowed by the Authorizer), nor can a malicious Authorizer allow malicious relayers to drain user funds\n    // (unless the user then allows this malicious relayer).\n\n    /**\n     * @dev Returns the Vault's Authorizer.\n     */\n    function getAuthorizer() external view returns (IAuthorizer);\n\n    /**\n     * @dev Sets a new Authorizer for the Vault. The caller must be allowed by the current Authorizer to do this.\n     */\n    function changeAuthorizer(IAuthorizer newAuthorizer) external;\n\n    /**\n     * @dev Returns true if `user` has allowed `relayer` as a relayer for them.\n     */\n    function hasAllowedRelayer(address user, address relayer) external view returns (bool);\n\n    /**\n     * @dev Allows `relayer` to act as a relayer for the caller if `allowed` is true, and disallows it otherwise.\n     */\n    function changeRelayerAllowance(address relayer, bool allowed) external;\n\n    // Internal Balance\n    //\n    // Users can deposit tokens into the Vault, where it is known as Internal Balance. This Internal Balance can be\n    // withdrawn or transferred, and it can also be used when joining Pools or performing swaps, with greatly reduced\n    // gas costs. Swaps and Pool exits can also be made to deposit to Internal Balance.\n    //\n    // Internal Balance functions feature batching, which means each call can be used to perform multiple operations of\n    // the same kind (deposit, withdraw or transfer) at once.\n\n    /**\n     * @dev Data for Internal Balance deposits and withdrawals, which include the possibility for ETH to be sent and\n     * received without manual WETH wrapping or unwrapping.\n     */\n    struct AssetBalanceTransfer {\n        IAsset asset;\n        uint256 amount;\n        address sender;\n        address payable recipient;\n    }\n\n    /**\n     * @dev Data for Internal Balance transfers, which are limited to ERC20 tokens.\n     */\n    struct TokenBalanceTransfer {\n        IERC20 token;\n        uint256 amount;\n        address sender;\n        address payable recipient;\n    }\n\n    /**\n     * @dev Returns `user`'s Internal Balance for a set of tokens.\n     */\n    function getInternalBalance(address user, IERC20[] memory tokens) external view returns (uint256[] memory);\n\n    /**\n     * @dev Deposits `amount` assets from each `sender` address into Internal Balances of the corresponding `recipient`\n     * accounts. The senders must have allowed the Vault to use their tokens via `IERC20.approve()`.\n     *\n     * If any of the senders doesn't match the contract caller, then it must be a relayer for them.\n     *\n     * ETH can be used by passing the ETH sentinel value as the asset and forwarding ETH in the call. It will be\n     * wrapped into WETH and deposited as that token. Any ETH amount remaining will be sent back to the caller (not the\n     * sender, which is relevant for relayers).\n     *\n     * Reverts if ETH was forwarded but not used in any transfer.\n     */\n    function depositToInternalBalance(AssetBalanceTransfer[] memory transfers) external payable;\n\n    /**\n     * @dev Withdraws `amount` assets from each `sender` address' Internal Balance to the corresponding `recipient`\n     * accounts. The senders must have allowed the Vault to use their tokens via `IERC20.approve()`.\n     *\n     * If any of the senders doesn't match the contract caller, then it must be a relayer for them.\n     *\n     * ETH can be used by passing the ETH sentinel value as the asset. This will deduct WETH instead, unwrap it and send\n     * it to the recipient.\n     *\n     * This charges protocol withdrawal fees.\n     */\n    function withdrawFromInternalBalance(AssetBalanceTransfer[] memory transfers) external;\n\n    /**\n     * @dev Transfers tokens from the internal balance of each `sender` address to Internal Balances of each\n     * `recipient`.\n     *\n     * This does not charge protocol withdrawal fees.\n     */\n    function transferInternalBalance(TokenBalanceTransfer[] memory transfers) external;\n\n    /**\n     * @dev Transfers tokens from each `sender` address to the corresponding `recipient` accounts, making use of the\n     * Vault's allowance. This action is equivalent to an Internal Balance deposit followed immediately by a withdrawal,\n     * except withdraw fees are not charged.\n     *\n     * Typically, this function will only be called by relayers, letting them leverage the allowance users have already\n     * given to the Vault.\n     */\n    function transferToExternalBalance(TokenBalanceTransfer[] memory transfers) external;\n\n    /**\n     * @dev Emitted when a user's Internal Balance changes, either due to calls to the Internal Balance functions, or\n     * due to interacting with Pools using Internal Balance.\n     *\n     * Because Internal Balance works exclusively with ERC20 tokens, ETH deposits and withdrawals will be reflected here\n     * as having used WETH.\n     */\n    event InternalBalanceChanged(address indexed user, IERC20 indexed token, int256 delta);\n\n    // Pools\n    //\n    // There are three specialization settings for Pools, which allow for lower swap gas costs at the cost of reduced\n    // functionality:\n    //\n    //  - general: no specialization, suited for all Pools. IGeneralPool is used for swap request callbacks, passing the\n    // balance of all tokens in the Pool. These Pools have the largest swap costs (because of the extra storage reads),\n    // and these increase with the number of registered tokens.\n    //\n    //  - minimal swap info: IMinimalSwapInfoPool is used instead of IGeneralPool, which saves gas by only passing the\n    // balance of the two tokens involved in the swap. This is suitable for some pricing algorithms, like the weighted\n    // constant product one popularized by Balancer v1. Swap costs are smaller compared to general Pools, and are\n    // independent of the number of registered tokens.\n    //\n    //  - two tokens: only allows two tokens to be registered. This achieves the lowest possible swap gas costs. Like\n    // minimal swap info Pools, these are called via IMinimalSwapInfoPool.\n\n    enum PoolSpecialization { GENERAL, MINIMAL_SWAP_INFO, TWO_TOKEN }\n\n    /**\n     * @dev Registers a the caller as a Pool with a chosen specialization setting. Returns the Pool's ID, which is used\n     * in all Pool-related functions. Pools cannot be deregistered, nor can the Pool's specialization be changed.\n     *\n     * The caller is expected to be a smart contract that implements one of `IGeneralPool` or `IMinimalSwapInfoPool`.\n     * This contract is known as the Pool's contract. Note that the same caller may register itself as multiple Pools\n     * with unique Pool IDs, or in other words, multiple Pools may have the same contract.\n     *\n     * Emits a `PoolRegistered` event.\n     */\n    function registerPool(PoolSpecialization specialization) external returns (bytes32);\n\n    /**\n     * @dev Emitted when a Pool is registered by calling `registerPool`.\n     */\n    event PoolRegistered(bytes32 poolId);\n\n    /**\n     * @dev Returns a Pool's contract address and specialization setting.\n     */\n    function getPool(bytes32 poolId) external view returns (address, PoolSpecialization);\n\n    /**\n     * @dev Registers `tokens` for the `poolId` Pool. Must be called by the Pool's contract.\n     *\n     * Pools can only interact with tokens they have registered. Users join a Pool by transferring registered tokens,\n     * exit by receiving registered tokens, and can only swap registered tokens.\n     *\n     * Each token can only be registered once. For Pools with the Two Token specialization, `tokens` must have a length\n     * of two, that is, both tokens must be registered in the same `registerTokens` call.\n     *\n     * The `tokens` and `assetManagers` arrays must have the same length, and each entry in these indicates the Asset\n     * Manager for each token. Asset Managers can manage a Pool's tokens by withdrawing and depositing them directly\n     * (via `withdrawFromPoolBalance` and `depositToPoolBalance`), and even set them to arbitrary amounts\n     * (`updateManagedBalance`). They are therefore expected to be highly secured smart contracts with sound design\n     * principles, and the decision to add an Asset Manager should not be made lightly.\n     *\n     * Pools can not set an Asset Manager by setting them to the zero address. Once an Asset Manager is set, it cannot\n     * be changed, except by deregistering the associated token and registering again with a different Manager.\n     *\n     * Emits `TokensRegistered` events.\n     */\n    function registerTokens(\n        bytes32 poolId,\n        IERC20[] calldata tokens,\n        address[] calldata assetManagers\n    ) external;\n\n    /**\n     * @dev Emitted when a Pool registers tokens by calling `registerTokens`.\n     */\n    event TokensRegistered(bytes32 poolId, IERC20[] tokens, address[] assetManagers);\n\n    /**\n     * @dev Deregisters `tokens` for the `poolId` Pool. Must be called by the Pool's contract.\n     *\n     * Only registered tokens (via `registerTokens`) can be deregistered. Additionally, they must have zero total\n     * balance. For Pools with the Two Token specialization, `tokens` must have a length of two, that is, both tokens\n     * must be deregistered in the same `deregisterTokens` call.\n     *\n     * A deregistered token can be re-registered later on, possibly with a different Asset Manager.\n     *\n     * Emits a `TokensDeregistered` event.\n     */\n    function deregisterTokens(bytes32 poolId, IERC20[] calldata tokens) external;\n\n    /**\n     * @dev Emitted when a Pool deregisters tokens by calling `deregisterTokens`.\n     */\n    event TokensDeregistered(bytes32 poolId, IERC20[] tokens);\n\n    /**\n     * @dev Returns a Pool's registered tokens and total balance for each.\n     *\n     * The order of the `tokens` array is the same order that will be used in `joinPool`, `exitPool`, as well as in all\n     * Pool hooks (where applicable). Calls to `registerTokens` and `deregisterTokens` may change this order.\n     *\n     * If a Pool only registers tokens once, and these are sorted in ascending order, they will be stored in the same\n     * order as passed to `registerTokens`.\n     *\n     * Total balances include both tokens held by the Vault and those withdrawn by the Pool's Asset Managers. These are\n     * the amounts used by joins, exits and swaps.\n     */\n    function getPoolTokens(bytes32 poolId) external view returns (IERC20[] memory tokens, uint256[] memory balances);\n\n    /**\n     * @dev Returns detailed information for a Pool's registered token.\n     *\n     * `cash` is the number of tokens the Vault currently holds for the Pool. `managed` is the number of tokens\n     * withdrawn and reported by the Pool's token Asset Manager. The Pool's total balance for `token` equals the sum of\n     * `cash` and `managed`.\n     *\n     * `blockNumber` is the number of the block in which `token`'s balance was last modified (via either a join, exit,\n     * swap, or Asset Management interactions). This value is useful to avoid so-called 'sandwich attacks', for example\n     * when developing price oracles.\n     *\n     * `assetManager` is the Pool's token Asset Manager.\n     */\n    function getPoolTokenInfo(bytes32 poolId, IERC20 token)\n        external\n        view\n        returns (\n            uint256 cash,\n            uint256 managed,\n            uint256 blockNumber,\n            address assetManager\n        );\n\n    /**\n     * @dev Called by users to join a Pool, which transfers tokens from `sender` into the Pool's balance. This will\n     * trigger custom Pool behavior, which will typically grant something in return to `recipient` - often tokenized\n     * Pool shares.\n     *\n     * If the caller is not `sender`, it must be an authorized relayer for them.\n     *\n     * The `tokens` and `maxAmountsIn` arrays must have the same length, and each entry in these indicates the maximum\n     * token amount to send for each token contract. The amounts to send are decided by the Pool and not the Vault: it\n     * just enforces these maximums.\n     *\n     * `tokens` must have the same length and order as the one returned by `getPoolTokens`. This prevents issues when\n     * interacting with Pools that register and deregister tokens frequently.\n     *\n     * If `fromInternalBalance` is true, the caller's Internal Balance will be preferred: ERC20 transfers will only\n     * be made for the difference between the requested amount and Internal Balance (if any).\n     *\n     * This causes the Vault to call the `IBasePool.onJoinPool` hook on the Pool's contract, where Pools implements\n     * their own custom logic. This typically requires additional information from the user (such as the expected number\n     * of Pool shares to obtain). This can be encoded in the `userData` argument, which is ignored by the Vault and\n     * passed directly to the Pool's contract, as is `recipient`.\n     *\n     * Emits a `PoolJoined` event.\n     */\n    function joinPool(\n        bytes32 poolId,\n        address sender,\n        address recipient,\n        IAsset[] memory assets,\n        uint256[] memory maxAmountsIn,\n        bool fromInternalBalance,\n        bytes memory userData\n    ) external payable;\n\n    /**\n     * @dev Emitted when a user joins a Pool by calling `joinPool`.\n     */\n    event PoolJoined(\n        bytes32 indexed poolId,\n        address indexed liquidityProvider,\n        IERC20[] tokens,\n        uint256[] amountsIn,\n        uint256[] protocolFees\n    );\n\n    /**\n     * @dev Called by users to exit a Pool, which transfers tokens from the Pool's balance to `recipient`. This will\n     * trigger custom Pool behavior, which will typically ask for something in return from `sender` - often tokenized\n     * Pool shares. The amount of tokens that can be withdraw is limited by the Pool's `cash` balance (see\n     * `getPoolTokenInfo`).\n     *\n     * If the caller is not `sender`, it must be an authorized relayer for them.\n     *\n     * The `tokens` and `minAmountsOut` arrays must have the same length, and each entry in these indicates the minimum\n     * token amount to receive for each token contract. The amounts to send are decided by the Pool and not the Vault:\n     * it just enforces these minimums.\n     *\n     * `tokens` must have the same length and order as the one returned by `getPoolTokens`. This prevents issues when\n     * interacting with Pools that register and deregister tokens frequently.\n     *\n     * If `toInternalBalance` is true, the tokens will be deposited to `recipient`'s Internal Balance. Otherwise,\n     * an ERC20 transfer will be performed, charging protocol withdraw fees.\n     *\n     * `minAmountsOut` is the minimum amount of tokens the user expects to get out of the Pool, for each token in the\n     * `tokens` array. This array must match the Pool's registered tokens.\n     *\n     * Pools are free to implement any arbitrary logic in the `IPool.onExitPool` hook, and may require additional\n     * information (such as the number of Pool shares to provide). This can be encoded in the `userData` argument, which\n     * is ignored by the Vault and passed directly to the Pool.\n     *\n     * This causes the Vault to call the `IBasePool.onExitPool` hook on the Pool's contract, where Pools implements\n     * their own custom logic. This typically requires additional information from the user (such as the expected number\n     * of Pool shares to return). This can be encoded in the `userData` argument, which is ignored by the Vault and\n     * passed directly to the Pool's contract.\n     *\n     * Emits a `PoolExited` event.\n     */\n    function exitPool(\n        bytes32 poolId,\n        address sender,\n        address payable recipient,\n        IAsset[] memory assets,\n        uint256[] memory minAmountsOut,\n        bool toInternalBalance,\n        bytes memory userData\n    ) external;\n\n    /**\n     * @dev Emitted when a user exits a pool by calling `exitPool`.\n     */\n    event PoolExited(\n        bytes32 indexed poolId,\n        address indexed liquidityProvider,\n        IERC20[] tokens,\n        uint256[] amountsOut,\n        uint256[] protocolFees\n    );\n\n    // Swaps\n    //\n    // Users can swap tokens with Pools by calling the `batchSwapGivenIn` and `batchSwapGivenOut` functions. To do this,\n    // they need not trust Pool contracts in any way: all security checks are made by the Vault. They must however be\n    // aware of the Pools' pricing algorithms in order to estimate the prices Pools will quote.\n    //\n    // Both swap functions are batched, meaning they perform multiple of swaps in sequence. In each individual swap,\n    // tokens of one kind are sent from the sender to the Pool (this is the 'token in'), and tokens of one\n    // other kind are sent from the Pool to the sender in exchange (this is the 'token out'). More complex swaps, such\n    // as one token in to multiple tokens out can be achieved by batching together individual swaps.\n    //\n    // Additionally, it is possible to chain swaps by using the output of one of them as the input for the other, as\n    // well as the opposite. This extended swap is known as a 'multihop' swap, since it 'hops' through a number of\n    // intermediate tokens before arriving at the final intended token.\n    //\n    // In all cases, tokens are only transferred in and out of the Vault (or withdrawn from and deposited into Internal\n    // Balance) after all individual swaps have been completed, and the net token balance change computed. This makes\n    // certain swap patterns, such as multihops, or swaps that interact with the same token pair in multiple Pools, cost\n    // much less gas than they would otherwise.\n    //\n    // It also means that it is possible to e.g. under certain conditions perform arbitrage by swapping with multiple\n    // Pools in a way that results in net token movement out of the Vault (profit), with no tokens being sent in (but\n    // updating the Pool's internal balances).\n    //\n    // To protect users from front-running or the market changing rapidly, they supply a list of 'limits' for each token\n    // involved in the swap, where either the maximum number of tokens to send (by passing a positive value) or minimum\n    // amount of tokens to receive (by passing a negative value) is specified.\n    //\n    // Additionally, a 'deadline' timestamp can also be provided, forcing the swap to fail if it occurs after\n    // this point in time (e.g. if the transaction failed to be included in a block promptly).\n    //\n    // If interacting with Pools that hold WETH, it is possible to both send and receive ETH directly: the Vault will do\n    // the wrapping and unwrapping. To enable this mecanism, the IAsset sentinel value (the zero address) must be passed\n    // in the `assets` array instead of the WETH address. Note that it is possible to combine ETH and WETH in the same\n    // swap. Any excess ETH will be sent back to the caller (not the sender, which is relevant for relayers).\n    //\n    // Finally, Internal Balance can be used both when sending and receiving tokens.\n\n    /**\n     * @dev Performs a series of swaps with one or multiple Pools. In individual each swap, the amount of tokens sent to\n     * the Pool is determined by the caller. For swaps where the amount of tokens received from the Pool is instead\n     * determined, see `batchSwapGivenOut`.\n     *\n     * Returns an array with the net Vault asset balance deltas. Positive amounts represent tokens (or ETH) sent to the\n     * Vault, and negative amounts tokens (or ETH) sent by the Vault. Each delta corresponds to the asset at the same\n     * index in the `assets` array.\n     *\n     * Swaps are executed sequentially, in the order specified by the `swaps` array. Each array element describes a\n     * Pool, the token and amount to send to this Pool, and the token to receive from it (but not the amount). This will\n     * be determined by the Pool's pricing algorithm once the Vault calls the `onSwapGivenIn` hook on it.\n     *\n     * Multihop swaps can be executed by passing an `amountIn` value of zero for a swap. This will cause the amount out\n     * of the previous swap to be used as the amount in of the current one. In such a scenario, `tokenIn` must equal the\n     * previous swap's `tokenOut`.\n     *\n     * The `assets` array contains the addresses of all assets involved in the swaps. These are either token addresses,\n     * or the IAsset sentinel value (the zero address) for ETH. Each entry in the `swaps` array specifies tokens in and\n     * out by referencing an index in `assets`. Note that Pools never interact with ETH directly: it will be wrapped or\n     * unwrapped using WETH by the Vault.\n     *\n     * Internal Balance usage and recipient are determined by the `funds` struct.\n     *\n     * Emits `Swap` events.\n     */\n    function batchSwapGivenIn(\n        SwapIn[] calldata swaps,\n        IAsset[] memory assets,\n        FundManagement calldata funds,\n        int256[] memory limits,\n        uint256 deadline\n    ) external payable returns (int256[] memory);\n\n    /**\n     * @dev Data for each individual swap executed by `batchSwapGivenIn`. The tokens in and out are indexed in the\n     * `assets` array passed to that function, where an ETH asset is translated into WETH.\n     *\n     * If `amountIn` is zero, the multihop mechanism is used to determine the actual amount based on the amount out from\n     * the previous swap.\n     *\n     * The `userData` field is ignored by the Vault, but forwarded to the Pool in the `onSwapGivenIn` hook, and may be\n     * used to extend swap behavior.\n     */\n    struct SwapIn {\n        bytes32 poolId;\n        uint256 tokenInIndex;\n        uint256 tokenOutIndex;\n        uint256 amountIn;\n        bytes userData;\n    }\n\n    /**\n     * @dev Performs a series of swaps with one or multiple Pools. In individual each swap, the amount of tokens\n     * received from the Pool is determined by the caller. For swaps where the amount of tokens sent to the Pool is\n     * instead determined, see `batchSwapGivenIn`.\n     *\n     * Returns an array with the net Vault asset balance deltas. Positive amounts represent tokens (or ETH) sent to the\n     * Vault, and negative amounts tokens (or ETH) sent by the Vault. Each delta corresponds to the asset at the same\n     * index in the `assets` array.\n     *\n     * Swaps are executed sequentially, in the order specified by the `swaps` array. Each array element describes a\n     * Pool, the token and amount to receive from this Pool, and the token to send to it (but not the amount). This will\n     * be determined by the Pool's pricing algorithm once the Vault calls the `onSwapGivenOut` hook on it.\n     *\n     * Multihop swaps can be executed by passing an `amountOut` value of zero for a swap. This will cause the amount in\n     * of the previous swap to be used as the amount out of the current one. In such a scenario, `tokenOut` must equal\n     * the previous swap's `tokenIn`.\n     *\n     * The `assets` array contains the addresses of all assets involved in the swaps. These are either token addresses,\n     * or the IAsset sentinel value (the zero address) for ETH. Each entry in the `swaps` array specifies tokens in and\n     * out by referencing an index in `assets`. Note that Pools never interact with ETH directly: it will be wrapped or\n     * unwrapped using WETH by the Vault.\n     *\n     * Internal Balance usage and recipient are determined by the `funds` struct.\n     *\n     * Emits `Swap` events.\n     */\n    function batchSwapGivenOut(\n        SwapOut[] calldata swaps,\n        IAsset[] memory assets,\n        FundManagement calldata funds,\n        int256[] memory limits,\n        uint256 deadline\n    ) external payable returns (int256[] memory);\n\n    /**\n     * @dev Data for each individual swap executed by `batchSwapGivenOut`. The tokens in and out are indexed in the\n     * `assets` array passed to that function, where an ETH asset is translated into WETH.\n     *\n     * If `amountOut` is zero, the multihop mechanism is used to determine the actual amount based on the amount in from\n     * the previous swap.\n     *\n     * The `userData` field is ignored by the Vault, but forwarded to the Pool in the `onSwapGivenOut` hook, and may be\n     * used to extend swap behavior.\n     */\n    struct SwapOut {\n        bytes32 poolId;\n        uint256 tokenInIndex;\n        uint256 tokenOutIndex;\n        uint256 amountOut;\n        bytes userData;\n    }\n\n    /**\n     * @dev Emitted for each individual swap performed by `batchSwapGivenIn` and `batchSwapGivenOut`.\n     */\n    event Swap(\n        bytes32 indexed poolId,\n        IERC20 indexed tokenIn,\n        IERC20 indexed tokenOut,\n        uint256 tokensIn,\n        uint256 tokensOut\n    );\n\n    /**\n     * @dev All tokens in a swap are sent to the Vault from the `sender`'s account, and sent to `recipient`.\n     *\n     * If the caller is not `sender`, it must be an authorized relayer for them.\n     *\n     * If `fromInternalBalance` is true, the `sender`'s Internal Balance will be preferred, performing an ERC20\n     * transfer for the difference between the requested amount and the User's Internal Balance (if any). The `sender`\n     * must have allowed the Vault to use their tokens via `IERC20.approve()`. This matches the behavior of\n     * `joinPool`.\n     *\n     * If `toInternalBalance` is true, tokens will be deposited to `recipient`'s internal balance instead of\n     * transferred. This matches the behavior of `exitPool`.\n     *\n     * Note that ETH cannot be deposited to or withdrawn from Internal Balance: attempting to do so with trigger a\n     * revert.\n     */\n    struct FundManagement {\n        address sender;\n        bool fromInternalBalance;\n        address payable recipient;\n        bool toInternalBalance;\n    }\n\n    /**\n     * @dev Simulates a call to `batchSwapGivenIn` or `batchSwapGivenOut`, returning an array of Vault asset deltas.\n     * Each element in the array corresponds to the asset at the same index, and indicates the number of tokens (or ETH)\n     * the Vault would take from the sender (if positive) or send to the recipient (if negative). The arguments it\n     * receives are the same that an equivalent `batchSwapGivenIn` or `batchSwapGivenOut` call would receive, except the\n     * `SwapRequest` struct is used instead, and the `kind` argument specifies whether the swap is given in or given\n     * out.\n     *\n     * Unlike `batchSwapGivenIn` and `batchSwapGivenOut`, this function performs no checks on the sender nor recipient\n     * field in the `funds` struct. This makes it suitable to be called by off-chain applications via eth_call without\n     * needing to hold tokens, approve them for the Vault, or even know a user's address.\n     *\n     * Note that this function is not 'view' (due to implementation details): the client code must explicitly execute\n     * eth_call instead of eth_sendTransaction.\n     */\n    function queryBatchSwap(\n        SwapKind kind,\n        SwapRequest[] memory swaps,\n        IAsset[] memory assets,\n        FundManagement memory funds\n    ) external returns (int256[] memory assetDeltas);\n\n    enum SwapKind { GIVEN_IN, GIVEN_OUT }\n\n    // This struct is identical in layout to SwapIn and SwapOut, except the 'amountIn/Out' field is named 'amount'.\n    struct SwapRequest {\n        bytes32 poolId;\n        uint256 tokenInIndex;\n        uint256 tokenOutIndex;\n        uint256 amount;\n        bytes userData;\n    }\n\n    // Flash Loans\n\n    /**\n     * @dev Performs a 'flash loan', sending tokens to `receiver` and executing the `receiveFlashLoan` hook on it,\n     * and then reverting unless the tokens plus a protocol fee have been returned.\n     *\n     * The `tokens` and `amounts` arrays must have the same length, and each entry in these indicates the amount to\n     * loan for each token contract. `tokens` must be sorted in ascending order.\n     *\n     * The 'receiverData' field is ignored by the Vault, and forwarded as-is to `receiver` as part of the\n     * `receiveFlashLoan` call.\n     */\n    function flashLoan(\n        IFlashLoanReceiver receiver,\n        IERC20[] calldata tokens,\n        uint256[] calldata amounts,\n        bytes calldata receiverData\n    ) external;\n\n    // Asset Management\n    //\n    // Each token registered for a Pool can be assigned an Asset Manager, which is able to freely withdraw the Pool's\n    // tokens from the Vault, deposit them, or assign arbitrary values to its `managed` balance (see\n    // `getPoolTokenInfo`). This makes them extremely powerful and dangerous, as they can not only steal a Pool's tokens\n    // but also manipulate its prices. However, a properly designed Asset Manager smart contract can be used to the\n    // Pool's benefit, for example by lending unused tokens at an interest, or using them to participate in voting\n    // protocols.\n\n    struct AssetManagerTransfer {\n        IERC20 token;\n        uint256 amount;\n    }\n\n    /**\n     * @dev Returns the Pool's Asset Managers for the given `tokens`. Asset Managers can manage a Pool's assets\n     * by taking them out of the Vault via `withdrawFromPoolBalance`, `depositToPoolBalance` and `updateManagedBalance`.\n     */\n    function getPoolAssetManagers(bytes32 poolId, IERC20[] memory tokens)\n        external\n        view\n        returns (address[] memory assetManagers);\n\n    /**\n     * @dev Emitted when a Pool's token Asset manager withdraws or deposits token balance via `withdrawFromPoolBalance`\n     * or `depositToPoolBalance`.\n     */\n    event PoolBalanceChanged(bytes32 indexed poolId, address indexed assetManager, IERC20 indexed token, int256 amount);\n\n    /**\n     * @dev Called by a Pool's Asset Manager to withdraw tokens from the Vault. This decreases\n     * the Pool's cash but increases its managed balance, leaving the total balance unchanged.\n     * Array input allows asset managers to manage multiple tokens for a pool in a single transaction.\n     */\n    function withdrawFromPoolBalance(bytes32 poolId, AssetManagerTransfer[] memory transfers) external;\n\n    /**\n     * @dev Called by a Pool's Asset Manager to deposit tokens into the Vault. This increases the Pool's cash,\n     * but decreases its managed balance, leaving the total balance unchanged. The Asset Manager must have approved\n     * the Vault to use each token. Array input allows asset managers to manage multiple tokens for a pool in a\n     * single transaction.\n     */\n    function depositToPoolBalance(bytes32 poolId, AssetManagerTransfer[] memory transfers) external;\n\n    /**\n     * @dev Called by a Pool's Asset Manager for to update the amount held outside the vault. This does not affect\n     * the Pool's cash balance, but because the managed balance changes, it does alter the total. The external\n     * amount can be either increased or decreased by this call (i.e., reporting a gain or a loss).\n     * Array input allows asset managers to manage multiple tokens for a pool in a single transaction.\n     */\n    function updateManagedBalance(bytes32 poolId, AssetManagerTransfer[] memory transfers) external;\n\n    // Protocol Fees\n    //\n    // Some operations cause the Vault to collect tokens in the form of protocol fees, which can then be withdrawn by\n    // permissioned accounts.\n    //\n    // There are three kinds of protocol fees:\n    //\n    //  - flash loan fees: charged on all flash loans, as a percentage of the amounts lent.\n    //\n    //  - withdraw fees: charged when users take tokens out of the Vault, by either calling\n    // `withdrawFromInternalBalance` or calling `exitPool` without depositing to Internal Balance. The fee is a\n    // percentage of the amount withdrawn. Swaps are unaffected by withdraw fees.\n    //\n    //  - swap fees: a percentage of the fees charged by Pools when performing swaps. For a number of reasons, including\n    // swap gas costs and interface simplicity, protocol swap fees are not charged on each individual swap. Rather,\n    // Pools are expected to keep track of how many swap fees they have charged, and pay any outstanding debts to the\n    // Vault when they are joined or exited. This prevents users from joining a Pool with unpaid debt, as well as\n    // exiting a Pool in debt without first paying their share.\n\n    /**\n     * @dev Returns the current protocol fee percentages. These are 18 decimals fixed point numbers, which means that\n     * e.g. a value of 0.1e18 stands for a 10% fee.\n     */\n    function getProtocolFees()\n        external\n        view\n        returns (\n            uint256 swapFee,\n            uint256 withdrawFee,\n            uint256 flashLoanFee\n        );\n\n    /**\n     * @dev Sets new Protocol fees. The caller must be allowed by the Authorizer to do this, and the new fee values must\n     * not be above the absolute maximum amounts.\n     */\n    function setProtocolFees(\n        uint256 swapFee,\n        uint256 withdrawFee,\n        uint256 flashLoanFee\n    ) external;\n\n    /**\n     * @dev Returns the amount of protocol fees collected by the Vault for each token in the `tokens` array.\n     */\n    function getCollectedFees(IERC20[] memory tokens) external view returns (uint256[] memory);\n\n    /**\n     * @dev Withdraws collected protocol fees, transferring them to `recipient`. The caller must be allowed by the\n     * Authorizer to do this.\n     */\n    function withdrawCollectedFees(\n        IERC20[] calldata tokens,\n        uint256[] calldata amounts,\n        address recipient\n    ) external;\n}\n"
    },
    "contracts/vault/interfaces/IFlashLoanReceiver.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\n// Inspired by Aave Protocol's IFlashLoanReceiver\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\ninterface IFlashLoanReceiver {\n    function receiveFlashLoan(\n        IERC20[] calldata tokens,\n        uint256[] calldata amounts,\n        uint256[] calldata feeAmounts,\n        bytes calldata receiverData\n    ) external;\n}\n"
    },
    "contracts/vault/interfaces/IAsset.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\n/**\n * @dev This is an empty interface used to represent either either token contracts that adhere to the IERC20 interface,\n * or a sentinel value that represents ETH (the zero address). We're just relying on the fact that `interface` can be\n * used to declare new address-like types.\n */\ninterface IAsset {\n    // solhint-disable-previous-line no-empty-blocks\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/SafeERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\nimport \"./IERC20.sol\";\nimport \"../../math/SafeMath.sol\";\nimport \"../../utils/Address.sol\";\n\n/**\n * @title SafeERC20\n * @dev Wrappers around ERC20 operations that throw on failure (when the token\n * contract returns false). Tokens that return no value (and instead revert or\n * throw on failure) are also supported, non-reverting calls are assumed to be\n * successful.\n * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,\n * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.\n */\nlibrary SafeERC20 {\n    using SafeMath for uint256;\n    using Address for address;\n\n    function safeTransfer(IERC20 token, address to, uint256 value) internal {\n        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));\n    }\n\n    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {\n        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));\n    }\n\n    /**\n     * @dev Deprecated. This function has issues similar to the ones found in\n     * {IERC20-approve}, and its usage is discouraged.\n     *\n     * Whenever possible, use {safeIncreaseAllowance} and\n     * {safeDecreaseAllowance} instead.\n     */\n    function safeApprove(IERC20 token, address spender, uint256 value) internal {\n        // safeApprove should only be called when setting an initial allowance,\n        // or when resetting it to zero. To increase and decrease it, use\n        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'\n        // solhint-disable-next-line max-line-length\n        require((value == 0) || (token.allowance(address(this), spender) == 0),\n            \"SafeERC20: approve from non-zero to non-zero allowance\"\n        );\n        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));\n    }\n\n    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 newAllowance = token.allowance(address(this), spender).add(value);\n        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));\n    }\n\n    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {\n        uint256 newAllowance = token.allowance(address(this), spender).sub(value, \"SafeERC20: decreased allowance below zero\");\n        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));\n    }\n\n    /**\n     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement\n     * on the return value: the return value is optional (but if data is returned, it must not be false).\n     * @param token The token targeted by the call.\n     * @param data The call data (encoded using abi.encode or one of its variants).\n     */\n    function _callOptionalReturn(IERC20 token, bytes memory data) private {\n        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since\n        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that\n        // the target address contains contract code and also asserts for success in the low-level call.\n\n        bytes memory returndata = address(token).functionCall(data, \"SafeERC20: low-level call failed\");\n        if (returndata.length > 0) { // Return data is optional\n            // solhint-disable-next-line max-line-length\n            require(abi.decode(returndata, (bool)), \"SafeERC20: ERC20 operation did not succeed\");\n        }\n    }\n}\n"
    },
    "contracts/vault/Fees.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/SafeERC20.sol\";\n\nimport \"../lib/math/Math.sol\";\nimport \"../lib/math/FixedPoint.sol\";\nimport \"../lib/helpers/InputHelpers.sol\";\nimport \"../lib/helpers/ReentrancyGuard.sol\";\n\nimport \"./interfaces/IVault.sol\";\n\nimport \"./VaultAuthorization.sol\";\nimport \"./AssetTransfersHandler.sol\";\n\nabstract contract Fees is IVault, ReentrancyGuard, AssetTransfersHandler, VaultAuthorization {\n    using Math for uint256;\n    using SafeERC20 for IERC20;\n\n    // Stores the fee collected per each token that is only withdrawable by the admin.\n    mapping(IERC20 => uint256) private _collectedProtocolFees;\n\n    // All fixed are 18-decimal fixed point numbers.\n\n    // The withdraw fee is charged whenever tokens exit the vault (except in the case of swaps), and is a\n    // percentage of the tokens exiting.\n    uint256 private _protocolWithdrawFee;\n\n    // The swap fee is charged whenever a swap occurs, and is a percentage of the fee charged by the Pool. These are not\n    // actually charged on each individual swap: the `Vault` relies on the Pools being honest and reporting due fees\n    // when joined and exited.\n    uint256 private _protocolSwapFee;\n\n    // The flash loan fee is charged whenever a flash loan occurs, and is a percentage of the tokens lent.\n    uint256 private _protocolFlashLoanFee;\n\n    // Absolute maximum fee percentages (1e18 = 100%, 1e16 = 1%).\n    uint256 private constant _MAX_PROTOCOL_SWAP_FEE = 50e16; // 50%\n    uint256 private constant _MAX_PROTOCOL_WITHDRAW_FEE = 0.5e16; // 0.5%\n    uint256 private constant _MAX_PROTOCOL_FLASH_LOAN_FEE = 1e16; // 1%\n\n    function setProtocolFees(\n        uint256 newSwapFee,\n        uint256 newWithdrawFee,\n        uint256 newFlashLoanFee\n    ) external override nonReentrant authenticate {\n        require(newSwapFee <= _MAX_PROTOCOL_SWAP_FEE, \"SWAP_FEE_TOO_HIGH\");\n        require(newWithdrawFee <= _MAX_PROTOCOL_WITHDRAW_FEE, \"WITHDRAW_FEE_TOO_HIGH\");\n        require(newFlashLoanFee <= _MAX_PROTOCOL_FLASH_LOAN_FEE, \"FLASH_LOAN_FEE_TOO_HIGH\");\n\n        _protocolSwapFee = newSwapFee;\n        _protocolWithdrawFee = newWithdrawFee;\n        _protocolFlashLoanFee = newFlashLoanFee;\n    }\n\n    function getProtocolFees()\n        external\n        view\n        override\n        returns (\n            uint256 swapFee,\n            uint256 withdrawFee,\n            uint256 flashLoanFee\n        )\n    {\n        return (_protocolSwapFee, _protocolWithdrawFee, _protocolFlashLoanFee);\n    }\n\n    /**\n     * @dev Returns the protocol swap fee percentage.\n     */\n    function _getProtocolSwapFee() internal view returns (uint256) {\n        return _protocolSwapFee;\n    }\n\n    /**\n     * @dev Returns the protocol fee to charge for a withdrawal of `amount`.\n     */\n    function _calculateProtocolWithdrawFeeAmount(uint256 amount) internal view override returns (uint256) {\n        // Fixed point multiplication introduces error: we round up, which means in certain scenarios the charged\n        // percentage can be slightly higher than intended.\n        return FixedPoint.mulUp(amount, _protocolWithdrawFee);\n    }\n\n    /**\n     * @dev Returns the protocol fee to charge for a flash loan of `amount`.\n     */\n    function _calculateProtocolFlashLoanFeeAmount(uint256 amount) internal view returns (uint256) {\n        // Fixed point multiplication introduces error: we round up, which means in certain scenarios the charged\n        // percentage can be slightly higher than intended.\n        return FixedPoint.mulUp(amount, _protocolFlashLoanFee);\n    }\n\n    function getCollectedFees(IERC20[] memory tokens) external view override returns (uint256[] memory) {\n        return _getCollectedFees(tokens);\n    }\n\n    function withdrawCollectedFees(\n        IERC20[] calldata tokens,\n        uint256[] calldata amounts,\n        address recipient\n    ) external override nonReentrant noEmergencyPeriod authenticate {\n        InputHelpers.ensureInputLengthMatch(tokens.length, amounts.length);\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            uint256 amount = amounts[i];\n            _decreaseCollectedFees(token, amount);\n            token.safeTransfer(recipient, amount);\n        }\n    }\n\n    /**\n     * @dev Increases the number of collected protocol fees for `token` by `amount`.\n     */\n    function _increaseCollectedFees(IERC20 token, uint256 amount) internal {\n        uint256 currentCollectedFees = _collectedProtocolFees[token];\n        uint256 newTotal = currentCollectedFees.add(amount);\n        _setCollectedFees(token, newTotal);\n    }\n\n    /**\n     * @dev Decreases the number of collected protocol fees for `token` by `amount`.\n     */\n    function _decreaseCollectedFees(IERC20 token, uint256 amount) internal {\n        uint256 currentCollectedFees = _collectedProtocolFees[token];\n        require(currentCollectedFees >= amount, \"INSUFFICIENT_COLLECTED_FEES\");\n\n        uint256 newTotal = currentCollectedFees - amount;\n        _setCollectedFees(token, newTotal);\n    }\n\n    /**\n     * @dev Sets the number of collected protocol fees for `token` to `newTotal`.\n     *\n     * This costs less gas than `_increaseCollectedFees` or `_decreaseCollectedFees`, since the current collected fees\n     * do not need to be read.\n     */\n    function _setCollectedFees(IERC20 token, uint256 newTotal) internal {\n        _collectedProtocolFees[token] = newTotal;\n    }\n\n    /**\n     * @dev Returns the number of collected fees for each token in the `tokens` array.\n     */\n    function _getCollectedFees(IERC20[] memory tokens) internal view returns (uint256[] memory fees) {\n        fees = new uint256[](tokens.length);\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            fees[i] = _collectedProtocolFees[tokens[i]];\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/math/SafeMath.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n/**\n * @dev Wrappers over Solidity's arithmetic operations with added overflow\n * checks.\n *\n * Arithmetic operations in Solidity wrap on overflow. This can easily result\n * in bugs, because programmers usually assume that an overflow raises an\n * error, which is the standard behavior in high level programming languages.\n * `SafeMath` restores this intuition by reverting the transaction when an\n * operation overflows.\n *\n * Using this library instead of the unchecked operations eliminates an entire\n * class of bugs, so it's recommended to use it always.\n */\nlibrary SafeMath {\n    /**\n     * @dev Returns the addition of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `+` operator.\n     *\n     * Requirements:\n     *\n     * - Addition cannot overflow.\n     */\n    function add(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c = a + b;\n        require(c >= a, \"SafeMath: addition overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers, reverting on\n     * overflow (when the result is negative).\n     *\n     * Counterpart to Solidity's `-` operator.\n     *\n     * Requirements:\n     *\n     * - Subtraction cannot overflow.\n     */\n    function sub(uint256 a, uint256 b) internal pure returns (uint256) {\n        return sub(a, b, \"SafeMath: subtraction overflow\");\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on\n     * overflow (when the result is negative).\n     *\n     * Counterpart to Solidity's `-` operator.\n     *\n     * Requirements:\n     *\n     * - Subtraction cannot overflow.\n     */\n    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b <= a, errorMessage);\n        uint256 c = a - b;\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the multiplication of two unsigned integers, reverting on\n     * overflow.\n     *\n     * Counterpart to Solidity's `*` operator.\n     *\n     * Requirements:\n     *\n     * - Multiplication cannot overflow.\n     */\n    function mul(uint256 a, uint256 b) internal pure returns (uint256) {\n        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the\n        // benefit is lost if 'b' is also tested.\n        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522\n        if (a == 0) {\n            return 0;\n        }\n\n        uint256 c = a * b;\n        require(c / a == b, \"SafeMath: multiplication overflow\");\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the integer division of two unsigned integers. Reverts on\n     * division by zero. The result is rounded towards zero.\n     *\n     * Counterpart to Solidity's `/` operator. Note: this function uses a\n     * `revert` opcode (which leaves remaining gas untouched) while Solidity\n     * uses an invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function div(uint256 a, uint256 b) internal pure returns (uint256) {\n        return div(a, b, \"SafeMath: division by zero\");\n    }\n\n    /**\n     * @dev Returns the integer division of two unsigned integers. Reverts with custom message on\n     * division by zero. The result is rounded towards zero.\n     *\n     * Counterpart to Solidity's `/` operator. Note: this function uses a\n     * `revert` opcode (which leaves remaining gas untouched) while Solidity\n     * uses an invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b > 0, errorMessage);\n        uint256 c = a / b;\n        // assert(a == b * c + a % b); // There is no case in which this doesn't hold\n\n        return c;\n    }\n\n    /**\n     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),\n     * Reverts when dividing by zero.\n     *\n     * Counterpart to Solidity's `%` operator. This function uses a `revert`\n     * opcode (which leaves remaining gas untouched) while Solidity uses an\n     * invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function mod(uint256 a, uint256 b) internal pure returns (uint256) {\n        return mod(a, b, \"SafeMath: modulo by zero\");\n    }\n\n    /**\n     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),\n     * Reverts with custom message when dividing by zero.\n     *\n     * Counterpart to Solidity's `%` operator. This function uses a `revert`\n     * opcode (which leaves remaining gas untouched) while Solidity uses an\n     * invalid opcode to revert (consuming all remaining gas).\n     *\n     * Requirements:\n     *\n     * - The divisor cannot be zero.\n     */\n    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {\n        require(b != 0, errorMessage);\n        return a % b;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize, which returns 0 for contracts in\n        // construction, since the code is only stored at the end of the\n        // constructor execution.\n\n        uint256 size;\n        // solhint-disable-next-line no-inline-assembly\n        assembly { size := extcodesize(account) }\n        return size > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value\n        (bool success, ) = recipient.call{ value: amount }(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain`call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n      return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        require(isContract(target), \"Address: call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.call{ value: value }(data);\n        return _verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {\n        require(isContract(target), \"Address: static call to non-contract\");\n\n        // solhint-disable-next-line avoid-low-level-calls\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return _verifyCallResult(success, returndata, errorMessage);\n    }\n\n    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n\n                // solhint-disable-next-line no-inline-assembly\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}\n"
    },
    "contracts/lib/math/Math.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n/**\n * @dev Wrappers over Solidity's arithmetic operations with added overflow checks.\n * Adapted from OpenZeppelin's SafeMath library\n */\nlibrary Math {\n    /**\n     * @dev Returns the addition of two unsigned integers of 256 bits, reverting on overflow.\n     */\n    function add(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c = a + b;\n        require(c >= a, \"ADD_OVERFLOW\");\n        return c;\n    }\n\n    /**\n     * @dev Returns the addition of two signed integers, reverting on overflow.\n     */\n    function add(int256 a, int256 b) internal pure returns (int256) {\n        int256 c = a + b;\n        require((b >= 0 && c >= a) || (b < 0 && c < a), \"ADD_OVERFLOW\");\n        return c;\n    }\n\n    /**\n     * @dev Returns the subtraction of two unsigned integers of 256 bits, reverting on overflow.\n     */\n    function sub(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b <= a, \"SUB_OVERFLOW\");\n        uint256 c = a - b;\n        return c;\n    }\n\n    /**\n     * @dev Returns the subtraction of two signed integers, reverting on overflow.\n     */\n    function sub(int256 a, int256 b) internal pure returns (int256) {\n        int256 c = a - b;\n        require((b >= 0 && c <= a) || (b < 0 && c > a), \"SUB_OVERFLOW\");\n        return c;\n    }\n\n    /**\n     * @dev Returns the largest of two numbers of 256 bits.\n     */\n    function max(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a >= b ? a : b;\n    }\n\n    /**\n     * @dev Returns the smallest of two numbers of 256 bits.\n     */\n    function min(uint256 a, uint256 b) internal pure returns (uint256) {\n        return a < b ? a : b;\n    }\n\n    function mul(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c = a * b;\n        require(a == 0 || c / a == b, \"MUL_OVERFLOW\");\n        return c;\n    }\n\n    function divDown(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b != 0, \"ZERO_DIVISION\");\n        return a / b;\n    }\n\n    function divUp(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b != 0, \"ZERO_DIVISION\");\n\n        if (a == 0) {\n            return 0;\n        } else {\n            return 1 + (a - 1) / b;\n        }\n    }\n}\n"
    },
    "contracts/lib/math/FixedPoint.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"../../lib/math/LogExpMath.sol\";\n\n/* solhint-disable private-vars-leading-underscore */\n\nlibrary FixedPoint {\n    uint256 internal constant ONE = 1e18; // 18 decimal places\n    uint256 internal constant MAX_POW_RELATIVE_ERROR = 10000; // 10^(-14)\n\n    function add(uint256 a, uint256 b) internal pure returns (uint256) {\n        // Fixed Point addition is the same as regular checked addition\n\n        uint256 c = a + b;\n        require(c >= a, \"ADD_OVERFLOW\");\n        return c;\n    }\n\n    function sub(uint256 a, uint256 b) internal pure returns (uint256) {\n        // Fixed Point addition is the same as regular checked addition\n\n        require(b <= a, \"SUB_OVERFLOW\");\n        uint256 c = a - b;\n        return c;\n    }\n\n    function mul(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 c0 = a * b;\n        require(a == 0 || c0 / a == b, \"MUL_OVERFLOW\");\n        uint256 c1 = c0 + (ONE / 2);\n        require(c1 >= c0, \"MUL_OVERFLOW\");\n        uint256 c2 = c1 / ONE;\n        return c2;\n    }\n\n    function mulDown(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 product = a * b;\n        require(a == 0 || product / a == b, \"MUL_OVERFLOW\");\n\n        return product / ONE;\n    }\n\n    function mulUp(uint256 a, uint256 b) internal pure returns (uint256) {\n        uint256 product = a * b;\n        require(a == 0 || product / a == b, \"MUL_OVERFLOW\");\n\n        if (product == 0) {\n            return 0;\n        } else {\n            // The traditional divUp formula is:\n            // divUp(x, y) := (x + y - 1) / y\n            // To avoid intermediate overflow in the addition, we distribute the division and get:\n            // divUp(x, y) := (x - 1) / y + 1\n            // Note that this requires x != 0, which we already tested for.\n\n            return ((product - 1) / ONE) + 1;\n        }\n    }\n\n    function div(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b != 0, \"ZERO_DIVISION\");\n        uint256 c0 = a * ONE;\n        require(a == 0 || c0 / a == ONE, \"DIV_INTERNAL\"); // mul overflow\n        uint256 c1 = c0 + (b / 2);\n        require(c1 >= c0, \"DIV_INTERNAL\"); // add require\n        uint256 c2 = c1 / b;\n        return c2;\n    }\n\n    function divDown(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b != 0, \"ZERO_DIVISION\");\n\n        if (a == 0) {\n            return 0;\n        } else {\n            uint256 aInflated = a * ONE;\n            require(aInflated / a == ONE, \"DIV_INTERNAL\"); // mul overflow\n\n            return aInflated / b;\n        }\n    }\n\n    function divUp(uint256 a, uint256 b) internal pure returns (uint256) {\n        require(b != 0, \"ZERO_DIVISION\");\n\n        if (a == 0) {\n            return 0;\n        } else {\n            uint256 aInflated = a * ONE;\n            require(aInflated / a == ONE, \"DIV_INTERNAL\"); // mul overflow\n\n            // The traditional divUp formula is:\n            // divUp(x, y) := (x + y - 1) / y\n            // To avoid intermediate overflow in the addition, we distribute the division and get:\n            // divUp(x, y) := (x - 1) / y + 1\n            // Note that this requires x != 0, which we already tested for.\n\n            return ((aInflated - 1) / b) + 1;\n        }\n    }\n\n    function pow(uint256 x, uint256 y) internal pure returns (uint256) {\n        return LogExpMath.pow(x, y);\n    }\n\n    function powDown(uint256 x, uint256 y) internal pure returns (uint256) {\n        uint256 result = LogExpMath.pow(x, y);\n        if (result == 0) {\n            return 0;\n        }\n        return sub(sub(result, mulDown(result, MAX_POW_RELATIVE_ERROR)), 1);\n    }\n\n    function powUp(uint256 x, uint256 y) internal pure returns (uint256) {\n        uint256 result = LogExpMath.pow(x, y);\n        return add(add(result, mulUp(result, MAX_POW_RELATIVE_ERROR)), 1);\n    }\n\n    /**\n     * @dev Tells the complement of a given value capped to zero to avoid overflow\n     */\n    function complement(uint256 x) internal pure returns (uint256) {\n        return x >= ONE ? 0 : sub(ONE, x);\n    }\n}\n"
    },
    "contracts/lib/helpers/InputHelpers.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\nimport \"../../vault/interfaces/IAsset.sol\";\n\nlibrary InputHelpers {\n    function ensureInputLengthMatch(uint256 a, uint256 b) internal pure {\n        require(a == b, \"INPUT_LENGTH_MISMATCH\");\n    }\n\n    function ensureInputLengthMatch(\n        uint256 a,\n        uint256 b,\n        uint256 c\n    ) internal pure {\n        require(a == b && b == c, \"INPUT_LENGTH_MISMATCH\");\n    }\n\n    function ensureArrayIsSorted(IAsset[] memory array) internal pure {\n        address[] memory addressArray;\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            addressArray := array\n        }\n        ensureArrayIsSorted(addressArray);\n    }\n\n    function ensureArrayIsSorted(IERC20[] memory array) internal pure {\n        address[] memory addressArray;\n        // solhint-disable-next-line no-inline-assembly\n        assembly {\n            addressArray := array\n        }\n        ensureArrayIsSorted(addressArray);\n    }\n\n    function ensureArrayIsSorted(address[] memory array) internal pure {\n        if (array.length < 2) {\n            return;\n        }\n\n        address previous = array[0];\n        for (uint256 i = 1; i < array.length; ++i) {\n            address current = array[i];\n            require(previous < current, \"UNSORTED_ARRAY\");\n            previous = current;\n        }\n    }\n}\n"
    },
    "contracts/vault/AssetTransfersHandler.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/SafeERC20.sol\";\nimport \"@openzeppelin/contracts/utils/Address.sol\";\n\nimport \"../lib/math/Math.sol\";\n\nimport \"./interfaces/IWETH.sol\";\nimport \"./interfaces/IAsset.sol\";\n\nabstract contract AssetTransfersHandler {\n    using SafeERC20 for IERC20;\n    using Address for address payable;\n    using Math for uint256;\n\n    // solhint-disable-next-line var-name-mixedcase\n    IWETH private immutable _WETH;\n\n    // Sentinel value used to indicate WETH with wrapping/unwrapping semantics. The zero address is a good choice for\n    // multiple reasons: it is cheap to pass as a calldata argument, it is a known invalid token and non-contract, and\n    // it is an adddress Pools cannot register as a token.\n    address private constant _ETH = address(0);\n\n    constructor(IWETH weth) {\n        _WETH = weth;\n    }\n\n    /**\n     * @dev Returns true if `asset` is the sentinel value that stands for ETH.\n     */\n    function _isETH(IAsset asset) internal pure returns (bool) {\n        return address(asset) == _ETH;\n    }\n\n    /**\n     * @dev Translates `asset` into an equivalent IERC20 token address. If `asset` stands for ETH, it will be translated\n     * into the WETH contract.\n     */\n    function _translateToIERC20(IAsset asset) internal view returns (IERC20) {\n        return _isETH(asset) ? _WETH : _asIERC20(asset);\n    }\n\n    /**\n     * @dev Same as `_translateToIERC20(IAsset)`, but for an entire array.\n     */\n    function _translateToIERC20(IAsset[] memory assets) internal view returns (IERC20[] memory) {\n        IERC20[] memory tokens = new IERC20[](assets.length);\n        for (uint256 i = 0; i < assets.length; ++i) {\n            tokens[i] = _translateToIERC20(assets[i]);\n        }\n\n        return tokens;\n    }\n\n    /**\n     * @dev Interprets `asset` as an IERC20 token. This function should only be called on `asset` if `_isETH` previously\n     * returned false for it, that is, if `asset` is guaranteed to not be the sentinel value that stands for ETH.\n     */\n    function _asIERC20(IAsset asset) internal pure returns (IERC20) {\n        return IERC20(address(asset));\n    }\n\n    /**\n     * @dev Receives `amount` of `asset` from `sender`. If `fromInternalBalance` is true, as much as possible is first\n     * withdrawn from Internal Balance, and the transfer is performed on the remaining amount, if any.\n     *\n     * If `asset` is ETH, `fromInternalBalance` must be false (as ETH cannot be held as internal balance), and the funds\n     * will be wrapped wrapped into WETH.\n     *\n     * WARNING: this function does not check that the contract caller has actually supplied any ETH - it is up to the\n     * caller of this function to check that this is true to prevent the Vault from using its own ETH (though the Vault\n     * typically doesn't hold any).\n     */\n    function _receiveAsset(\n        IAsset asset,\n        uint256 amount,\n        address sender,\n        bool fromInternalBalance\n    ) internal {\n        if (amount == 0) {\n            return;\n        }\n\n        if (_isETH(asset)) {\n            require(!fromInternalBalance, \"INVALID_ETH_INTERNAL_BALANCE\");\n\n            // The ETH amount to receive is deposited into the WETH contract, which will in turn mint WETH for\n            // the Vault at a 1:1 ratio.\n\n            // A check for this condition is also introduced by the compiler, but this one provides a revert reason.\n            // Note we're checking for the Vault's total balance, *not* ETH sent in this transaction.\n            require(address(this).balance >= amount, \"INSUFFICIENT_ETH\");\n            _WETH.deposit{ value: amount }();\n        } else {\n            IERC20 token = _asIERC20(asset);\n\n            if (fromInternalBalance) {\n                // Note that we ignore the taxable amount here since these assets are not being withdrawn from the Vault\n                // but rather reallocated (e.g. as part of a swap or join).\n                // Because `receivedFromInternalBalance` will be always the minimum between the current internal balance\n                // and the amount to decrease, it is safe to perform unchecked arithmetic.\n                (, uint256 receivedFromInternalBalance) = _decreaseInternalBalance(sender, token, amount, true);\n                amount -= receivedFromInternalBalance;\n            }\n\n            if (amount > 0) {\n                token.safeTransferFrom(sender, address(this), amount);\n            }\n        }\n    }\n\n    /**\n     * @dev Sends `amount` of `asset` to `recipient`. If `toInternalBalance` is true, the asset is deposited as Internal\n     * Balance instead of being transferred.\n     *\n     * If `asset` is ETH, `toInternalBalance` must be false (as ETH cannot be held as internal balance), and the funds\n     * are instead sent directly after unwrapping WETH.\n     */\n    function _sendAsset(\n        IAsset asset,\n        uint256 amount,\n        address payable recipient,\n        bool toInternalBalance,\n        bool trackExempt\n    ) internal {\n        if (amount == 0) {\n            return;\n        }\n\n        if (_isETH(asset)) {\n            // Sending ETH is not as involved as receiving it: the only special behavior it has is it cannot be\n            // deposited to Internal Balance.\n            require(!toInternalBalance, \"INVALID_ETH_INTERNAL_BALANCE\");\n\n            // First, the Vault withdraws deposited ETH in the WETH contract, by burning the same amount of WETH\n            // from the Vault. This receipt will be handled by the Vault's `receive`.\n            _WETH.withdraw(amount);\n\n            // Then, the withdrawn ETH is sent to the recipient.\n            recipient.sendValue(amount);\n        } else {\n            IERC20 token = _asIERC20(asset);\n            if (toInternalBalance) {\n                _increaseInternalBalance(recipient, token, amount, trackExempt);\n            } else {\n                token.safeTransfer(recipient, amount);\n            }\n        }\n    }\n\n    /**\n     * @dev Returns excess ETH back to the contract caller, assuming `amountUsed` of it has been spent.\n     *\n     * Because the caller might not now exactly how much ETH a Vault action will require, they may send extra amounts.\n     * Note that this excess value is returned *to the contract caller* (msg.sender). If caller and e.g. swap sender are\n     * not the same (because the caller is a relayer for the sender), then it is up to the caller to manage this\n     * returned ETH.\n     *\n     * Reverts if the contract caller sent less ETH than `amountUsed`.\n     */\n    function _returnExcessEthToCaller(uint256 amountUsed) internal {\n        require(msg.value >= amountUsed, \"INSUFFICIENT_ETH\");\n\n        uint256 excess = msg.value - amountUsed;\n        if (excess > 0) {\n            msg.sender.sendValue(excess);\n        }\n    }\n\n    /**\n     * @dev Reverts in transactions where a user sent ETH, but didn't specify usage of it as an asset. `ethAssetSeen`\n     * should be true if any asset held the sentinel value for ETH, and false otherwise.\n     */\n    function _ensureNoUnallocatedETH(bool ethAssetSeen) internal view {\n        if (msg.value > 0) {\n            require(ethAssetSeen, \"UNALLOCATED_ETH\");\n        }\n    }\n\n    /**\n     * @dev Enables the Vault to receive ETH. This is required for it to be able to unwrap WETH, which sends ETH to the\n     * caller.\n     *\n     * Any ETH sent to the Vault outside of the WETH unwrapping mechanism would be forever locked inside the Vault, so\n     * we prevent that from happening. Other mechanisms used to send ETH to the Vault (such as selfdestruct, or have it\n     * be the recipient of the block mining reward) will result in locked funds, but are not otherwise a security or\n     * soundness issue. This check only exists as an attempt to prevent user error.\n     */\n    receive() external payable {\n        require(msg.sender == address(_WETH), \"ETH_TRANSFER\");\n    }\n\n    // This contract has uses virtual internal functions instead of inheriting from the modules that implement them (in\n    // this case, Fees and InternalBalance) in order to decouple it from the rest of the system and enable standalone\n    // testing by implementing these with mocks.\n\n    function _calculateProtocolWithdrawFeeAmount(uint256 amount) internal view virtual returns (uint256);\n\n    function _increaseInternalBalance(\n        address account,\n        IERC20 token,\n        uint256 amount,\n        bool track\n    ) internal virtual;\n\n    function _decreaseInternalBalance(\n        address account,\n        IERC20 token,\n        uint256 amount,\n        bool capped\n    ) internal virtual returns (uint256, uint256);\n}\n"
    },
    "contracts/lib/math/LogExpMath.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General internal License for more details.\n\n// You should have received a copy of the GNU General internal License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\n// There's plenty of linter errors caused by this file, we'll eventually\n// revisit it to make it more readable, verfiable and testable.\n/* solhint-disable */\n\n/**\n * @title Ethereum library for logarithm and exponential functions with 18 decimal precision.\n * @author Fernando Martinelli - @fernandomartinelli\n * @author Sergio Yuhjtman - @sergioyuhjtman\n * @author Daniel Fernandez - @dmf7z\n */\nlibrary LogExpMath {\n    int256 constant DECIMALS = 1e18;\n    int256 constant DOUBLE_DECIMALS = DECIMALS * DECIMALS;\n    int256 constant PRECISION = 10**20;\n    int256 constant DOUBLE_PRECISION = PRECISION * PRECISION;\n    int256 constant PRECISION_LOG_UNDER_BOUND = DECIMALS - 10**17;\n    int256 constant PRECISION_LOG_UPPER_BOUND = DECIMALS + 10**17;\n    int256 constant EXPONENT_LB = -41446531673892822312;\n    int256 constant EXPONENT_UB = 130700829182905140221;\n    uint256 constant MILD_EXPONENT_BOUND = 2**254 / uint256(PRECISION);\n\n    int256 constant x0 = 128000000000000000000; //27\n    int256 constant a0 = 38877084059945950922200000000000000000000000000000000000; //e(x0)\n    int256 constant x1 = 64000000000000000000; //26\n    int256 constant a1 = 6235149080811616882910000000; //e(x1)\n    int256 constant x2 = 3200000000000000000000; //25\n    int256 constant a2 = 7896296018268069516100000000000000; //e(x2)\n    int256 constant x3 = 1600000000000000000000; //24\n    int256 constant a3 = 888611052050787263676000000; //e(x3)\n    int256 constant x4 = 800000000000000000000; //23\n    int256 constant a4 = 298095798704172827474000; //e(x4)\n    int256 constant x5 = 400000000000000000000; //22\n    int256 constant a5 = 5459815003314423907810; //e(x5)\n    int256 constant x6 = 200000000000000000000; //21\n    int256 constant a6 = 738905609893065022723; //e(x6)\n    int256 constant x7 = 100000000000000000000; //20\n    int256 constant a7 = 271828182845904523536; //e(x7)\n    int256 constant x8 = 50000000000000000000; //2-1\n    int256 constant a8 = 164872127070012814685; //e(x8)\n    int256 constant x9 = 25000000000000000000; //2-2\n    int256 constant a9 = 128402541668774148407; //e(x9)\n    int256 constant x10 = 12500000000000000000; //2-3\n    int256 constant a10 = 113314845306682631683; //e(x10)\n    int256 constant x11 = 6250000000000000000; //2-4\n    int256 constant a11 = 106449445891785942956; //e(x11)\n\n    /**\n     * Calculate the natural exponentiation of a number with 18 decimals precision.\n     * @param x Exponent with 18 decimal places.\n     * @notice Max x is log((2^255 - 1) / 10^20) = 130.700829182905140221\n     * @notice Min x log(0.000000000000000001) = -41.446531673892822312\n     * @return ex\n     */\n    function n_exp(int256 x) internal pure returns (int256) {\n        require(x >= EXPONENT_LB && x <= EXPONENT_UB, \"OUT_OF_BOUNDS\");\n\n        if (x < 0) return (DOUBLE_DECIMALS / n_exp(-x));\n        int256 ans = PRECISION;\n        int256 last = 1;\n        if (x >= x0) {\n            last = a0;\n            x -= x0;\n        }\n        if (x >= x1) {\n            last *= a1;\n            x -= x1;\n        }\n        x *= 100;\n        if (x >= x2) {\n            ans = (ans * a2) / PRECISION;\n            x -= x2;\n        }\n        if (x >= x3) {\n            ans = (ans * a3) / PRECISION;\n            x -= x3;\n        }\n        if (x >= x4) {\n            ans = (ans * a4) / PRECISION;\n            x -= x4;\n        }\n        if (x >= x5) {\n            ans = (ans * a5) / PRECISION;\n            x -= x5;\n        }\n        if (x >= x6) {\n            ans = (ans * a6) / PRECISION;\n            x -= x6;\n        }\n        if (x >= x7) {\n            ans = (ans * a7) / PRECISION;\n            x -= x7;\n        }\n        if (x >= x8) {\n            ans = (ans * a8) / PRECISION;\n            x -= x8;\n        }\n        if (x >= x9) {\n            ans = (ans * a9) / PRECISION;\n            x -= x9;\n        }\n        int256 s = PRECISION;\n        int256 t = x;\n        s += t;\n        t = ((t * x) / 2) / PRECISION;\n        s += t;\n        t = ((t * x) / 3) / PRECISION;\n        s += t;\n        t = ((t * x) / 4) / PRECISION;\n        s += t;\n        t = ((t * x) / 5) / PRECISION;\n        s += t;\n        t = ((t * x) / 6) / PRECISION;\n        s += t;\n        t = ((t * x) / 7) / PRECISION;\n        s += t;\n        t = ((t * x) / 8) / PRECISION;\n        s += t;\n        t = ((t * x) / 9) / PRECISION;\n        s += t;\n        t = ((t * x) / 10) / PRECISION;\n        s += t;\n        t = ((t * x) / 11) / PRECISION;\n        s += t;\n        t = ((t * x) / 12) / PRECISION;\n        s += t;\n        return (((ans * s) / PRECISION) * last) / 100;\n    }\n\n    /**\n     * Calculate the natural logarithm of a number with 18 decimals precision.\n     * @param a Positive number with 18 decimal places.\n     * @return ln(x)\n     */\n    function n_log(int256 a) internal pure returns (int256) {\n        require(a > 0, \"OUT_OF_BOUNDS\");\n        if (a < DECIMALS) return (-n_log(DOUBLE_DECIMALS / a));\n        int256 ans = 0;\n        if (a >= a0 * DECIMALS) {\n            ans += x0;\n            a /= a0;\n        }\n        if (a >= a1 * DECIMALS) {\n            ans += x1;\n            a /= a1;\n        }\n        a *= 100;\n        ans *= 100;\n        if (a >= a2) {\n            ans += x2;\n            a = (a * PRECISION) / a2;\n        }\n        if (a >= a3) {\n            ans += x3;\n            a = (a * PRECISION) / a3;\n        }\n        if (a >= a4) {\n            ans += x4;\n            a = (a * PRECISION) / a4;\n        }\n        if (a >= a5) {\n            ans += x5;\n            a = (a * PRECISION) / a5;\n        }\n        if (a >= a6) {\n            ans += x6;\n            a = (a * PRECISION) / a6;\n        }\n        if (a >= a7) {\n            ans += x7;\n            a = (a * PRECISION) / a7;\n        }\n        if (a >= a8) {\n            ans += x8;\n            a = (a * PRECISION) / a8;\n        }\n        if (a >= a9) {\n            ans += x9;\n            a = (a * PRECISION) / a9;\n        }\n        if (a >= a10) {\n            ans += x10;\n            a = (a * PRECISION) / a10;\n        }\n        if (a >= a11) {\n            ans += x11;\n            a = (a * PRECISION) / a11;\n        }\n        int256 z = (PRECISION * (a - PRECISION)) / (a + PRECISION);\n        int256 s = z;\n        int256 z_squared = (z * z) / PRECISION;\n        int256 t = (z * z_squared) / PRECISION;\n        s += t / 3;\n        t = (t * z_squared) / PRECISION;\n        s += t / 5;\n        t = (t * z_squared) / PRECISION;\n        s += t / 7;\n        t = (t * z_squared) / PRECISION;\n        s += t / 9;\n        t = (t * z_squared) / PRECISION;\n        s += t / 11;\n        return (ans + 2 * s) / 100;\n    }\n\n    /**\n     * Computes x to the power of y for numbers with 18 decimals precision.\n     * @param x Base with 18 decimal places.\n     * @param y Exponent with 18 decimal places.\n     * @notice Must fulfil: -41.446531673892822312  < (log(x) * y) <  130.700829182905140221\n     * @return xy\n     */\n    function pow(uint256 x, uint256 y) internal pure returns (uint256) {\n        if (y == 0) {\n            return uint256(DECIMALS);\n        }\n\n        if (x == 0) {\n            return 0;\n        }\n\n        require(x < 2**255, \"X_OUT_OF_BOUNDS\"); // uint256 can be casted to a positive int256\n        require(y < MILD_EXPONENT_BOUND, \"Y_OUT_OF_BOUNDS\");\n        int256 x_int256 = int256(x);\n        int256 y_int256 = int256(y);\n        int256 logx_times_y;\n        if (PRECISION_LOG_UNDER_BOUND < x_int256 && x_int256 < PRECISION_LOG_UPPER_BOUND) {\n            int256 logbase = n_log_36(x_int256);\n            logx_times_y = ((logbase / DECIMALS) * y_int256 + ((logbase % DECIMALS) * y_int256) / DECIMALS);\n        } else {\n            logx_times_y = n_log(x_int256) * y_int256;\n        }\n        require(\n            EXPONENT_LB * DECIMALS <= logx_times_y && logx_times_y <= EXPONENT_UB * DECIMALS,\n            \"PRODUCT_OUT_OF_BOUNDS\"\n        );\n        logx_times_y /= DECIMALS;\n        return uint256(n_exp(logx_times_y));\n    }\n\n    /**\n     * Computes log of a number in base of another number, both numbers with 18 decimals precision.\n     * @param arg Argument with 18 decimal places.\n     * @param base Base with 18 decimal places.\n     * @notice Must fulfil: -41.446531673892822312  < (log(x) * y) <  130.700829182905140221\n     * @return log[base](arg)\n     */\n    function log(int256 arg, int256 base) internal pure returns (int256) {\n        int256 logbase;\n        if (PRECISION_LOG_UNDER_BOUND < base && base < PRECISION_LOG_UPPER_BOUND) {\n            logbase = n_log_36(base);\n        } else {\n            logbase = n_log(base) * DECIMALS;\n        }\n        int256 logarg;\n        if (PRECISION_LOG_UNDER_BOUND < arg && arg < PRECISION_LOG_UPPER_BOUND) {\n            logarg = n_log_36(arg);\n        } else {\n            logarg = n_log(arg) * DECIMALS;\n        }\n        return (logarg * DECIMALS) / logbase;\n    }\n\n    /**\n     * Private function to calculate the natural logarithm of a number with 36 decimals precision.\n     * @param a Positive number with 18 decimal places.\n     * @return ln(x)\n     */\n    function n_log_36(int256 a) private pure returns (int256) {\n        a *= DECIMALS;\n        int256 z = (DOUBLE_DECIMALS * (a - DOUBLE_DECIMALS)) / (a + DOUBLE_DECIMALS);\n        int256 s = z;\n        int256 z_squared = (z * z) / DOUBLE_DECIMALS;\n        int256 t = (z * z_squared) / DOUBLE_DECIMALS;\n        s += t / 3;\n        t = (t * z_squared) / DOUBLE_DECIMALS;\n        s += t / 5;\n        t = (t * z_squared) / DOUBLE_DECIMALS;\n        s += t / 7;\n        t = (t * z_squared) / DOUBLE_DECIMALS;\n        s += t / 9;\n        t = (t * z_squared) / DOUBLE_DECIMALS;\n        s += t / 11;\n        t = (t * z_squared) / DOUBLE_DECIMALS;\n        s += t / 13;\n        t = (t * z_squared) / DOUBLE_DECIMALS;\n        s += t / 15;\n        return 2 * s;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/EnumerableSet.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n/**\n * @dev Library for managing\n * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive\n * types.\n *\n * Sets have the following properties:\n *\n * - Elements are added, removed, and checked for existence in constant time\n * (O(1)).\n * - Elements are enumerated in O(n). No guarantees are made on the ordering.\n *\n * ```\n * contract Example {\n *     // Add the library methods\n *     using EnumerableSet for EnumerableSet.AddressSet;\n *\n *     // Declare a set state variable\n *     EnumerableSet.AddressSet private mySet;\n * }\n * ```\n *\n * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)\n * and `uint256` (`UintSet`) are supported.\n */\nlibrary EnumerableSet {\n    // To implement this library for multiple types with as little code\n    // repetition as possible, we write it in terms of a generic Set type with\n    // bytes32 values.\n    // The Set implementation uses private functions, and user-facing\n    // implementations (such as AddressSet) are just wrappers around the\n    // underlying Set.\n    // This means that we can only create new EnumerableSets for types that fit\n    // in bytes32.\n\n    struct Set {\n        // Storage of set values\n        bytes32[] _values;\n\n        // Position of the value in the `values` array, plus 1 because index 0\n        // means a value is not in the set.\n        mapping (bytes32 => uint256) _indexes;\n    }\n\n    /**\n     * @dev Add a value to a set. O(1).\n     *\n     * Returns true if the value was added to the set, that is if it was not\n     * already present.\n     */\n    function _add(Set storage set, bytes32 value) private returns (bool) {\n        if (!_contains(set, value)) {\n            set._values.push(value);\n            // The value is stored at length-1, but we add 1 to all indexes\n            // and use 0 as a sentinel value\n            set._indexes[value] = set._values.length;\n            return true;\n        } else {\n            return false;\n        }\n    }\n\n    /**\n     * @dev Removes a value from a set. O(1).\n     *\n     * Returns true if the value was removed from the set, that is if it was\n     * present.\n     */\n    function _remove(Set storage set, bytes32 value) private returns (bool) {\n        // We read and store the value's index to prevent multiple reads from the same storage slot\n        uint256 valueIndex = set._indexes[value];\n\n        if (valueIndex != 0) { // Equivalent to contains(set, value)\n            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in\n            // the array, and then remove the last element (sometimes called as 'swap and pop').\n            // This modifies the order of the array, as noted in {at}.\n\n            uint256 toDeleteIndex = valueIndex - 1;\n            uint256 lastIndex = set._values.length - 1;\n\n            // When the value to delete is the last one, the swap operation is unnecessary. However, since this occurs\n            // so rarely, we still do the swap anyway to avoid the gas cost of adding an 'if' statement.\n\n            bytes32 lastvalue = set._values[lastIndex];\n\n            // Move the last value to the index where the value to delete is\n            set._values[toDeleteIndex] = lastvalue;\n            // Update the index for the moved value\n            set._indexes[lastvalue] = toDeleteIndex + 1; // All indexes are 1-based\n\n            // Delete the slot where the moved value was stored\n            set._values.pop();\n\n            // Delete the index for the deleted slot\n            delete set._indexes[value];\n\n            return true;\n        } else {\n            return false;\n        }\n    }\n\n    /**\n     * @dev Returns true if the value is in the set. O(1).\n     */\n    function _contains(Set storage set, bytes32 value) private view returns (bool) {\n        return set._indexes[value] != 0;\n    }\n\n    /**\n     * @dev Returns the number of values on the set. O(1).\n     */\n    function _length(Set storage set) private view returns (uint256) {\n        return set._values.length;\n    }\n\n   /**\n    * @dev Returns the value stored at position `index` in the set. O(1).\n    *\n    * Note that there are no guarantees on the ordering of values inside the\n    * array, and it may change when more values are added or removed.\n    *\n    * Requirements:\n    *\n    * - `index` must be strictly less than {length}.\n    */\n    function _at(Set storage set, uint256 index) private view returns (bytes32) {\n        require(set._values.length > index, \"EnumerableSet: index out of bounds\");\n        return set._values[index];\n    }\n\n    // Bytes32Set\n\n    struct Bytes32Set {\n        Set _inner;\n    }\n\n    /**\n     * @dev Add a value to a set. O(1).\n     *\n     * Returns true if the value was added to the set, that is if it was not\n     * already present.\n     */\n    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {\n        return _add(set._inner, value);\n    }\n\n    /**\n     * @dev Removes a value from a set. O(1).\n     *\n     * Returns true if the value was removed from the set, that is if it was\n     * present.\n     */\n    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {\n        return _remove(set._inner, value);\n    }\n\n    /**\n     * @dev Returns true if the value is in the set. O(1).\n     */\n    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {\n        return _contains(set._inner, value);\n    }\n\n    /**\n     * @dev Returns the number of values in the set. O(1).\n     */\n    function length(Bytes32Set storage set) internal view returns (uint256) {\n        return _length(set._inner);\n    }\n\n   /**\n    * @dev Returns the value stored at position `index` in the set. O(1).\n    *\n    * Note that there are no guarantees on the ordering of values inside the\n    * array, and it may change when more values are added or removed.\n    *\n    * Requirements:\n    *\n    * - `index` must be strictly less than {length}.\n    */\n    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {\n        return _at(set._inner, index);\n    }\n\n    // AddressSet\n\n    struct AddressSet {\n        Set _inner;\n    }\n\n    /**\n     * @dev Add a value to a set. O(1).\n     *\n     * Returns true if the value was added to the set, that is if it was not\n     * already present.\n     */\n    function add(AddressSet storage set, address value) internal returns (bool) {\n        return _add(set._inner, bytes32(uint256(value)));\n    }\n\n    /**\n     * @dev Removes a value from a set. O(1).\n     *\n     * Returns true if the value was removed from the set, that is if it was\n     * present.\n     */\n    function remove(AddressSet storage set, address value) internal returns (bool) {\n        return _remove(set._inner, bytes32(uint256(value)));\n    }\n\n    /**\n     * @dev Returns true if the value is in the set. O(1).\n     */\n    function contains(AddressSet storage set, address value) internal view returns (bool) {\n        return _contains(set._inner, bytes32(uint256(value)));\n    }\n\n    /**\n     * @dev Returns the number of values in the set. O(1).\n     */\n    function length(AddressSet storage set) internal view returns (uint256) {\n        return _length(set._inner);\n    }\n\n   /**\n    * @dev Returns the value stored at position `index` in the set. O(1).\n    *\n    * Note that there are no guarantees on the ordering of values inside the\n    * array, and it may change when more values are added or removed.\n    *\n    * Requirements:\n    *\n    * - `index` must be strictly less than {length}.\n    */\n    function at(AddressSet storage set, uint256 index) internal view returns (address) {\n        return address(uint256(_at(set._inner, index)));\n    }\n\n\n    // UintSet\n\n    struct UintSet {\n        Set _inner;\n    }\n\n    /**\n     * @dev Add a value to a set. O(1).\n     *\n     * Returns true if the value was added to the set, that is if it was not\n     * already present.\n     */\n    function add(UintSet storage set, uint256 value) internal returns (bool) {\n        return _add(set._inner, bytes32(value));\n    }\n\n    /**\n     * @dev Removes a value from a set. O(1).\n     *\n     * Returns true if the value was removed from the set, that is if it was\n     * present.\n     */\n    function remove(UintSet storage set, uint256 value) internal returns (bool) {\n        return _remove(set._inner, bytes32(value));\n    }\n\n    /**\n     * @dev Returns true if the value is in the set. O(1).\n     */\n    function contains(UintSet storage set, uint256 value) internal view returns (bool) {\n        return _contains(set._inner, bytes32(value));\n    }\n\n    /**\n     * @dev Returns the number of values on the set. O(1).\n     */\n    function length(UintSet storage set) internal view returns (uint256) {\n        return _length(set._inner);\n    }\n\n   /**\n    * @dev Returns the value stored at position `index` in the set. O(1).\n    *\n    * Note that there are no guarantees on the ordering of values inside the\n    * array, and it may change when more values are added or removed.\n    *\n    * Requirements:\n    *\n    * - `index` must be strictly less than {length}.\n    */\n    function at(UintSet storage set, uint256 index) internal view returns (uint256) {\n        return uint256(_at(set._inner, index));\n    }\n}\n"
    },
    "contracts/lib/helpers/EnumerableMap.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n// Based on the EnumerableSet library from OpenZeppelin contracts, altered to include\n// the following:\n//  * a map from IERC20 to bytes32\n//  * entries are stored in mappings instead of arrays, reducing implicit storage reads for out-of-bounds checks\n//  * _unchecked_at and _unchecked_valueAt, which allow for more gas efficient data reads in some scenarios\n//  * _indexOf and _unchecked_setAt, which allow for more gas efficient data writes in some scenarios\n\n// We're using non-standard casing for the unchecked functions to differentiate them, so we need to turn off that rule\n// solhint-disable func-name-mixedcase\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\n/**\n * @dev Library for managing an enumerable variant of Solidity's\n * https://solidity.readthedocs.io/en/latest/types.html#mapping-types[`mapping`]\n * type.\n *\n * Maps have the following properties:\n *\n * - Entries are added, removed, and checked for existence in constant time\n * (O(1)).\n * - Entries are enumerated in O(n). No guarantees are made on the ordering.\n *\n * ```\n * contract Example {\n *     // Add the library methods\n *     using EnumerableMap for EnumerableMap.UintToAddressMap;\n *\n *     // Declare a set state variable\n *     EnumerableMap.UintToAddressMap private myMap;\n * }\n * ```\n */\nlibrary EnumerableMap {\n    // To implement this library for multiple types with as little code\n    // repetition as possible, we write it in terms of a generic Map type with\n    // bytes32 keys and values.\n    // The Map implementation uses private functions, and user-facing\n    // implementations (such as Uint256ToAddressMap) are just wrappers around\n    // the underlying Map.\n    // This means that we can only create new EnumerableMaps for types that fit\n    // in bytes32.\n\n    struct MapEntry {\n        bytes32 _key;\n        bytes32 _value;\n    }\n\n    struct Map {\n        // Number of entries in the map\n        uint256 _length;\n        // Storage of map keys and values\n        mapping(uint256 => MapEntry) _entries;\n        // Position of the entry defined by a key in the `entries` array, plus 1\n        // because index 0 means a key is not in the map.\n        mapping(bytes32 => uint256) _indexes;\n    }\n\n    /**\n     * @dev Adds a key-value pair to a map, or updates the value for an existing\n     * key. O(1).\n     *\n     * Returns true if the key was added to the map, that is if it was not\n     * already present.\n     */\n    function _set(\n        Map storage map,\n        bytes32 key,\n        bytes32 value\n    ) private returns (bool) {\n        // We read and store the key's index to prevent multiple reads from the same storage slot\n        uint256 keyIndex = map._indexes[key];\n\n        // Equivalent to !contains(map, key)\n        if (keyIndex == 0) {\n            uint256 previousLength = map._length;\n            map._entries[previousLength] = MapEntry({ _key: key, _value: value });\n            map._length = previousLength + 1;\n\n            // The entry is stored at previousLength, but we add 1 to all indexes\n            // and use 0 as a sentinel value\n            map._indexes[key] = previousLength + 1;\n            return true;\n        } else {\n            map._entries[keyIndex - 1]._value = value;\n            return false;\n        }\n    }\n\n    /**\n     * @dev Returns the index for `key`, which can be used alongside the {at} family of functions, including critically\n     * {unchecked_setAt}. O(1).\n     *\n     * Requirements:\n     *\n     * - `key` must be in the map.\n     */\n    function _indexOf(Map storage map, bytes32 key) private view returns (uint256) {\n        return _indexOf(map, key, \"OUT_OF_BOUNDS\");\n    }\n\n    /**\n     * @dev Same as {_indexOf}, with a custom error message when `key` is not in the map.\n     */\n    function _indexOf(\n        Map storage map,\n        bytes32 key,\n        string memory notFoundErrorMessage\n    ) private view returns (uint256) {\n        uint256 index = map._indexes[key];\n        require(index > 0, notFoundErrorMessage);\n        return index - 1;\n    }\n\n    /**\n     * @dev Updates the value for an entry, given its key's index. The key index can be retrieved via {indexOf}, and it\n     * should be noted that key indices may change when calling {add} or {remove}. O(1).\n     *\n     * This function performs one less storage read than {set}, but it should only be used when `index` is known to be\n     * within bounds.\n     */\n    function _unchecked_setAt(\n        Map storage map,\n        uint256 index,\n        bytes32 value\n    ) private {\n        map._entries[index]._value = value;\n    }\n\n    /**\n     * @dev Removes a key-value pair from a map. O(1).\n     *\n     * Returns true if the key was removed from the map, that is if it was present.\n     */\n    function _remove(Map storage map, bytes32 key) private returns (bool) {\n        // We read and store the key's index to prevent multiple reads from the same storage slot\n        uint256 keyIndex = map._indexes[key];\n\n        // Equivalent to contains(map, key)\n        if (keyIndex != 0) {\n            // To delete a key-value pair from the _entries pseudo-array in O(1), we swap the entry to delete with the\n            // one at the highest index, and then remove this last entry (sometimes called as 'swap and pop').\n            // This modifies the order of the pseudo-array, as noted in {at}.\n\n            uint256 toDeleteIndex = keyIndex - 1;\n            uint256 lastIndex = map._length - 1;\n\n            // When the entry to delete is the last one, the swap operation is unnecessary. However, since this occurs\n            // so rarely, we still do the swap anyway to avoid the gas cost of adding an 'if' statement.\n\n            MapEntry storage lastEntry = map._entries[lastIndex];\n\n            // Move the last entry to the index where the entry to delete is\n            map._entries[toDeleteIndex] = lastEntry;\n            // Update the index for the moved entry\n            map._indexes[lastEntry._key] = toDeleteIndex + 1; // All indexes are 1-based\n\n            // Delete the slot where the moved entry was stored\n            delete map._entries[lastIndex];\n            map._length = lastIndex;\n\n            // Delete the index for the deleted slot\n            delete map._indexes[key];\n\n            return true;\n        } else {\n            return false;\n        }\n    }\n\n    /**\n     * @dev Returns true if the key is in the map. O(1).\n     */\n    function _contains(Map storage map, bytes32 key) private view returns (bool) {\n        return map._indexes[key] != 0;\n    }\n\n    /**\n     * @dev Returns the number of key-value pairs in the map. O(1).\n     */\n    function _length(Map storage map) private view returns (uint256) {\n        return map._length;\n    }\n\n    /**\n     * @dev Returns the key-value pair stored at position `index` in the map. O(1).\n     *\n     * Note that there are no guarantees on the ordering of entries inside the\n     * array, and it may change when more entries are added or removed.\n     *\n     * Requirements:\n     *\n     * - `index` must be strictly less than {length}.\n     */\n    function _at(Map storage map, uint256 index) private view returns (bytes32, bytes32) {\n        require(map._length > index, \"OUT_OF_BOUNDS\");\n\n        MapEntry storage entry = map._entries[index];\n        return (entry._key, entry._value);\n    }\n\n    /**\n     * @dev Same as {at}, except this doesn't revert if `index` it outside of the map (i.e. if it is\n     * equal or larger than {length}). O(1).\n     *\n     * This function performs one less storage read than {at}, but should only be used when `index` is known to be\n     * within bounds.\n     */\n    function _unchecked_at(Map storage map, uint256 index) private view returns (bytes32, bytes32) {\n        MapEntry storage entry = map._entries[index];\n        return (entry._key, entry._value);\n    }\n\n    /**\n     * @dev Same as {unchecked_valueAt}, except it only returns the value and not the key (performing one less storage\n     * read). O(1).\n     */\n    function _unchecked_valueAt(Map storage map, uint256 index) private view returns (bytes32) {\n        return map._entries[index]._value;\n    }\n\n    /**\n     * @dev Returns the value associated with `key`. O(1).\n     *\n     * Requirements:\n     *\n     * - `key` must be in the map.\n     */\n    function _get(Map storage map, bytes32 key) private view returns (bytes32) {\n        return _get(map, key, \"OUT_OF_BOUNDS\");\n    }\n\n    /**\n     * @dev Same as {_get}, with a custom error message when `key` is not in the map.\n     */\n    function _get(\n        Map storage map,\n        bytes32 key,\n        string memory notFoundErrorMessage\n    ) private view returns (bytes32) {\n        uint256 index = _indexOf(map, key, notFoundErrorMessage);\n        return _unchecked_valueAt(map, index);\n    }\n\n    // UintToAddressMap\n\n    struct UintToAddressMap {\n        Map _inner;\n    }\n\n    /**\n     * @dev Adds a key-value pair to a map, or updates the value for an existing\n     * key. O(1).\n     *\n     * Returns true if the key was added to the map, that is if it was not\n     * already present.\n     */\n    function set(\n        UintToAddressMap storage map,\n        uint256 key,\n        address value\n    ) internal returns (bool) {\n        return _set(map._inner, bytes32(key), bytes32(uint256(value)));\n    }\n\n    /**\n     * @dev Returns the index for `key`, which can be used alongside the {at} family of functions, including critically\n     * {unchecked_setAt}. O(1).\n     *\n     * Requirements:\n     *\n     * - `key` must be in the map.\n     */\n    function indexOf(UintToAddressMap storage map, uint256 key) internal view returns (uint256) {\n        return _indexOf(map._inner, bytes32(uint256(address(key))));\n    }\n\n    /**\n     * @dev Updates the value for an entry, given its key's index. The key index can be retrieved via {indexOf}, and it\n     * should be noted that key indices may change when calling {add} or {remove}. O(1).\n     *\n     * This function performs one less storage read than {set}, but it should only be used when `index` is known to be\n     * within bounds.\n     */\n    function unchecked_setAt(\n        UintToAddressMap storage map,\n        uint256 index,\n        address value\n    ) internal {\n        _unchecked_setAt(map._inner, index, bytes32(uint256(value)));\n    }\n\n    /**\n     * @dev Removes a value from a set. O(1).\n     *\n     * Returns true if the key was removed from the map, that is if it was present.\n     */\n    function remove(UintToAddressMap storage map, uint256 key) internal returns (bool) {\n        return _remove(map._inner, bytes32(key));\n    }\n\n    /**\n     * @dev Returns true if the key is in the map. O(1).\n     */\n    function contains(UintToAddressMap storage map, uint256 key) internal view returns (bool) {\n        return _contains(map._inner, bytes32(key));\n    }\n\n    /**\n     * @dev Returns the number of elements in the map. O(1).\n     */\n    function length(UintToAddressMap storage map) internal view returns (uint256) {\n        return _length(map._inner);\n    }\n\n    /**\n     * @dev Returns the element stored at position `index` in the set. O(1).\n     * Note that there are no guarantees on the ordering of values inside the\n     * array, and it may change when more values are added or removed.\n     *\n     * Requirements:\n     *\n     * - `index` must be strictly less than {length}.\n     */\n    function at(UintToAddressMap storage map, uint256 index) internal view returns (uint256, address) {\n        (bytes32 key, bytes32 value) = _at(map._inner, index);\n        return (uint256(key), address(uint256(value)));\n    }\n\n    /**\n     * @dev Same as {at}, except this doesn't revert if `index` it outside of the map (i.e. if it is\n     * equal or larger than {length}). O(1).\n     *\n     * This function performs one less storage read than {at}, but should only be used when `index` is known to be\n     * within bounds.\n     */\n    function unchecked_at(UintToAddressMap storage map, uint256 index) internal view returns (uint256, address) {\n        (bytes32 key, bytes32 value) = _unchecked_at(map._inner, index);\n        return (uint256(key), address(uint256(value)));\n    }\n\n    /**\n     * @dev Same as {unchecked_valueAt}, except it only returns the value and not the key (performing one less storage\n     * read). O(1).\n     */\n    function unchecked_valueAt(UintToAddressMap storage map, uint256 index) internal view returns (address) {\n        return address(uint256(_unchecked_valueAt(map._inner, index)));\n    }\n\n    /**\n     * @dev Returns the value associated with `key`.  O(1).\n     *\n     * Requirements:\n     *\n     * - `key` must be in the map.\n     */\n    function get(UintToAddressMap storage map, uint256 key) internal view returns (address) {\n        return address(uint256(_get(map._inner, bytes32(key))));\n    }\n\n    /**\n     * @dev Same as {get}, with a custom error message when `key` is not in the map.\n     */\n    function get(\n        UintToAddressMap storage map,\n        uint256 key,\n        string memory errorMessage\n    ) internal view returns (address) {\n        return address(uint256(_get(map._inner, bytes32(key), errorMessage)));\n    }\n\n    // IERC20ToBytes32Map\n\n    struct IERC20ToBytes32Map {\n        Map _inner;\n    }\n\n    /**\n     * @dev Adds a key-value pair to a map, or updates the value for an existing\n     * key. O(1).\n     *\n     * Returns true if the key was added to the map, that is if it was not\n     * already present.\n     */\n    function set(\n        IERC20ToBytes32Map storage map,\n        IERC20 key,\n        bytes32 value\n    ) internal returns (bool) {\n        return _set(map._inner, bytes32(uint256(address(key))), value);\n    }\n\n    /**\n     * @dev Returns the index for `key`, which can be used alongside the {at} family of functions, including critically\n     * {unchecked_setAt}. O(1).\n     *\n     * Requirements:\n     *\n     * - `key` must be in the map.\n     */\n    function indexOf(IERC20ToBytes32Map storage map, IERC20 key) internal view returns (uint256) {\n        return _indexOf(map._inner, bytes32(uint256(address(key))));\n    }\n\n    /**\n     * @dev Same as {indexOf}, with a custom error message when `key` is not in the map.\n     */\n    function indexOf(\n        IERC20ToBytes32Map storage map,\n        IERC20 key,\n        string memory notFoundErrorMessage\n    ) internal view returns (uint256) {\n        return _indexOf(map._inner, bytes32(uint256(address(key))), notFoundErrorMessage);\n    }\n\n    /**\n     * @dev Updates the value for an entry, given its key's index. The key index can be retrieved via {indexOf}, and it\n     * should be noted that key indices may change when calling {add} or {remove}. O(1).\n     *\n     * This function performs one less storage read than {set}, but it should only be used when `index` is known to be\n     * within bounds.\n     */\n    function unchecked_setAt(\n        IERC20ToBytes32Map storage map,\n        uint256 index,\n        bytes32 value\n    ) internal {\n        _unchecked_setAt(map._inner, index, value);\n    }\n\n    /**\n     * @dev Removes a value from a set. O(1).\n     *\n     * Returns true if the key was removed from the map, that is if it was present.\n     */\n    function remove(IERC20ToBytes32Map storage map, IERC20 key) internal returns (bool) {\n        return _remove(map._inner, bytes32(uint256(address(key))));\n    }\n\n    /**\n     * @dev Returns true if the key is in the map. O(1).\n     */\n    function contains(IERC20ToBytes32Map storage map, IERC20 key) internal view returns (bool) {\n        return _contains(map._inner, bytes32(uint256(address(key))));\n    }\n\n    /**\n     * @dev Returns the number of elements in the map. O(1).\n     */\n    function length(IERC20ToBytes32Map storage map) internal view returns (uint256) {\n        return _length(map._inner);\n    }\n\n    /**\n     * @dev Returns the element stored at position `index` in the set. O(1).\n     * Note that there are no guarantees on the ordering of values inside the\n     * array, and it may change when more values are added or removed.\n     *\n     * Requirements:\n     *\n     * - `index` must be strictly less than {length}.\n     */\n    function at(IERC20ToBytes32Map storage map, uint256 index) internal view returns (IERC20, bytes32) {\n        (bytes32 key, bytes32 value) = _at(map._inner, index);\n        return (IERC20(uint256(key)), value);\n    }\n\n    /**\n     * @dev Same as {at}, except this doesn't revert if `index` it outside of the map (i.e. if it is\n     * equal or larger than {length}). O(1).\n     *\n     * This function performs one less storage read than {at}, but should only be used when `index` is known to be\n     * within bounds.\n     */\n    function unchecked_at(IERC20ToBytes32Map storage map, uint256 index) internal view returns (IERC20, bytes32) {\n        (bytes32 key, bytes32 value) = _unchecked_at(map._inner, index);\n        return (IERC20(uint256(key)), value);\n    }\n\n    /**\n     * @dev Same as {unchecked_valueAt}, except it only returns the value and not the key (performing one less storage\n     * read). O(1).\n     */\n    function unchecked_valueAt(IERC20ToBytes32Map storage map, uint256 index) internal view returns (bytes32) {\n        return _unchecked_valueAt(map._inner, index);\n    }\n\n    /**\n     * @dev Returns the value associated with `key`. O(1).\n     *\n     * Requirements:\n     *\n     * - `key` must be in the map.\n     */\n    function get(IERC20ToBytes32Map storage map, IERC20 key) internal view returns (bytes32) {\n        return _get(map._inner, bytes32(uint256(address(key))));\n    }\n\n    /**\n     * @dev Returns the value associated with `key`. O(1).\n     *\n     * Requirements:\n     *\n     * - `key` must be in the map.\n     */\n    function get(\n        IERC20ToBytes32Map storage map,\n        IERC20 key,\n        string memory errorMessage\n    ) internal view returns (bytes32) {\n        return _get(map._inner, bytes32(uint256(address(key))), errorMessage);\n    }\n}\n"
    },
    "contracts/vault/PoolRegistry.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/utils/SafeCast.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/SafeERC20.sol\";\nimport \"@openzeppelin/contracts/utils/Counters.sol\";\n\nimport \"../lib/math/Math.sol\";\nimport \"../lib/helpers/InputHelpers.sol\";\nimport \"../lib/helpers/ReentrancyGuard.sol\";\n\nimport \"./interfaces/IBasePool.sol\";\nimport \"./InternalBalance.sol\";\nimport \"./balances/BalanceAllocation.sol\";\nimport \"./balances/GeneralPoolsBalance.sol\";\nimport \"./balances/MinimalSwapInfoPoolsBalance.sol\";\nimport \"./balances/TwoTokenPoolsBalance.sol\";\n\nabstract contract PoolRegistry is\n    ReentrancyGuard,\n    InternalBalance,\n    GeneralPoolsBalance,\n    MinimalSwapInfoPoolsBalance,\n    TwoTokenPoolsBalance\n{\n    using Math for uint256;\n    using SafeCast for uint256;\n    using SafeERC20 for IERC20;\n    using BalanceAllocation for bytes32;\n    using BalanceAllocation for bytes32[];\n    using Counters for Counters.Counter;\n\n    // Ensure Pool IDs are unique.\n    Counters.Counter private _poolNonce;\n\n    // Pool IDs are stored as `bytes32`.\n    mapping(bytes32 => bool) private _isPoolRegistered;\n\n    // Stores the Asset Manager for each token of each Pool.\n    mapping(bytes32 => mapping(IERC20 => address)) private _poolAssetManagers;\n\n    /**\n     * @dev Reverts unless `poolId` corresponds to a registered Pool.\n     */\n    modifier withRegisteredPool(bytes32 poolId) {\n        _ensureRegisteredPool(poolId);\n        _;\n    }\n\n    /**\n     * @dev Reverts unless `poolId` corresponds to a registered Pool, and the caller is the Pool's contract.\n     */\n    modifier onlyPool(bytes32 poolId) {\n        _ensurePoolIsSender(poolId);\n        _;\n    }\n\n    /**\n     * @dev Creates a Pool ID.\n     *\n     * These are deterministically created by packing into the ID the Pool's contract address and its specialization\n     * setting. This saves gas as this data does not need to be written to or read from storage with interacting with\n     * the Pool.\n     *\n     * Since a single contract can register multiple Pools, a unique nonce must be provided to ensure Pool IDs are\n     * unique.\n     */\n    function _toPoolId(\n        address pool,\n        PoolSpecialization specialization,\n        uint80 nonce\n    ) internal pure returns (bytes32) {\n        uint256 serialized;\n\n        // | 20 bytes pool address | 2 bytes specialization setting | 10 bytes nonce |\n        serialized |= uint256(nonce);\n        serialized |= uint256(specialization) << (10 * 8);\n        serialized |= uint256(pool) << (12 * 8);\n\n        return bytes32(serialized);\n    }\n\n    /**\n     * @dev Returns a Pool's address.\n     *\n     * Due to how Pool IDs are created, this is done with no storage accesses and costs little gas.\n     */\n    function _getPoolAddress(bytes32 poolId) internal pure returns (address) {\n        // | 20 bytes pool address | 2 bytes specialization setting | 10 bytes nonce |\n        return address((uint256(poolId) >> (12 * 8)) & (2**(20 * 8) - 1));\n    }\n\n    /**\n     * @dev Returns a Pool's specialization setting.\n     *\n     * Due to how Pool IDs are created, this is done with no storage accesses and costs little gas.\n     */\n    function _getPoolSpecialization(bytes32 poolId) internal pure returns (PoolSpecialization) {\n        // | 20 bytes pool address | 2 bytes specialization setting | 10 bytes nonce |\n        return PoolSpecialization(uint256(poolId >> (10 * 8)) & (2**(2 * 8) - 1));\n    }\n\n    function registerPool(PoolSpecialization specialization)\n        external\n        override\n        nonReentrant\n        noEmergencyPeriod\n        returns (bytes32)\n    {\n        // Use _totalPools as the Pool ID nonce. uint80 assumes there will never be more than than 2**80 Pools.\n        bytes32 poolId = _toPoolId(msg.sender, specialization, uint80(_poolNonce.current()));\n        require(!_isPoolRegistered[poolId], \"INVALID_POOL_ID\"); // Should never happen\n\n        _poolNonce.increment();\n        _isPoolRegistered[poolId] = true;\n\n        emit PoolRegistered(poolId);\n        return poolId;\n    }\n\n    function getPoolTokens(bytes32 poolId)\n        external\n        view\n        override\n        withRegisteredPool(poolId)\n        returns (IERC20[] memory tokens, uint256[] memory balances)\n    {\n        bytes32[] memory rawBalances;\n        (tokens, rawBalances) = _getPoolTokens(poolId);\n        balances = rawBalances.totals();\n    }\n\n    function getPoolTokenInfo(bytes32 poolId, IERC20 token)\n        external\n        view\n        override\n        withRegisteredPool(poolId)\n        returns (\n            uint256 cash,\n            uint256 managed,\n            uint256 blockNumber,\n            address assetManager\n        )\n    {\n        bytes32 balance;\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n\n        if (specialization == PoolSpecialization.TWO_TOKEN) {\n            balance = _getTwoTokenPoolBalance(poolId, token);\n        } else if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            balance = _getMinimalSwapInfoPoolBalance(poolId, token);\n        } else {\n            balance = _getGeneralPoolBalance(poolId, token);\n        }\n\n        cash = balance.cash();\n        managed = balance.managed();\n        blockNumber = balance.blockNumber();\n        assetManager = _poolAssetManagers[poolId][token];\n    }\n\n    function getPool(bytes32 poolId)\n        external\n        view\n        override\n        withRegisteredPool(poolId)\n        returns (address, PoolSpecialization)\n    {\n        return (_getPoolAddress(poolId), _getPoolSpecialization(poolId));\n    }\n\n    function registerTokens(\n        bytes32 poolId,\n        IERC20[] calldata tokens,\n        address[] calldata assetManagers\n    ) external override nonReentrant noEmergencyPeriod onlyPool(poolId) {\n        InputHelpers.ensureInputLengthMatch(tokens.length, assetManagers.length);\n\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n        if (specialization == PoolSpecialization.TWO_TOKEN) {\n            require(tokens.length == 2, \"TOKENS_LENGTH_MUST_BE_2\");\n            _registerTwoTokenPoolTokens(poolId, tokens[0], tokens[1]);\n        } else if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            _registerMinimalSwapInfoPoolTokens(poolId, tokens);\n        } else {\n            _registerGeneralPoolTokens(poolId, tokens);\n        }\n\n        // Assign each token's asset manager\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            address assetManager = assetManagers[i];\n\n            _poolAssetManagers[poolId][token] = assetManager;\n        }\n\n        emit TokensRegistered(poolId, tokens, assetManagers);\n    }\n\n    function deregisterTokens(bytes32 poolId, IERC20[] calldata tokens)\n        external\n        override\n        nonReentrant\n        noEmergencyPeriod\n        onlyPool(poolId)\n    {\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n        if (specialization == PoolSpecialization.TWO_TOKEN) {\n            require(tokens.length == 2, \"TOKENS_LENGTH_MUST_BE_2\");\n            _deregisterTwoTokenPoolTokens(poolId, tokens[0], tokens[1]);\n        } else if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            _deregisterMinimalSwapInfoPoolTokens(poolId, tokens);\n        } else {\n            _deregisterGeneralPoolTokens(poolId, tokens);\n        }\n\n        // The deregister calls above ensure the total token balance is zero. It is therefore safe to now remove any\n        // associated Asset Managers, since they hold no Pool balance.\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            delete _poolAssetManagers[poolId][tokens[i]];\n        }\n\n        emit TokensDeregistered(poolId, tokens);\n    }\n\n    function joinPool(\n        bytes32 poolId,\n        address sender,\n        address recipient,\n        IAsset[] memory assets,\n        uint256[] memory maxAmountsIn,\n        bool fromInternalBalance,\n        bytes memory userData\n    ) external payable override nonReentrant noEmergencyPeriod withRegisteredPool(poolId) authenticateFor(sender) {\n        InputHelpers.ensureInputLengthMatch(assets.length, maxAmountsIn.length);\n\n        bytes32[] memory balances = _validateTokensAndGetBalances(poolId, assets);\n\n        _callOnJoinPoolAndReceiveAssets(\n            poolId,\n            assets,\n            balances,\n            sender,\n            recipient,\n            userData,\n            maxAmountsIn,\n            fromInternalBalance\n        );\n\n        IERC20[] memory tokens = _translateToIERC20(assets);\n\n        // Update the Pool's balance - how this is done depends on the Pool specialization setting.\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n        if (specialization == PoolSpecialization.TWO_TOKEN) {\n            _setTwoTokenPoolCashBalances(poolId, tokens[0], balances[0], tokens[1], balances[1]);\n        } else if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            _setMinimalSwapInfoPoolBalances(poolId, tokens, balances);\n        } else {\n            _setGeneralPoolBalances(poolId, balances);\n        }\n    }\n\n    // Temporary function to work around stack-too-deep\n    function _callOnJoinPoolAndReceiveAssets(\n        bytes32 poolId,\n        IAsset[] memory assets,\n        bytes32[] memory balances,\n        address sender,\n        address recipient,\n        bytes memory userData,\n        uint256[] memory maxAmountsIn,\n        bool fromInternalBalance\n    ) private {\n        // Call the `onJoinPool` hook to get the amounts to send to the Pool and to charge as protocol swap fees for\n        // each token.\n        (uint256[] memory amountsIn, uint256[] memory dueProtocolFeeAmounts) = _callOnJoinPool(\n            poolId,\n            assets,\n            balances,\n            sender,\n            recipient,\n            userData\n        );\n\n        _receivePoolAssets(\n            assets,\n            balances,\n            amountsIn,\n            dueProtocolFeeAmounts,\n            maxAmountsIn,\n            sender,\n            fromInternalBalance\n        );\n\n        emit PoolJoined(poolId, sender, _translateToIERC20(assets), amountsIn, dueProtocolFeeAmounts);\n    }\n\n    // Temporary function to work around stack-too-deep\n    function _receivePoolAssets(\n        IAsset[] memory assets,\n        bytes32[] memory balances,\n        uint256[] memory amountsIn,\n        uint256[] memory dueProtocolFeeAmounts,\n        uint256[] memory maxAmountsIn,\n        address sender,\n        bool fromInternalBalance\n    ) private {\n        bool ethAssetSeen = false;\n        uint256 wrappedETH = 0;\n\n        for (uint256 i = 0; i < assets.length; ++i) {\n            uint256 amountIn = amountsIn[i];\n            require(amountIn <= maxAmountsIn[i], \"JOIN_ABOVE_MAX\");\n\n            // Receive assets from the caller - possibly from Internal Balance\n            _receiveAsset(assets[i], amountIn, sender, fromInternalBalance);\n\n            if (_isETH(assets[i])) {\n                ethAssetSeen = true;\n                wrappedETH = wrappedETH.add(amountIn);\n            }\n\n            uint256 feeToPay = dueProtocolFeeAmounts[i];\n\n            // Compute the new Pool balances - we reuse the `balances` array to avoid allocating more memory. Note that\n            // due protocol fees might be larger than amounts in, resulting in an overall decrease of the Pool's balance\n            // for a token.\n            balances[i] = amountIn >= feeToPay\n                ? balances[i].increaseCash(amountIn - feeToPay) // Don't need checked arithmetic\n                : balances[i].decreaseCash(feeToPay - amountIn); // Same as -(int256(amountIn) - int256(feeToPay))\n\n            _increaseCollectedFees(_translateToIERC20(assets[i]), feeToPay);\n        }\n\n        // We prevent user error by reverting if ETH was sent but not referenced by any asset.\n        _ensureNoUnallocatedETH(ethAssetSeen);\n\n        // By returning the excess ETH, we also check that at least wrappedETH has been received.\n        _returnExcessEthToCaller(wrappedETH);\n    }\n\n    function exitPool(\n        bytes32 poolId,\n        address sender,\n        address payable recipient,\n        IAsset[] memory assets,\n        uint256[] memory minAmountsOut,\n        bool toInternalBalance,\n        bytes memory userData\n    ) external override nonReentrant withRegisteredPool(poolId) authenticateFor(sender) {\n        InputHelpers.ensureInputLengthMatch(assets.length, minAmountsOut.length);\n\n        bytes32[] memory balances = _validateTokensAndGetBalances(poolId, assets);\n\n        // Call the `onExitPool` hook to get the amounts to take from the Pool and to charge as protocol swap fees for\n        // each token.\n        (uint256[] memory amountsOut, uint256[] memory dueProtocolFeeAmounts) = _callOnExitPool(\n            poolId,\n            assets,\n            balances,\n            sender,\n            recipient,\n            userData\n        );\n\n        for (uint256 i = 0; i < assets.length; ++i) {\n            require(amountsOut[i] >= minAmountsOut[i], \"EXIT_BELOW_MIN\");\n            uint256 amountOut = amountsOut[i];\n\n            // Send tokens from the recipient - possibly to Internal Balance\n            // Tokens deposited to Internal Balance are not later exempt from withdrawal fees.\n\n            uint256 withdrawFee = toInternalBalance ? 0 : _calculateProtocolWithdrawFeeAmount(amountOut);\n            _sendAsset(assets[i], amountOut.sub(withdrawFee), recipient, toInternalBalance, false);\n\n            uint256 protocolSwapFee = dueProtocolFeeAmounts[i];\n\n            // Compute the new Pool balances - we reuse the `balances` array to avoid allocating more memory. A Pool's\n            // token balance always decreases after an exit (potentially by 0).\n            uint256 delta = amountOut.add(protocolSwapFee);\n            balances[i] = balances[i].decreaseCash(delta);\n\n            _increaseCollectedFees(_translateToIERC20(assets[i]), protocolSwapFee.add(withdrawFee));\n        }\n\n        IERC20[] memory tokens = _translateToIERC20(assets);\n\n        // Update the Pool's balance - how this is done depends on the Pool specialization setting.\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n        if (specialization == PoolSpecialization.TWO_TOKEN) {\n            _setTwoTokenPoolCashBalances(poolId, tokens[0], balances[0], tokens[1], balances[1]);\n        } else if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            _setMinimalSwapInfoPoolBalances(poolId, tokens, balances);\n        } else {\n            _setGeneralPoolBalances(poolId, balances);\n        }\n\n        emit PoolExited(poolId, sender, tokens, amountsOut, dueProtocolFeeAmounts);\n    }\n\n    /**\n     * @dev Internal helper to call the `onJoinPool` hook on a Pool's contract and perform basic validation on the\n     * returned values. Avoid stack-too-deep issues.\n     */\n    function _callOnJoinPool(\n        bytes32 poolId,\n        IAsset[] memory assets,\n        bytes32[] memory balances,\n        address sender,\n        address recipient,\n        bytes memory userData\n    ) private returns (uint256[] memory amountsIn, uint256[] memory dueProtocolFeeAmounts) {\n        (uint256[] memory totalBalances, uint256 latestBlockNumberUsed) = balances.totalsAndMaxBlockNumber();\n\n        address pool = _getPoolAddress(poolId);\n        (amountsIn, dueProtocolFeeAmounts) = IBasePool(pool).onJoinPool(\n            poolId,\n            sender,\n            recipient,\n            totalBalances,\n            latestBlockNumberUsed,\n            _getProtocolSwapFee(),\n            userData\n        );\n\n        InputHelpers.ensureInputLengthMatch(assets.length, amountsIn.length, dueProtocolFeeAmounts.length);\n    }\n\n    /**\n     * @dev Internal helper to call the `onExitPool` hook on a Pool's contract and perform basic validation on the\n     * returned values. Avoid stack-too-deep issues.\n     */\n    function _callOnExitPool(\n        bytes32 poolId,\n        IAsset[] memory assets,\n        bytes32[] memory balances,\n        address sender,\n        address recipient,\n        bytes memory userData\n    ) private returns (uint256[] memory amountsOut, uint256[] memory dueProtocolFeeAmounts) {\n        (uint256[] memory totalBalances, uint256 latestBlockNumberUsed) = balances.totalsAndMaxBlockNumber();\n\n        address pool = _getPoolAddress(poolId);\n        (amountsOut, dueProtocolFeeAmounts) = IBasePool(pool).onExitPool(\n            poolId,\n            sender,\n            recipient,\n            totalBalances,\n            latestBlockNumberUsed,\n            _getProtocolSwapFee(),\n            userData\n        );\n\n        InputHelpers.ensureInputLengthMatch(assets.length, amountsOut.length, dueProtocolFeeAmounts.length);\n    }\n\n    /**\n     * @dev Returns the total balance for `poolId`'s `expectedTokens`.\n     *\n     * `expectedTokens` must equal exactly the token array returned by `getPoolTokens`: both arrays must have the same\n     * length, elements and order.\n     */\n    function _validateTokensAndGetBalances(bytes32 poolId, IAsset[] memory expectedAssets)\n        internal\n        view\n        returns (bytes32[] memory)\n    {\n        (IERC20[] memory actualTokens, bytes32[] memory balances) = _getPoolTokens(poolId);\n        InputHelpers.ensureInputLengthMatch(actualTokens.length, expectedAssets.length);\n\n        for (uint256 i = 0; i < actualTokens.length; ++i) {\n            require(actualTokens[i] == _translateToIERC20(expectedAssets[i]), \"TOKENS_MISMATCH\");\n        }\n\n        return balances;\n    }\n\n    // Assets under management\n\n    function getPoolAssetManagers(bytes32 poolId, IERC20[] memory tokens)\n        external\n        view\n        override\n        returns (address[] memory assetManagers)\n    {\n        _ensureRegisteredPool(poolId);\n        assetManagers = new address[](tokens.length);\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n\n            _ensureTokenRegistered(poolId, token);\n            assetManagers[i] = _poolAssetManagers[poolId][token];\n        }\n    }\n\n    function withdrawFromPoolBalance(bytes32 poolId, AssetManagerTransfer[] memory transfers)\n        external\n        override\n        nonReentrant\n        noEmergencyPeriod\n    {\n        _ensureRegisteredPool(poolId);\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n\n        for (uint256 i = 0; i < transfers.length; ++i) {\n            IERC20 token = transfers[i].token;\n            _ensurePoolAssetManagerIsSender(poolId, token);\n\n            uint256 amount = transfers[i].amount;\n            if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n                _minimalSwapInfoPoolCashToManaged(poolId, token, amount);\n            } else if (specialization == PoolSpecialization.TWO_TOKEN) {\n                _twoTokenPoolCashToManaged(poolId, token, amount);\n            } else {\n                _generalPoolCashToManaged(poolId, token, amount);\n            }\n\n            token.safeTransfer(msg.sender, amount);\n            emit PoolBalanceChanged(poolId, msg.sender, token, -(amount.toInt256()));\n        }\n    }\n\n    function depositToPoolBalance(bytes32 poolId, AssetManagerTransfer[] memory transfers)\n        external\n        override\n        nonReentrant\n        noEmergencyPeriod\n    {\n        _ensureRegisteredPool(poolId);\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n\n        for (uint256 i = 0; i < transfers.length; ++i) {\n            IERC20 token = transfers[i].token;\n            _ensurePoolAssetManagerIsSender(poolId, token);\n\n            uint256 amount = transfers[i].amount;\n            if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n                _minimalSwapInfoPoolManagedToCash(poolId, token, amount);\n            } else if (specialization == PoolSpecialization.TWO_TOKEN) {\n                _twoTokenPoolManagedToCash(poolId, token, amount);\n            } else {\n                _generalPoolManagedToCash(poolId, token, amount);\n            }\n\n            token.safeTransferFrom(msg.sender, address(this), amount);\n            emit PoolBalanceChanged(poolId, msg.sender, token, amount.toInt256());\n        }\n    }\n\n    function updateManagedBalance(bytes32 poolId, AssetManagerTransfer[] memory transfers)\n        external\n        override\n        nonReentrant\n        noEmergencyPeriod\n    {\n        _ensureRegisteredPool(poolId);\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n\n        for (uint256 i = 0; i < transfers.length; ++i) {\n            IERC20 token = transfers[i].token;\n            _ensurePoolAssetManagerIsSender(poolId, token);\n\n            uint256 amount = transfers[i].amount;\n            if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n                _setMinimalSwapInfoPoolManagedBalance(poolId, token, amount);\n            } else if (specialization == PoolSpecialization.TWO_TOKEN) {\n                _setTwoTokenPoolManagedBalance(poolId, token, amount);\n            } else {\n                _setGeneralPoolManagedBalance(poolId, token, amount);\n            }\n        }\n    }\n\n    /**\n     * @dev Returns all of `poolId`'s registered tokens, along with their raw balances.\n     */\n    function _getPoolTokens(bytes32 poolId) internal view returns (IERC20[] memory tokens, bytes32[] memory balances) {\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n        if (specialization == PoolSpecialization.TWO_TOKEN) {\n            return _getTwoTokenPoolTokens(poolId);\n        } else if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            return _getMinimalSwapInfoPoolTokens(poolId);\n        } else {\n            return _getGeneralPoolTokens(poolId);\n        }\n    }\n\n    /**\n     * @dev Reverts unless `poolId` corresponds to a registered Pool.\n     */\n    function _ensureRegisteredPool(bytes32 poolId) internal view {\n        require(_isPoolRegistered[poolId], \"INVALID_POOL_ID\");\n    }\n\n    /**\n     * @dev Reverts unless `poolId` corresponds to a registered Pool, and the caller is the Pool's contract.\n     */\n    function _ensurePoolIsSender(bytes32 poolId) private view {\n        _ensureRegisteredPool(poolId);\n        address pool = _getPoolAddress(poolId);\n        require(pool == msg.sender, \"CALLER_NOT_POOL\");\n    }\n\n    /**\n     * @dev Reverts unless `poolId` corresponds to a registered Pool, `token` is registered for that Pool, and the\n     * caller is the Pool's Asset Manager for `token`.\n     */\n    function _ensurePoolAssetManagerIsSender(bytes32 poolId, IERC20 token) private view {\n        _ensureTokenRegistered(poolId, token);\n        require(_poolAssetManagers[poolId][token] == msg.sender, \"SENDER_NOT_ASSET_MANAGER\");\n    }\n\n    /**\n     * @dev Reverts unless `token` is registered for `poolId`.\n     */\n    function _ensureTokenRegistered(bytes32 poolId, IERC20 token) private view {\n        require(_isTokenRegistered(poolId, token), \"TOKEN_NOT_REGISTERED\");\n    }\n\n    /**\n     * @dev Returns true if `token` is registered for `poolId`.\n     */\n    function _isTokenRegistered(bytes32 poolId, IERC20 token) private view returns (bool) {\n        PoolSpecialization specialization = _getPoolSpecialization(poolId);\n        if (specialization == PoolSpecialization.TWO_TOKEN) {\n            return _isTwoTokenPoolTokenRegistered(poolId, token);\n        } else if (specialization == PoolSpecialization.MINIMAL_SWAP_INFO) {\n            return _isMinimalSwapInfoPoolTokenRegistered(poolId, token);\n        } else {\n            return _isGeneralPoolTokenRegistered(poolId, token);\n        }\n    }\n}\n"
    },
    "contracts/vault/interfaces/IPoolSwapStructs.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\ninterface IPoolSwapStructs {\n    // This is not really an interface - it just defines common structs used by other interfaces: IGeneralPool and\n    // IMinimalSwapInfoPool.\n\n    // This data structure represents a request for a token swap, where the amount received by the Pool is known.\n    //\n    // `tokenIn` and `tokenOut` are the tokens the Pool will receive and send, respectively. `amountIn` is the number of\n    // `tokenIn` tokens that the Pool will receive.\n    //\n    // `poolId` is the ID of the Pool involved in the swap - this is useful for Pool contracts that implement more than\n    // one Pool.\n    //\n    // All other fields are not strictly necessary for most swaps, but are provided to support advanced scenarios in\n    // some Pools.\n    // `from` is the origin address where funds the Pool receives are coming from, and `to` is the destination address\n    // where the funds the Pool sends are going to.\n    // `userData` is extra data provided by the caller - typically a signature from a trusted party.\n    struct SwapRequestGivenIn {\n        IERC20 tokenIn;\n        IERC20 tokenOut;\n        uint256 amountIn;\n        // Misc data\n        bytes32 poolId;\n        uint256 latestBlockNumberUsed;\n        address from;\n        address to;\n        bytes userData;\n    }\n\n    // This data structure represents a request for a token swap, where the amount sent by the Pool is known.\n    //\n    // `tokenIn` and `tokenOut` are the tokens the Pool will receive and send, respectively. `amountOut` is the number\n    // of `tokenOut` tokens that the Pool will send.\n    //\n    // All other fields are not strictly necessary for most swaps, but are provided to support advanced scenarios in\n    // some Pools.\n    // `poolId` is the ID of the Pool involved in the swap - this is useful for Pool contracts that implement more than\n    // one Pool.\n    // `from` is the origin address where funds the Pool receives are coming from, and `to` is the destination address\n    // where the funds the Pool sends are going to.\n    // `userData` is extra data provided by the caller - typically a signature from a trusted party.\n    struct SwapRequestGivenOut {\n        IERC20 tokenIn;\n        IERC20 tokenOut;\n        uint256 amountOut;\n        // Misc data\n        bytes32 poolId;\n        uint256 latestBlockNumberUsed;\n        address from;\n        address to;\n        bytes userData;\n    }\n}\n"
    },
    "contracts/vault/interfaces/IGeneralPool.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"./IBasePool.sol\";\nimport \"./IPoolSwapStructs.sol\";\n\n/**\n * @dev Interface contracts for Pools with the general specialization setting should implement.\n */\ninterface IGeneralPool is IBasePool {\n    /**\n     * @dev Called by the Vault when a user calls `IVault.batchSwapGivenIn` to swap with this Pool. Returns the number\n     * of tokens the Pool will grant to the user as part of the swap.\n     *\n     * This can be often implemented by a `view` function, since many pricing algorithms don't need to track state\n     * changes in swaps. However, contracts implementing this in non-view functions should check that the caller is\n     * indeed the Vault.\n     */\n    function onSwapGivenIn(\n        IPoolSwapStructs.SwapRequestGivenIn calldata swapRequest,\n        uint256[] calldata balances,\n        uint256 indexIn,\n        uint256 indexOut\n    ) external returns (uint256 amountOut);\n\n    /**\n     * @dev Called by the Vault when a user calls `IVault.batchSwapGivenOut` to swap with this Pool. Returns the number\n     * of tokens the user must grant to the Pool as part of the swap.\n     *\n     * This can be often implemented by a `view` function, since many pricing algorithms don't need to track state\n     * changes in swaps. However, contracts implementing this in non-view functions should check that the caller is\n     * indeed the Vault.\n     */\n    function onSwapGivenOut(\n        IPoolSwapStructs.SwapRequestGivenOut calldata swapRequest,\n        uint256[] calldata balances,\n        uint256 indexIn,\n        uint256 indexOut\n    ) external returns (uint256 amountIn);\n}\n"
    },
    "contracts/vault/interfaces/IMinimalSwapInfoPool.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"./IBasePool.sol\";\nimport \"./IPoolSwapStructs.sol\";\n\n/**\n * @dev Interface contracts for Pools with the minimal swap info or two token specialization settings should implement.\n */\ninterface IMinimalSwapInfoPool is IBasePool {\n    /**\n     * @dev Called by the Vault when a user calls `IVault.batchSwapGivenIn` to swap with this Pool. Returns the number\n     * of tokens the Pool will grant to the user as part of the swap.\n     *\n     * This can be often implemented by a `view` function, since many pricing algorithms don't need to track state\n     * changes in swaps. However, contracts implementing this in non-view functions should check that the caller is\n     * indeed the Vault.\n     */\n    function onSwapGivenIn(\n        IPoolSwapStructs.SwapRequestGivenIn calldata swapRequest,\n        uint256 currentBalanceTokenIn,\n        uint256 currentBalanceTokenOut\n    ) external returns (uint256 amountOut);\n\n    /**\n     * @dev Called by the Vault when a user calls `IVault.batchSwapGivenOut` to swap with this Pool. Returns the number\n     * of tokens the user must grant to the Pool as part of the swap.\n     *\n     * This can be often implemented by a `view` function, since many pricing algorithms don't need to track state\n     * changes in swaps. However, contracts implementing this in non-view functions should check that the caller is\n     * indeed the Vault.\n     */\n    function onSwapGivenOut(\n        IPoolSwapStructs.SwapRequestGivenOut calldata swapRequest,\n        uint256 currentBalanceTokenIn,\n        uint256 currentBalanceTokenOut\n    ) external returns (uint256 amountIn);\n}\n"
    },
    "contracts/vault/balances/BalanceAllocation.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"../../lib/math/Math.sol\";\n\n// This library is used to create a data structure that represents a token's balance for a Pool. 'cash' is how many\n// tokens the Pool has sitting inside of the Vault. 'managed' is how many tokens were withdrawn from the Vault by the\n// Pool's Asset Manager. 'total' is the sum of these two, and represents the Pool's total token balance, including\n// tokens that are *not* inside of the Vault.\n//\n// 'cash' is updated whenever tokens enter and exit the Vault, while 'managed' is only updated if the reason tokens are\n// moving is due to an Asset Manager action. This is reflected in the different methods available: 'increaseCash'\n// and 'decreaseCash' for swaps and add/remove liquidity events, and 'cashToManaged' and 'managedToCash' for\n// events transferring funds to and from the asset manager.\n//\n// The Vault disallows the Pool's 'cash' ever becoming negative, in other words, it can never use any tokens that\n// are not inside of the Vault.\n//\n// One of the goals of this library is to store the entire token balance in a single storage slot, which is we we use\n// 112 bit unsigned integers for 'cash' and 'managed'. Since 'total' is also a 112 bit unsigned value, any combination\n// of 'cash' and 'managed' that yields a 'total' that doesn't fit in that range is disallowed.\n//\n// The remaining 32 bits of each storage slot are used to store the most recent block number where a balance was\n// updated. This can be used to implement price oracles that are resilient to 'sandwich' attacks.\n//\n// We could use a Solidity struct to pack these two values together in a single storage slot, but unfortunately Solidity\n// only allows for structs to live in either storage, calldata or memory. Because a memory struct still takes up a\n// slot in the stack (to store its memory location), and because the entire balance fits in a single stack slot (two\n// 112 bit values), using memory is strictly less gas performant. Therefore, we do manual packing and unpacking. The\n// type we use to represent these values is bytes32, as it doesn't have any arithmetic operations and therefore reduces\n// the chance of misuse.\nlibrary BalanceAllocation {\n    using Math for uint256;\n\n    // The 'cash' portion of the balance is stored in the least significant 112 bits of a 256 bit word, while the\n    // 'managed' part uses the following 112 bits. The remaining 32 bits are used to store the block number.\n\n    /**\n     * @dev Returns the total amount of Pool tokens, including those that are not currently in the Vault ('managed').\n     */\n    function total(bytes32 balance) internal pure returns (uint256) {\n        return cash(balance).add(managed(balance));\n    }\n\n    /**\n     * @dev Returns the amount of Pool tokens currently in the Vault.\n     */\n    function cash(bytes32 balance) internal pure returns (uint256) {\n        uint256 mask = 2**(112) - 1;\n        return uint256(balance) & mask;\n    }\n\n    /**\n     * @dev Returns the amount of Pool tokens that have been withdrawn (or reported) by its Asset Manager.\n     */\n    function managed(bytes32 balance) internal pure returns (uint256) {\n        uint256 mask = 2**(112) - 1;\n        return uint256(balance >> 112) & mask;\n    }\n\n    /**\n     * @dev Returns the last block number when a balance was updated.\n     */\n    function blockNumber(bytes32 balance) internal pure returns (uint256) {\n        uint256 mask = 2**(32) - 1;\n        return uint256(balance >> 224) & mask;\n    }\n\n    /**\n     * @dev Returns the total balance for each entry in `balances`.\n     */\n    function totals(bytes32[] memory balances) internal pure returns (uint256[] memory results) {\n        results = new uint256[](balances.length);\n        for (uint256 i = 0; i < results.length; i++) {\n            results[i] = total(balances[i]);\n        }\n    }\n\n    /**\n     * @dev Returns the total balance for each entry in `balances`, as well as the latest block number when any of them\n     * was last updated.\n     */\n    function totalsAndMaxBlockNumber(bytes32[] memory balances)\n        internal\n        pure\n        returns (uint256[] memory results, uint256 maxBlockNumber)\n    {\n        maxBlockNumber = 0;\n        results = new uint256[](balances.length);\n\n        for (uint256 i = 0; i < results.length; i++) {\n            bytes32 balance = balances[i];\n            results[i] = total(balance);\n            maxBlockNumber = Math.max(maxBlockNumber, blockNumber(balance));\n        }\n    }\n\n    /**\n     * @dev Returns true if `balance`'s total balance is zero. Costs less gas than computing the total.\n     */\n    function isZero(bytes32 balance) internal pure returns (bool) {\n        // We simply need to check the least significant 224 bytes of the word, the block number does not affect this.\n        uint256 mask = 2**(224) - 1;\n        return (uint256(balance) & mask) == 0;\n    }\n\n    /**\n     * @dev Returns true if `balance`'s total balance is not zero. Costs less gas than computing the total.\n     */\n    function isNotZero(bytes32 balance) internal pure returns (bool) {\n        return !isZero(balance);\n    }\n\n    /**\n     * @dev Packs together cash and managed amounts with a block number to create a balance value.\n     * Critically, this also checks the sum of cash and external doesn't overflow, that is, that `total()` can be\n     * computed.\n     */\n    function toBalance(\n        uint256 _cash,\n        uint256 _managed,\n        uint256 _blockNumber\n    ) internal pure returns (bytes32) {\n        uint256 balance = _cash + _managed;\n        require(balance >= _cash && balance < 2**112, \"BALANCE_TOTAL_OVERFLOW\");\n        // We assume the block number will fits in an uint32 - this is expected to hold for at least a few decades.\n        return _pack(_cash, _managed, _blockNumber);\n    }\n\n    /**\n     * @dev Increases a Pool's 'cash' (and therefore its 'total'). Called when Pool tokens are sent to the Vault (except\n     * when an Asset Manager action decreases the managed balance).\n     */\n    function increaseCash(bytes32 balance, uint256 amount) internal view returns (bytes32) {\n        uint256 newCash = cash(balance).add(amount);\n        uint256 currentManaged = managed(balance);\n        uint256 newBlockNumber = block.number;\n\n        return toBalance(newCash, currentManaged, newBlockNumber);\n    }\n\n    /**\n     * @dev Decreases a Pool's 'cash' (and therefore its 'total'). Called when Pool tokens are sent from the Vault\n     * (except as an Asset Manager action that increases the managed balance).\n     */\n    function decreaseCash(bytes32 balance, uint256 amount) internal view returns (bytes32) {\n        uint256 newCash = cash(balance).sub(amount);\n        uint256 currentManaged = managed(balance);\n        uint256 newBlockNumber = block.number;\n\n        return toBalance(newCash, currentManaged, newBlockNumber);\n    }\n\n    /**\n     * @dev Moves 'cash' into 'managed', leaving 'total' unchanged. Called when Pool tokens are sent from the Vault\n     * when an Asset Manager action increases the managed balance.\n     */\n    function cashToManaged(bytes32 balance, uint256 amount) internal pure returns (bytes32) {\n        uint256 newCash = cash(balance).sub(amount);\n        uint256 newManaged = managed(balance).add(amount);\n        uint256 currentBlockNumber = blockNumber(balance);\n\n        return toBalance(newCash, newManaged, currentBlockNumber);\n    }\n\n    /**\n     * @dev Moves 'managed' into 'cash', leaving 'total' unchanged. Called when Pool tokens are sent to the Vault when\n     * an Asset Manager action decreases the managed balance.\n     */\n    function managedToCash(bytes32 balance, uint256 amount) internal pure returns (bytes32) {\n        uint256 newCash = cash(balance).add(amount);\n        uint256 newManaged = managed(balance).sub(amount);\n        uint256 currentBlockNumber = blockNumber(balance);\n\n        return toBalance(newCash, newManaged, currentBlockNumber);\n    }\n\n    /**\n     * @dev Sets 'managed' balance to an arbitrary value, changing 'total'. Called when the Asset Manager reports\n     * profits or losses. It's the Manager's responsibility to provide a meaningful value.\n     */\n    function setManaged(bytes32 balance, uint256 newManaged) internal view returns (bytes32) {\n        uint256 currentCash = cash(balance);\n        uint256 newBlockNumber = block.number;\n        return toBalance(currentCash, newManaged, newBlockNumber);\n    }\n\n    // Alternative mode for Pools with the token token specialization setting\n\n    // Instead of storing cash and external for each token in a single storage slot, two token pools store the cash for\n    // both tokens in the same slot, and the external for both in another one. This reduces the gas cost for swaps,\n    // because the only slot that needs to be updated is the one with the cash. However, it also means that managing\n    // balances is more cumbersome, as both tokens need to be read/written at the same time.\n    // The field with both cash balances packed is called sharedCash, and the one with external amounts is called\n    // sharedManaged. These two are collectively called the 'shared' balance fields. In both of these, the portion\n    // that corresponds to token A is stored in the least significant 112 bits of a 256 bit word, while token B's part\n    // uses the most significant 112 bits.\n    // Because only cash is written to during a swap, we store the block number there. Typically Pools have a distinct\n    // block number per token: in the case of two token Pools this is not necessary, as both values will be the same.\n\n    /**\n     * @dev Unpacks the shared token A and token B cash and managed balances into the balance for token A.\n     */\n    function fromSharedToBalanceA(bytes32 sharedCash, bytes32 sharedManaged) internal pure returns (bytes32) {\n        return toBalance(_decodeBalanceA(sharedCash), _decodeBalanceA(sharedManaged), blockNumber(sharedCash));\n    }\n\n    /**\n     * @dev Unpacks the shared token A and token B cash and managed balances into the balance for token B.\n     */\n    function fromSharedToBalanceB(bytes32 sharedCash, bytes32 sharedManaged) internal pure returns (bytes32) {\n        return toBalance(_decodeBalanceB(sharedCash), _decodeBalanceB(sharedManaged), blockNumber(sharedCash));\n    }\n\n    /**\n     * @dev Returns the sharedCash shared field, given the current balances for tokenA and tokenB.\n     */\n    function toSharedCash(bytes32 tokenABalance, bytes32 tokenBBalance) internal pure returns (bytes32) {\n        // Both balances have the block number, since both balances are always updated at the same time it does not\n        // mater where we pick it from.\n        return _pack(cash(tokenABalance), cash(tokenBBalance), blockNumber(tokenABalance));\n    }\n\n    /**\n     * @dev Returns the sharedManaged shared field, given the current balances for tokenA and tokenB.\n     */\n    function toSharedManaged(bytes32 tokenABalance, bytes32 tokenBBalance) internal pure returns (bytes32) {\n        return _pack(managed(tokenABalance), managed(tokenBBalance), 0);\n    }\n\n    /**\n     * @dev Unpacks the balance corresponding to token A for a shared balance\n     * Note that this function can be used to decode both cash and managed balances.\n     */\n    function _decodeBalanceA(bytes32 sharedBalance) private pure returns (uint256) {\n        uint256 mask = 2**(112) - 1;\n        return uint256(sharedBalance) & mask;\n    }\n\n    /**\n     * @dev Unpacks the balance corresponding to token B for a shared balance\n     * Note that this function can be used to decode both cash and managed balances.\n     */\n    function _decodeBalanceB(bytes32 sharedBalance) private pure returns (uint256) {\n        uint256 mask = 2**(112) - 1;\n        return uint256(sharedBalance >> 112) & mask;\n    }\n\n    // Shared functions\n\n    /**\n     * @dev Packs together two uint112 and one uint32 into a bytes32\n     */\n    function _pack(\n        uint256 _leastSignificant,\n        uint256 _midSignificant,\n        uint256 _mostSignificant\n    ) private pure returns (bytes32) {\n        return bytes32((_mostSignificant << 224) + (_midSignificant << 112) + _leastSignificant);\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/SafeCast.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\n\n/**\n * @dev Wrappers over Solidity's uintXX/intXX casting operators with added overflow\n * checks.\n *\n * Downcasting from uint256/int256 in Solidity does not revert on overflow. This can\n * easily result in undesired exploitation or bugs, since developers usually\n * assume that overflows raise errors. `SafeCast` restores this intuition by\n * reverting the transaction when such an operation overflows.\n *\n * Using this library instead of the unchecked operations eliminates an entire\n * class of bugs, so it's recommended to use it always.\n *\n * Can be combined with {SafeMath} and {SignedSafeMath} to extend it to smaller types, by performing\n * all math on `uint256` and `int256` and then downcasting.\n */\nlibrary SafeCast {\n\n    /**\n     * @dev Returns the downcasted uint128 from uint256, reverting on\n     * overflow (when the input is greater than largest uint128).\n     *\n     * Counterpart to Solidity's `uint128` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 128 bits\n     */\n    function toUint128(uint256 value) internal pure returns (uint128) {\n        require(value < 2**128, \"SafeCast: value doesn\\'t fit in 128 bits\");\n        return uint128(value);\n    }\n\n    /**\n     * @dev Returns the downcasted uint64 from uint256, reverting on\n     * overflow (when the input is greater than largest uint64).\n     *\n     * Counterpart to Solidity's `uint64` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 64 bits\n     */\n    function toUint64(uint256 value) internal pure returns (uint64) {\n        require(value < 2**64, \"SafeCast: value doesn\\'t fit in 64 bits\");\n        return uint64(value);\n    }\n\n    /**\n     * @dev Returns the downcasted uint32 from uint256, reverting on\n     * overflow (when the input is greater than largest uint32).\n     *\n     * Counterpart to Solidity's `uint32` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 32 bits\n     */\n    function toUint32(uint256 value) internal pure returns (uint32) {\n        require(value < 2**32, \"SafeCast: value doesn\\'t fit in 32 bits\");\n        return uint32(value);\n    }\n\n    /**\n     * @dev Returns the downcasted uint16 from uint256, reverting on\n     * overflow (when the input is greater than largest uint16).\n     *\n     * Counterpart to Solidity's `uint16` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 16 bits\n     */\n    function toUint16(uint256 value) internal pure returns (uint16) {\n        require(value < 2**16, \"SafeCast: value doesn\\'t fit in 16 bits\");\n        return uint16(value);\n    }\n\n    /**\n     * @dev Returns the downcasted uint8 from uint256, reverting on\n     * overflow (when the input is greater than largest uint8).\n     *\n     * Counterpart to Solidity's `uint8` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 8 bits.\n     */\n    function toUint8(uint256 value) internal pure returns (uint8) {\n        require(value < 2**8, \"SafeCast: value doesn\\'t fit in 8 bits\");\n        return uint8(value);\n    }\n\n    /**\n     * @dev Converts a signed int256 into an unsigned uint256.\n     *\n     * Requirements:\n     *\n     * - input must be greater than or equal to 0.\n     */\n    function toUint256(int256 value) internal pure returns (uint256) {\n        require(value >= 0, \"SafeCast: value must be positive\");\n        return uint256(value);\n    }\n\n    /**\n     * @dev Returns the downcasted int128 from int256, reverting on\n     * overflow (when the input is less than smallest int128 or\n     * greater than largest int128).\n     *\n     * Counterpart to Solidity's `int128` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 128 bits\n     *\n     * _Available since v3.1._\n     */\n    function toInt128(int256 value) internal pure returns (int128) {\n        require(value >= -2**127 && value < 2**127, \"SafeCast: value doesn\\'t fit in 128 bits\");\n        return int128(value);\n    }\n\n    /**\n     * @dev Returns the downcasted int64 from int256, reverting on\n     * overflow (when the input is less than smallest int64 or\n     * greater than largest int64).\n     *\n     * Counterpart to Solidity's `int64` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 64 bits\n     *\n     * _Available since v3.1._\n     */\n    function toInt64(int256 value) internal pure returns (int64) {\n        require(value >= -2**63 && value < 2**63, \"SafeCast: value doesn\\'t fit in 64 bits\");\n        return int64(value);\n    }\n\n    /**\n     * @dev Returns the downcasted int32 from int256, reverting on\n     * overflow (when the input is less than smallest int32 or\n     * greater than largest int32).\n     *\n     * Counterpart to Solidity's `int32` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 32 bits\n     *\n     * _Available since v3.1._\n     */\n    function toInt32(int256 value) internal pure returns (int32) {\n        require(value >= -2**31 && value < 2**31, \"SafeCast: value doesn\\'t fit in 32 bits\");\n        return int32(value);\n    }\n\n    /**\n     * @dev Returns the downcasted int16 from int256, reverting on\n     * overflow (when the input is less than smallest int16 or\n     * greater than largest int16).\n     *\n     * Counterpart to Solidity's `int16` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 16 bits\n     *\n     * _Available since v3.1._\n     */\n    function toInt16(int256 value) internal pure returns (int16) {\n        require(value >= -2**15 && value < 2**15, \"SafeCast: value doesn\\'t fit in 16 bits\");\n        return int16(value);\n    }\n\n    /**\n     * @dev Returns the downcasted int8 from int256, reverting on\n     * overflow (when the input is less than smallest int8 or\n     * greater than largest int8).\n     *\n     * Counterpart to Solidity's `int8` operator.\n     *\n     * Requirements:\n     *\n     * - input must fit into 8 bits.\n     *\n     * _Available since v3.1._\n     */\n    function toInt8(int256 value) internal pure returns (int8) {\n        require(value >= -2**7 && value < 2**7, \"SafeCast: value doesn\\'t fit in 8 bits\");\n        return int8(value);\n    }\n\n    /**\n     * @dev Converts an unsigned uint256 into a signed int256.\n     *\n     * Requirements:\n     *\n     * - input must be less than or equal to maxInt256.\n     */\n    function toInt256(uint256 value) internal pure returns (int256) {\n        require(value < 2**255, \"SafeCast: value doesn't fit in an int256\");\n        return int256(value);\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Counters.sol": {
      "content": "// SPDX-License-Identifier: MIT\n\npragma solidity ^0.7.0;\n\nimport \"../math/SafeMath.sol\";\n\n/**\n * @title Counters\n * @author Matt Condon (@shrugs)\n * @dev Provides counters that can only be incremented or decremented by one. This can be used e.g. to track the number\n * of elements in a mapping, issuing ERC721 ids, or counting request ids.\n *\n * Include with `using Counters for Counters.Counter;`\n * Since it is not possible to overflow a 256 bit integer with increments of one, `increment` can skip the {SafeMath}\n * overflow check, thereby saving gas. This does assume however correct usage, in that the underlying `_value` is never\n * directly accessed.\n */\nlibrary Counters {\n    using SafeMath for uint256;\n\n    struct Counter {\n        // This variable should never be directly accessed by users of the library: interactions must be restricted to\n        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add\n        // this feature: see https://github.com/ethereum/solidity/issues/4637\n        uint256 _value; // default: 0\n    }\n\n    function current(Counter storage counter) internal view returns (uint256) {\n        return counter._value;\n    }\n\n    function increment(Counter storage counter) internal {\n        // The {SafeMath} overflow check can be skipped here, see the comment at the top\n        counter._value += 1;\n    }\n\n    function decrement(Counter storage counter) internal {\n        counter._value = counter._value.sub(1);\n    }\n}\n"
    },
    "contracts/vault/interfaces/IBasePool.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"./IVault.sol\";\n\n/**\n * @dev Interface all Pool contracts should implement. Note that this is not the complete Pool contract interface, as it\n * is missing the swap hooks: Pool contracts should instead inherit from either IGeneralPool or IMinimalSwapInfoPool.\n */\ninterface IBasePool {\n    /**\n     * @dev Called by the Vault when a user calls `IVault.joinPool` to join this Pool. Returns how many tokens the user\n     * should provide for each registered token, as well as how many protocol fees the Pool owes to the Vault. After\n     * returning, the Vault will take tokens from the `sender` and add it to the Pool's balance, as well as collect\n     * reported protocol fees. The current protocol swap fee percentage is provided to help compute this value.\n     *\n     * Due protocol fees are reported and charged on join events so that new users join the Pool free of debt.\n     *\n     * `sender` is the account performing the join (from whom tokens will be withdrawn), and `recipient` an account\n     * designated to receive any benefits (typically pool shares). `currentBalances` contains the total token balances\n     * for each token the Pool registered in the Vault, in the same order as `IVault.getPoolTokens` would return.\n     *\n     * `latestBlockNumberUsed` is the last block number in which any of the Pool's registered tokens last changed its\n     * balance. This can be used to implement price oracles that are resilient to 'sandwich' attacks.\n     *\n     * Contracts implementing this function should check that the caller is indeed the Vault before performing any\n     * state-changing operations, such as minting pool shares.\n     */\n    function onJoinPool(\n        bytes32 poolId,\n        address sender,\n        address recipient,\n        uint256[] calldata currentBalances,\n        uint256 latestBlockNumberUsed,\n        uint256 protocolSwapFee,\n        bytes calldata userData\n    ) external returns (uint256[] memory amountsIn, uint256[] memory dueProtocolFeeAmounts);\n\n    /**\n     * @dev Called by the Vault when a user calls `IVault.exitPool` to exit this Pool. Returns how many tokens the Vault\n     * should deduct from the Pool, as well as how many protocol fees the Pool owes to the Vault. After returning, the\n     * Vault will take tokens from the Pool's balance and add grant them to `recipient`, as well as collect reported\n     * protocol fees. The current protocol swap fee percentage is provided to help compute this value.\n     *\n     * Due protocol fees are reported and charged on exit events so that users exit the Pool having paid all debt.\n     *\n     * `sender` is the account performing the exit (typically the holder of pool shares), and `recipient` the account to\n     * which the Vault will grant tokens. `currentBalances` contains the total token balances for each token the Pool\n     * registered in the Vault, in the same order as `IVault.getPoolTokens` would return.\n     *\n     * `latestBlockNumberUsed` is the last block number in which any of the Pool's registered tokens last changed its\n     * balance. This can be used to implement price oracles that are resilient to 'sandwich' attacks.\n     *\n     * Contracts implementing this function should check that the caller is indeed the Vault before performing any\n     * state-changing operations, such as burning pool shares.\n     */\n    function onExitPool(\n        bytes32 poolId,\n        address sender,\n        address recipient,\n        uint256[] calldata currentBalances,\n        uint256 latestBlockNumberUsed,\n        uint256 protocolSwapFee,\n        bytes calldata userData\n    ) external returns (uint256[] memory amountsOut, uint256[] memory dueProtocolFeeAmounts);\n\n    /**\n     * @dev This function returns the appreciation of one BPT relative to the\n     * underlying tokens. This starts at 1 when the pool is created and grows over time\n     * It's the equivalent to Curve's get_virtual_price() function\n     */\n    function getRate() external view returns (uint256);\n}\n"
    },
    "contracts/vault/InternalBalance.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\npragma experimental ABIEncoderV2;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/token/ERC20/SafeERC20.sol\";\n\nimport \"../lib/math/Math.sol\";\nimport \"../lib/helpers/InputHelpers.sol\";\nimport \"../lib/helpers/ReentrancyGuard.sol\";\n\nimport \"./Fees.sol\";\nimport \"./AssetTransfersHandler.sol\";\nimport \"./balances/InternalBalanceAllocation.sol\";\n\nabstract contract InternalBalance is ReentrancyGuard, AssetTransfersHandler, Fees {\n    using Math for uint256;\n    using SafeERC20 for IERC20;\n    using InternalBalanceAllocation for bytes32;\n\n    // Stores all account's Internal Balance for each token.\n    mapping(address => mapping(IERC20 => bytes32)) private _internalTokenBalance;\n\n    function getInternalBalance(address user, IERC20[] memory tokens)\n        external\n        view\n        override\n        returns (uint256[] memory balances)\n    {\n        balances = new uint256[](tokens.length);\n        for (uint256 i = 0; i < tokens.length; i++) {\n            balances[i] = _getInternalBalance(user, tokens[i]).actual();\n        }\n    }\n\n    function depositToInternalBalance(AssetBalanceTransfer[] memory transfers)\n        external\n        payable\n        override\n        nonReentrant\n        noEmergencyPeriod\n    {\n        bool ethAssetSeen = false;\n        uint256 wrappedETH = 0;\n\n        for (uint256 i = 0; i < transfers.length; i++) {\n            address sender = transfers[i].sender;\n            _authenticateCallerFor(sender);\n\n            IAsset asset = transfers[i].asset;\n            uint256 amount = transfers[i].amount;\n            address recipient = transfers[i].recipient;\n\n            _increaseInternalBalance(recipient, _translateToIERC20(asset), amount, true);\n\n            // _receiveAsset does not check if the caller sent enough ETH, so we keep track of it independently (as\n            // multiple deposits may have all deposited ETH).\n            _receiveAsset(asset, amount, sender, false);\n            if (_isETH(asset)) {\n                ethAssetSeen = true;\n                wrappedETH = wrappedETH.add(amount);\n            }\n        }\n\n        // We prevent user error by reverting if ETH was sent but not allocated to any deposit.\n        _ensureNoUnallocatedETH(ethAssetSeen);\n\n        // By returning the excess ETH, we also check that at least wrappedETH has been received.\n        _returnExcessEthToCaller(wrappedETH);\n    }\n\n    function withdrawFromInternalBalance(AssetBalanceTransfer[] memory transfers) external override nonReentrant {\n        for (uint256 i = 0; i < transfers.length; i++) {\n            address sender = transfers[i].sender;\n            _authenticateCallerFor(sender);\n\n            IAsset asset = transfers[i].asset;\n            uint256 amount = transfers[i].amount;\n            address payable recipient = transfers[i].recipient;\n            IERC20 token = _translateToIERC20(asset);\n\n            uint256 amountToSend = amount;\n            (uint256 taxableAmount, ) = _decreaseInternalBalance(sender, token, amount, false);\n\n            if (taxableAmount > 0) {\n                uint256 feeAmount = _calculateProtocolWithdrawFeeAmount(taxableAmount);\n                _increaseCollectedFees(token, feeAmount);\n                amountToSend = amountToSend.sub(feeAmount);\n            }\n\n            _sendAsset(asset, amountToSend, recipient, false, false);\n        }\n    }\n\n    function transferInternalBalance(TokenBalanceTransfer[] memory transfers)\n        external\n        override\n        nonReentrant\n        noEmergencyPeriod\n    {\n        for (uint256 i = 0; i < transfers.length; i++) {\n            address sender = transfers[i].sender;\n            _authenticateCallerFor(sender);\n\n            IERC20 token = transfers[i].token;\n            uint256 amount = transfers[i].amount;\n            address recipient = transfers[i].recipient;\n\n            // Transferring internal balance to another account is not charged withdrawal fees\n            _decreaseInternalBalance(sender, token, amount, false);\n            // Tokens transferred internally are not later exempt from withdrawal fees.\n            _increaseInternalBalance(recipient, token, amount, false);\n        }\n    }\n\n    function transferToExternalBalance(TokenBalanceTransfer[] memory transfers) external override nonReentrant {\n        for (uint256 i = 0; i < transfers.length; i++) {\n            address sender = transfers[i].sender;\n            _authenticateCallerFor(sender);\n\n            IERC20 token = transfers[i].token;\n            uint256 amount = transfers[i].amount;\n            address recipient = transfers[i].recipient;\n\n            // Do not charge withdrawal fee, since it's just making use of the Vault's allowance\n            token.safeTransferFrom(sender, recipient, amount);\n        }\n    }\n\n    /**\n     * @dev Increases `account`'s Internal Balance for `token` by `amount`.\n     */\n    function _increaseInternalBalance(\n        address account,\n        IERC20 token,\n        uint256 amount,\n        bool trackExempt\n    ) internal override {\n        bytes32 currentInternalBalance = _getInternalBalance(account, token);\n        bytes32 newBalance = currentInternalBalance.increase(amount, trackExempt);\n\n        // Because Internal Balance is stored in 112 bits internally, we can safely cast to int256 as the value is\n        // guaranteed to fit.\n        emit InternalBalanceChanged(account, token, int256(amount));\n\n        _internalTokenBalance[account][token] = newBalance;\n    }\n\n    /**\n     * @dev Decreases `account`'s Internal Balance for `token` by `amount`.\n     * When `capped` the internal balance will be decreased as much as possible without reverting.\n     * @return taxableAmount The amount that should be used to charge fees. Some functionalities in the Vault\n     * allows users to avoid fees when working with internal balance deltas in the same block. This is the case for\n     * deposits and withdrawals for example.\n     * @return decreasedAmount The amount that was actually decreased, note this might not be equal to `amount` in\n     * case it was `capped` and the internal balance was actually lower than it.\n     */\n    function _decreaseInternalBalance(\n        address account,\n        IERC20 token,\n        uint256 amount,\n        bool capped\n    ) internal override returns (uint256, uint256) {\n        bytes32 currentInternalBalance = _getInternalBalance(account, token);\n        (bytes32 newBalance, uint256 taxableAmount, uint256 decreasedAmount) = currentInternalBalance.decrease(\n            amount,\n            capped\n        );\n\n        // Because Internal Balance is stored in 112 bits internally, we can safely cast to int256 as the value is\n        // guaranteed to fit.\n        emit InternalBalanceChanged(account, token, -int256(amount));\n\n        _internalTokenBalance[account][token] = newBalance;\n\n        return (taxableAmount, decreasedAmount);\n    }\n\n    /**\n     * @dev Returns `account`'s Internal Balance for `token`.\n     */\n    function _getInternalBalance(address account, IERC20 token) internal view returns (bytes32) {\n        return _internalTokenBalance[account][token];\n    }\n}\n"
    },
    "contracts/vault/balances/GeneralPoolsBalance.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\nimport \"./BalanceAllocation.sol\";\nimport \"../../lib/helpers/EnumerableMap.sol\";\n\ncontract GeneralPoolsBalance {\n    using BalanceAllocation for bytes32;\n    using EnumerableMap for EnumerableMap.IERC20ToBytes32Map;\n\n    // Data for Pools with General Pool Specialization setting\n    //\n    // These Pools use the IGeneralPool interface, which means the Vault must query the balance for *all* of their\n    // tokens in every swap. If we kept a mapping of token to balance plus a set (array) of tokens, it'd be very gas\n    // intensive to read all token addresses just to then do a lookup on the balance mapping.\n    // Instead, we use our customized EnumerableMap, which lets us read the N balances in N+1 storage accesses (one for\n    // the number of tokens in the Pool), as well as access the index of any token in a single read (required for the\n    // IGeneralPool call) and update an entry's value given its index.\n\n    mapping(bytes32 => EnumerableMap.IERC20ToBytes32Map) internal _generalPoolsBalances;\n\n    /**\n     * @dev Registers a list of tokens in a General Pool.\n     *\n     * Requirements:\n     *\n     * - Each token must not be the zero address.\n     * - Each token must not be registered in the Pool.\n     */\n    function _registerGeneralPoolTokens(bytes32 poolId, IERC20[] memory tokens) internal {\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[poolId];\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            require(token != IERC20(0), \"ZERO_ADDRESS_TOKEN\");\n            bool added = poolBalances.set(token, 0);\n            require(added, \"TOKEN_ALREADY_REGISTERED\");\n        }\n    }\n\n    /**\n     * @dev Deregisters a list of tokens in a General Pool.\n     *\n     * Requirements:\n     *\n     * - Each token must be registered in the Pool.\n     * - Each token must have non balance in the Vault.\n     */\n    function _deregisterGeneralPoolTokens(bytes32 poolId, IERC20[] memory tokens) internal {\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[poolId];\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            bytes32 currentBalance = _getGeneralPoolBalance(poolBalances, token);\n            require(currentBalance.isZero(), \"NONZERO_TOKEN_BALANCE\");\n\n            // We don't need to check remove's return value, since _getGeneralPoolBalance already checks that the token\n            // was registered.\n            poolBalances.remove(token);\n        }\n    }\n\n    function _setGeneralPoolBalances(bytes32 poolId, bytes32[] memory balances) internal {\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[poolId];\n        for (uint256 i = 0; i < balances.length; ++i) {\n            // Note we assume all balances are properly ordered.\n            // Thus, we can use `unchecked_setAt` to avoid one less storage read per call.\n            poolBalances.unchecked_setAt(i, balances[i]);\n        }\n    }\n\n    function _generalPoolCashToManaged(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateGeneralPoolBalance(poolId, token, BalanceAllocation.cashToManaged, amount);\n    }\n\n    function _generalPoolManagedToCash(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateGeneralPoolBalance(poolId, token, BalanceAllocation.managedToCash, amount);\n    }\n\n    function _setGeneralPoolManagedBalance(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateGeneralPoolBalance(poolId, token, BalanceAllocation.setManaged, amount);\n    }\n\n    function _updateGeneralPoolBalance(\n        bytes32 poolId,\n        IERC20 token,\n        function(bytes32, uint256) returns (bytes32) mutation,\n        uint256 amount\n    ) internal {\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[poolId];\n        bytes32 currentBalance = _getGeneralPoolBalance(poolBalances, token);\n        poolBalances.set(token, mutation(currentBalance, amount));\n    }\n\n    /**\n     * @dev Returns an array with all the tokens and balances in a General Pool.\n     * This order may change when tokens are added to or removed from the Pool.\n     */\n    function _getGeneralPoolTokens(bytes32 poolId)\n        internal\n        view\n        returns (IERC20[] memory tokens, bytes32[] memory balances)\n    {\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[poolId];\n        tokens = new IERC20[](poolBalances.length());\n        balances = new bytes32[](tokens.length);\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            // Because the iteration is bounded by `tokens.length` already fetched from the enumerable map,\n            // we can use `unchecked_at` as we know `i` is a valid token index, saving storage reads.\n            (IERC20 token, bytes32 balance) = poolBalances.unchecked_at(i);\n            tokens[i] = token;\n            balances[i] = balance;\n        }\n    }\n\n    function _getGeneralPoolBalance(bytes32 poolId, IERC20 token) internal view returns (bytes32) {\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[poolId];\n        return _getGeneralPoolBalance(poolBalances, token);\n    }\n\n    function _getGeneralPoolBalance(EnumerableMap.IERC20ToBytes32Map storage poolBalances, IERC20 token)\n        internal\n        view\n        returns (bytes32)\n    {\n        return poolBalances.get(token, \"TOKEN_NOT_REGISTERED\");\n    }\n\n    function _isGeneralPoolTokenRegistered(bytes32 poolId, IERC20 token) internal view returns (bool) {\n        EnumerableMap.IERC20ToBytes32Map storage poolBalances = _generalPoolsBalances[poolId];\n        return poolBalances.contains(token);\n    }\n}\n"
    },
    "contracts/vault/balances/MinimalSwapInfoPoolsBalance.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\nimport \"@openzeppelin/contracts/utils/EnumerableSet.sol\";\n\nimport \"./BalanceAllocation.sol\";\n\ncontract MinimalSwapInfoPoolsBalance {\n    using BalanceAllocation for bytes32;\n    using EnumerableSet for EnumerableSet.AddressSet;\n\n    // Data for Pools with Minimal Swap Info Specialization setting\n    //\n    // These Pools use the IMinimalSwapInfoPool interface, and so the Vault must read the balance of the two tokens\n    // in the swap. The best solution is to use a mapping from token to balance, which lets us read or write any token's\n    // balance in a single storage access.\n    // We also keep a set with all tokens in the Pool, and update this set when cash is added or removed from the pool.\n    // Tokens in the set always have a non-zero balance, so we don't need\n    // to check the set for token existence during a swap: the non-zero balance check achieves this for less gas.\n\n    mapping(bytes32 => EnumerableSet.AddressSet) internal _minimalSwapInfoPoolsTokens;\n    mapping(bytes32 => mapping(IERC20 => bytes32)) internal _minimalSwapInfoPoolsBalances;\n\n    /**\n     * @dev Registers a list of tokens in a Minimal Swap Info Pool.\n     *\n     * Requirements:\n     *\n     * - Each token must not be the zero address.\n     * - Each token must not be registered in the Pool.\n     */\n    function _registerMinimalSwapInfoPoolTokens(bytes32 poolId, IERC20[] memory tokens) internal {\n        EnumerableSet.AddressSet storage poolTokens = _minimalSwapInfoPoolsTokens[poolId];\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            require(token != IERC20(0), \"ZERO_ADDRESS_TOKEN\");\n            bool added = poolTokens.add(address(token));\n            require(added, \"TOKEN_ALREADY_REGISTERED\");\n        }\n    }\n\n    /**\n     * @dev Deregisters a list of tokens in a Minimal Swap Info Pool.\n     *\n     * Requirements:\n     *\n     * - Each token must be registered in the Pool.\n     * - Each token must have non balance in the Vault.\n     */\n    function _deregisterMinimalSwapInfoPoolTokens(bytes32 poolId, IERC20[] memory tokens) internal {\n        EnumerableSet.AddressSet storage poolTokens = _minimalSwapInfoPoolsTokens[poolId];\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = tokens[i];\n            require(_minimalSwapInfoPoolsBalances[poolId][token].isZero(), \"NONZERO_TOKEN_BALANCE\");\n            bool removed = poolTokens.remove(address(token));\n            require(removed, \"TOKEN_NOT_REGISTERED\");\n            // No need to delete the balance entries, since they already are zero\n        }\n    }\n\n    function _setMinimalSwapInfoPoolBalances(\n        bytes32 poolId,\n        IERC20[] memory tokens,\n        bytes32[] memory balances\n    ) internal {\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            _minimalSwapInfoPoolsBalances[poolId][tokens[i]] = balances[i];\n        }\n    }\n\n    function _minimalSwapInfoPoolCashToManaged(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateMinimalSwapInfoPoolBalance(poolId, token, BalanceAllocation.cashToManaged, amount);\n    }\n\n    function _minimalSwapInfoPoolManagedToCash(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateMinimalSwapInfoPoolBalance(poolId, token, BalanceAllocation.managedToCash, amount);\n    }\n\n    function _setMinimalSwapInfoPoolManagedBalance(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateMinimalSwapInfoPoolBalance(poolId, token, BalanceAllocation.setManaged, amount);\n    }\n\n    function _updateMinimalSwapInfoPoolBalance(\n        bytes32 poolId,\n        IERC20 token,\n        function(bytes32, uint256) returns (bytes32) mutation,\n        uint256 amount\n    ) internal {\n        bytes32 currentBalance = _getMinimalSwapInfoPoolBalance(poolId, token);\n        _minimalSwapInfoPoolsBalances[poolId][token] = mutation(currentBalance, amount);\n    }\n\n    /**\n     * @dev Returns an array with all the tokens and balances in a Minimal Swap Info Pool.\n     * This order may change when tokens are added to or removed from the Pool.\n     */\n    function _getMinimalSwapInfoPoolTokens(bytes32 poolId)\n        internal\n        view\n        returns (IERC20[] memory tokens, bytes32[] memory balances)\n    {\n        EnumerableSet.AddressSet storage poolTokens = _minimalSwapInfoPoolsTokens[poolId];\n        tokens = new IERC20[](poolTokens.length());\n        balances = new bytes32[](tokens.length);\n\n        for (uint256 i = 0; i < tokens.length; ++i) {\n            IERC20 token = IERC20(poolTokens.at(i));\n            tokens[i] = token;\n            balances[i] = _minimalSwapInfoPoolsBalances[poolId][token];\n        }\n    }\n\n    /**\n     * @dev Returns the balance for a token in a Minimal Swap Info Pool.\n     *\n     * Requirements:\n     *\n     * - `token` must be in the Pool.\n     */\n    function _getMinimalSwapInfoPoolBalance(bytes32 poolId, IERC20 token) internal view returns (bytes32) {\n        bytes32 balance = _minimalSwapInfoPoolsBalances[poolId][token];\n        bool existsToken = balance.isNotZero() || _minimalSwapInfoPoolsTokens[poolId].contains(address(token));\n        require(existsToken, \"TOKEN_NOT_REGISTERED\");\n        return balance;\n    }\n\n    function _isMinimalSwapInfoPoolTokenRegistered(bytes32 poolId, IERC20 token) internal view returns (bool) {\n        EnumerableSet.AddressSet storage poolTokens = _minimalSwapInfoPoolsTokens[poolId];\n        return poolTokens.contains(address(token));\n    }\n}\n"
    },
    "contracts/vault/balances/TwoTokenPoolsBalance.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"@openzeppelin/contracts/token/ERC20/IERC20.sol\";\n\nimport \"./BalanceAllocation.sol\";\n\ncontract TwoTokenPoolsBalance {\n    using BalanceAllocation for bytes32;\n\n    // Data for Pools with Two Tokens\n    //\n    // These are similar to the Minimal Swap Info Pool case (because the Pool only has two tokens, and therefore there\n    // are only two balances to read), but there's a key difference in how data is stored. Keeping a set makes little\n    // sense, as it will only ever hold two tokens, so we can just store those two directly.\n    // The gas savings associated with using these Pools come from how token balances are stored: cash for token A and\n    // token B is packed together, as are external amounts. Because only cash changes in a swap, there's no need to\n    // write to this second storage slot.\n    // This however makes Vault code that interacts with these Pools cumbersome: both balances must be accessed at the\n    // same time by using both token addresses, and some logic is needed to differentiate token A from token B. In this\n    // case, token A is always the token with the lowest numerical address value. The token X and token Y names are used\n    // in functions when it is unknown which one is A and which one is B.\n\n    struct TwoTokenPoolBalances {\n        bytes32 sharedCash;\n        bytes32 sharedManaged;\n    }\n\n    struct TwoTokenPoolTokens {\n        IERC20 tokenA;\n        IERC20 tokenB;\n        mapping(bytes32 => TwoTokenPoolBalances) balances;\n    }\n\n    // We could just keep a mapping from Pool ID to TwoTokenSharedBalances, but there's an issue: we wouldn't know to\n    // which tokens those balances correspond. This would mean having to also check the tokens struct in a swap, to make\n    // sure the tokens being swapped are the ones in the Pool.\n    // What we do instead to save those storage reads is keep a nested mapping from token pair hash to the balances\n    // struct. The Pool only has two tokens, so only a single entry of this mapping is set (the one that corresponds to\n    // that pair's hash). This means queries for token pairs where any of the tokens is not in the Pool will generate a\n    // hash for a mapping entry that was not set, containing zero balances. Non-zero balances are only possible if both\n    // tokens in the pair are the Pool's tokens, which means we don't have to check the TwoTokensTokens struct and save\n    // storage reads.\n    mapping(bytes32 => TwoTokenPoolTokens) private _twoTokenPoolTokens;\n\n    /**\n     * @dev Registers the tokens of a Two Token Pool.\n     *\n     * Requirements:\n     *\n     * - `tokenX` and `tokenY` cannot be the same.\n     * - Both tokens must not be the zero address.\n     * - Both tokens must not be registered in the Pool.\n     * - Tokens must be ordered: tokenX < tokenY.\n     */\n    function _registerTwoTokenPoolTokens(\n        bytes32 poolId,\n        IERC20 tokenX,\n        IERC20 tokenY\n    ) internal {\n        require(tokenX != IERC20(0) && tokenY != IERC20(0), \"ZERO_ADDRESS_TOKEN\");\n\n        // Not technically true since we didn't register yet, but this is consistent with the error messages of other\n        // specialization settings.\n        require(tokenX != tokenY, \"TOKEN_ALREADY_REGISTERED\");\n        require(tokenX < tokenY, \"UNSORTED_TOKENS\");\n\n        TwoTokenPoolTokens storage poolTokens = _twoTokenPoolTokens[poolId];\n        require(poolTokens.tokenA == IERC20(0) && poolTokens.tokenB == IERC20(0), \"TOKENS_ALREADY_SET\");\n\n        poolTokens.tokenA = tokenX;\n        poolTokens.tokenB = tokenY;\n    }\n\n    /**\n     * @dev Deregisters the tokens of a Two Token Pool.\n     *\n     * Requirements:\n     *\n     * - `tokenX` and `tokenY` must be the Pool's tokens.\n     * - Both tokens must have non balance in the Vault.\n     */\n    function _deregisterTwoTokenPoolTokens(\n        bytes32 poolId,\n        IERC20 tokenX,\n        IERC20 tokenY\n    ) internal {\n        (bytes32 balanceA, bytes32 balanceB, ) = _getTwoTokenPoolSharedBalances(poolId, tokenX, tokenY);\n        require(balanceA.isZero() && balanceB.isZero(), \"NONZERO_TOKEN_BALANCE\");\n\n        delete _twoTokenPoolTokens[poolId];\n        // No need to delete the balance entries, since they already are zero\n    }\n\n    /**\n     * @dev This setter is only used when adding/removing liquidity, where tokens are guaranteed to be ordered and\n     * registered. These methods actually fetch the tokens from `_getTwoTokenPoolTokens` first.\n     */\n    function _setTwoTokenPoolCashBalances(\n        bytes32 poolId,\n        IERC20 tokenA,\n        bytes32 balanceA,\n        IERC20 tokenB,\n        bytes32 balanceB\n    ) internal {\n        bytes32 hash = _getTwoTokenPairHash(tokenA, tokenB);\n        TwoTokenPoolBalances storage poolBalances = _twoTokenPoolTokens[poolId].balances[hash];\n        poolBalances.sharedCash = BalanceAllocation.toSharedCash(balanceA, balanceB);\n    }\n\n    function _twoTokenPoolCashToManaged(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateTwoTokenPoolSharedBalance(poolId, token, BalanceAllocation.cashToManaged, amount);\n    }\n\n    function _twoTokenPoolManagedToCash(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateTwoTokenPoolSharedBalance(poolId, token, BalanceAllocation.managedToCash, amount);\n    }\n\n    function _setTwoTokenPoolManagedBalance(\n        bytes32 poolId,\n        IERC20 token,\n        uint256 amount\n    ) internal {\n        _updateTwoTokenPoolSharedBalance(poolId, token, BalanceAllocation.setManaged, amount);\n    }\n\n    function _updateTwoTokenPoolSharedBalance(\n        bytes32 poolId,\n        IERC20 token,\n        function(bytes32, uint256) returns (bytes32) mutation,\n        uint256 amount\n    ) private {\n        (\n            TwoTokenPoolBalances storage balances,\n            IERC20 tokenA,\n            bytes32 balanceA,\n            IERC20 tokenB,\n            bytes32 balanceB\n        ) = _getTwoTokenPoolBalances(poolId);\n\n        if (token == tokenA) {\n            balanceA = mutation(balanceA, amount);\n        } else if (token == tokenB) {\n            balanceB = mutation(balanceB, amount);\n        } else {\n            revert(\"TOKEN_NOT_REGISTERED\");\n        }\n\n        balances.sharedCash = BalanceAllocation.toSharedCash(balanceA, balanceB);\n        balances.sharedManaged = BalanceAllocation.toSharedManaged(balanceA, balanceB);\n    }\n\n    /**\n     * @dev Returns the balance for a token pair in a Two Token Pool, reverting if either of the tokens is\n     * not registered by the Pool.\n     *\n     * The returned balances are those of token A and token B, where token A is the lowest of token X and token Y, and\n     * token B the other.\n     *\n     * This function also returns a storage pointer to the TwoTokenSharedBalances entry associated with the token pair,\n     * which can be used to update this entry without having to recompute the pair hash and storage slot.\n     */\n    function _getTwoTokenPoolSharedBalances(\n        bytes32 poolId,\n        IERC20 tokenX,\n        IERC20 tokenY\n    )\n        internal\n        view\n        returns (\n            bytes32 balanceA,\n            bytes32 balanceB,\n            TwoTokenPoolBalances storage poolBalances\n        )\n    {\n        (IERC20 tokenA, IERC20 tokenB) = _sortTwoTokens(tokenX, tokenY);\n        bytes32 pairHash = _getTwoTokenPairHash(tokenA, tokenB);\n        poolBalances = _twoTokenPoolTokens[poolId].balances[pairHash];\n\n        bytes32 sharedCash = poolBalances.sharedCash;\n        bytes32 sharedManaged = poolBalances.sharedManaged;\n\n        // Only registered tokens can have non-zero balances, so we can use this as a shortcut to avoid the\n        // expensive _hasPoolTwoTokens check.\n        bool exists = sharedCash.isNotZero() || sharedManaged.isNotZero() || _hasPoolTwoTokens(poolId, tokenA, tokenB);\n        require(exists, \"TOKEN_NOT_REGISTERED\");\n\n        balanceA = BalanceAllocation.fromSharedToBalanceA(sharedCash, sharedManaged);\n        balanceB = BalanceAllocation.fromSharedToBalanceB(sharedCash, sharedManaged);\n    }\n\n    /**\n     * @dev Returns an array with all the tokens and balances in a Two Token Pool.\n     * This array will have either two or zero entries (if the Pool doesn't have any tokens).\n     */\n    function _getTwoTokenPoolTokens(bytes32 poolId)\n        internal\n        view\n        returns (IERC20[] memory tokens, bytes32[] memory balances)\n    {\n        (, IERC20 tokenA, bytes32 balanceA, IERC20 tokenB, bytes32 balanceB) = _getTwoTokenPoolBalances(poolId);\n\n        // Both tokens will either be zero or non-zero, but we keep the full check for clarity\n        if (tokenA == IERC20(0) || tokenB == IERC20(0)) {\n            return (new IERC20[](0), new bytes32[](0));\n        }\n\n        // Note that functions relying on this getter expect tokens to be properly ordered, so it's extremely important\n        // to always return (A,B)\n\n        tokens = new IERC20[](2);\n        tokens[0] = tokenA;\n        tokens[1] = tokenB;\n\n        balances = new bytes32[](2);\n        balances[0] = balanceA;\n        balances[1] = balanceB;\n    }\n\n    /**\n     * @dev Returns the balance for a token in a Two Token Pool.\n     *\n     * This function is convenient but not particularly gas efficient, and should be avoided during gas-sensitive\n     * operations, such as swaps. For those, _getTwoTokenPoolSharedBalances provides a more flexible interface.\n     *\n     * Requirements:\n     *\n     * - `token` must be in the Pool.\n     */\n    function _getTwoTokenPoolBalance(bytes32 poolId, IERC20 token) internal view returns (bytes32 balance) {\n        // We can't just read the balance of token, because we need to know the full pair in order to compute the pair\n        // hash and access the balance mapping. We therefore also read the TwoTokenPoolTokens struct.\n        (, IERC20 tokenA, bytes32 balanceA, IERC20 tokenB, bytes32 balanceB) = _getTwoTokenPoolBalances(poolId);\n\n        if (token == tokenA) {\n            return balanceA;\n        } else if (token == tokenB) {\n            return balanceB;\n        } else {\n            revert(\"TOKEN_NOT_REGISTERED\");\n        }\n    }\n\n    function _hasPoolTwoTokens(\n        bytes32 poolId,\n        IERC20 tokenA,\n        IERC20 tokenB\n    ) internal view returns (bool) {\n        TwoTokenPoolTokens storage poolTokens = _twoTokenPoolTokens[poolId];\n        return poolTokens.tokenA == tokenA && tokenB == poolTokens.tokenB;\n    }\n\n    function _isTwoTokenPoolTokenRegistered(bytes32 poolId, IERC20 token) internal view returns (bool) {\n        TwoTokenPoolTokens storage poolTokens = _twoTokenPoolTokens[poolId];\n        return (token == poolTokens.tokenA || token == poolTokens.tokenB) && token != IERC20(0);\n    }\n\n    function _getTwoTokenPoolBalances(bytes32 poolId)\n        private\n        view\n        returns (\n            TwoTokenPoolBalances storage poolBalances,\n            IERC20 tokenA,\n            bytes32 balanceA,\n            IERC20 tokenB,\n            bytes32 balanceB\n        )\n    {\n        TwoTokenPoolTokens storage poolTokens = _twoTokenPoolTokens[poolId];\n        tokenA = poolTokens.tokenA;\n        tokenB = poolTokens.tokenB;\n\n        bytes32 pairHash = _getTwoTokenPairHash(tokenA, tokenB);\n        poolBalances = poolTokens.balances[pairHash];\n\n        bytes32 sharedCash = poolBalances.sharedCash;\n        bytes32 sharedManaged = poolBalances.sharedManaged;\n\n        balanceA = BalanceAllocation.fromSharedToBalanceA(sharedCash, sharedManaged);\n        balanceB = BalanceAllocation.fromSharedToBalanceB(sharedCash, sharedManaged);\n    }\n\n    /**\n     * @dev Returns a hash associated with a given token pair.\n     */\n    function _getTwoTokenPairHash(IERC20 tokenA, IERC20 tokenB) private pure returns (bytes32) {\n        return keccak256(abi.encodePacked(tokenA, tokenB));\n    }\n\n    /**\n     * @dev Sorts two tokens ascendingly, returning them as a (tokenA, tokenB) tuple.\n     */\n    function _sortTwoTokens(IERC20 tokenX, IERC20 tokenY) private pure returns (IERC20, IERC20) {\n        return tokenX < tokenY ? (tokenX, tokenY) : (tokenY, tokenX);\n    }\n}\n"
    },
    "contracts/vault/balances/InternalBalanceAllocation.sol": {
      "content": "// SPDX-License-Identifier: GPL-3.0-or-later\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU General Public License for more details.\n\n// You should have received a copy of the GNU General Public License\n// along with this program.  If not, see <http://www.gnu.org/licenses/>.\n\npragma solidity ^0.7.0;\n\nimport \"../../lib/math/Math.sol\";\n\nlibrary InternalBalanceAllocation {\n    using Math for uint256;\n\n    /**\n     * @dev Returns the actual amount of internal balance.\n     */\n    function actual(bytes32 balance) internal pure returns (uint256) {\n        uint256 mask = 2**(112) - 1;\n        return uint256(balance) & mask;\n    }\n\n    /**\n     * @dev Returns the exempt amount of internal balance.\n     */\n    function exempt(bytes32 balance) internal pure returns (uint256) {\n        uint256 mask = 2**(112) - 1;\n        return uint256(balance >> 112) & mask;\n    }\n\n    /**\n     * @dev Returns the last block number when an internal balance exempt was cached.\n     */\n    function blockNumber(bytes32 balance) internal pure returns (uint256) {\n        uint256 mask = 2**(32) - 1;\n        return uint256(balance >> 224) & mask;\n    }\n\n    /**\n     * @dev Increases an internal balance. It can also track the internal balance exempt if requested.\n     * In case it is, it will compare the current block number with the last one cached to see if it should\n     * reset the exempt amount or increment it. The exempt amount will always be considered to compute\n     * the taxable amount when an internal balance is decreased\n     */\n    function increase(\n        bytes32 balance,\n        uint256 amount,\n        bool trackExempt\n    ) internal view returns (bytes32) {\n        uint256 newActual = actual(balance).add(amount);\n        uint256 newExempt = exempt(balance);\n        uint256 newBlockNumber = blockNumber(balance);\n\n        if (trackExempt) {\n            if (newBlockNumber == block.number) {\n                newExempt = newExempt.add(amount);\n            } else {\n                newExempt = amount;\n                newBlockNumber = block.number;\n            }\n        }\n\n        return toInternalBalance(newActual, newExempt, newBlockNumber);\n    }\n\n    /**\n     * @dev Decreases an internal balance. In case `capped` was requested, it will be decreased as much as possible.\n     * @return newBalance the new balance obtained after decreasing it\n     * @return taxableAmount the amount that should be used to charge fees considering any existing exempts\n     * @return decreasedAmount the amount the was actually deducted from the internal balance\n     */\n    function decrease(\n        bytes32 balance,\n        uint256 amount,\n        bool capped\n    )\n        internal\n        view\n        returns (\n            bytes32,\n            uint256,\n            uint256\n        )\n    {\n        uint256 currentActual = actual(balance);\n        require(capped || (currentActual >= amount), \"INSUFFICIENT_INTERNAL_BALANCE\");\n\n        // We know the decreased amount will be always the minimum between the actual value and the given amount.\n        // If the given amount was greater than the actual value and it wasn't requested to be capped, then it\n        // was caught by the require above\n        uint256 decreased = Math.min(currentActual, amount);\n        uint256 newActual = currentActual - decreased;\n\n        uint256 lastBlockNumber = blockNumber(balance);\n        if (lastBlockNumber == block.number) {\n            // A user could be decreasing its internal balance by a number greater than its exempt value.\n            // Then we should always do a sub capped to zero.\n            uint256 currentExempt = exempt(balance);\n            uint256 newExempt = currentExempt > amount ? currentExempt - amount : 0;\n            uint256 taxableAmount = currentExempt > amount ? 0 : amount - currentExempt;\n            bytes32 newBalance = toInternalBalance(newActual, newExempt, lastBlockNumber);\n            return (newBalance, taxableAmount, decreased);\n        } else {\n            // Note that we consider the case where the current block number doesn't match with the last one as a\n            // regular decrease. We cannot handle negative exempt values, it would be like \"credit\" for potential\n            // future ops in the same block.\n            bytes32 newBalance = toInternalBalance(newActual, 0, 0);\n            return (newBalance, amount, decreased);\n        }\n    }\n\n    /**\n     * @dev Packs together actual and exempt amounts with a block number to create a balance value.\n     * Critically, this also checks both amounts can be packed together in the same slot.\n     */\n    function toInternalBalance(\n        uint256 _actual,\n        uint256 _exempt,\n        uint256 _blockNumber\n    ) internal pure returns (bytes32) {\n        require(_actual < 2**112 && _exempt < 2**112, \"INTERNAL_BALANCE_OVERFLOW\");\n        // We assume the block number will fits in an uint32 - this is expected to hold for at least a few decades.\n        return _pack(_actual, _exempt, _blockNumber);\n    }\n\n    /**\n     * @dev Packs together two uint112 and one uint32 into a bytes32\n     */\n    function _pack(\n        uint256 _leastSignificant,\n        uint256 _midSignificant,\n        uint256 _mostSignificant\n    ) private pure returns (bytes32) {\n        return bytes32((_mostSignificant << 224) + (_midSignificant << 112) + _leastSignificant);\n    }\n}\n"
    }
  },
  "settings": {
    "optimizer": {
      "enabled": true,
      "runs": 400
    },
    "debug": {
      "revertStrings": "strip"
    },
    "outputSelection": {
      "*": {
        "*": [
          "abi",
          "evm.bytecode",
          "evm.deployedBytecode",
          "evm.methodIdentifiers"
        ],
        "": [
          "ast"
        ]
      }
    }
  }
}